!<
    ==================================
    Global Trade Analysis Project
    The GTAP Modeling Framework
    GTAP Version 6.2
    September 2003
    ==================================

    For documentation see:
    (a) Hertel, T.W. and M.E. Tsigas, "Structure of the Standard GTAP Model",
    Chapter 2 in T.W. Hertel (editor) "Global Trade Analysis: Modeling and
    Applications", Cambridge University Press, 1997.

    (b) R.A. McDougall, "A New Regional Household Demand System for GTAP",
    GTAP Technical Paper #20, Center for Global Trade Analysis, Purdue
    University

    (c) Hertel T.W., K. Itakura and R.A. McDougall, "GTAP.TAB: The Standard
    GTAP Model" (yet to be drafted), GTAP Technical Paper, Center for Global
    Trade Analysis, Purdue University,
    http://www.agecon.purdue.edu/gtap/techpapr/.  This technical paper
    documents all of the changes since the GTAP book TAB file.


    --------------------
    HISTORY of this file
    --------------------

    Version 6.2 fixes bugs and provides minor enhancements:
        - EV_ALT, CNTdpar fix
        - SX_IR fix
        - new treatment of export and import tariffs
        - original levels defined for power-of-tax variables
        - CNTalleffir range extended from TRAD_COMM to DEMD_COMM
        - INCOME formula, quantity index equations more AnalyzeGE-friendly

    Version 6.1 fixes bugs in 6.0:
        - Computation of tax receipts ratio is now separately done for total
          tax and indirect tax (equation DTAXRATIO and DINDTAXRATIO).
        - Computation of DTBALR, the ratio of the trade balance to regional
          income, has been corrected (equation DTBALRATIO).
        - Computation of EV_ALT, the summary variable for EV, has been
          corrected (equation EV_DECOMPOSITION).
    Also the notation has been changed for the tax-income ratio variables:
        TGCR -> del_taxrgc,    TPCR -> del_taxrpc,    TIUR -> del_taxriu,
        TFUR -> del_taxrfu,    TOUTR -> del_taxrout,  TEXPR -> del_taxrexp,
        TIMPR -> del_taxrimp,  TINCR -> del_taxrinc,  DTAXR -> del_ttaxr.

    Version 6.0 replaces version 5.0 used in the August 2000 short course, but
    never formally released.  The subsequent changes have been quite
    significant (primarily focused on final demand and welfare decomposition),
    so we decided to give it a new version number.  This also prevents
    confusion with the soon to be released version 5.0 GTAP data set.

    The differences with the previous publicly released version of GTAP.TAB,
    Version 4.1, dated November 1998, are numerous.  One goal of this version
    is to reorganize the TAB file which had not be redesigned since its
    inception.  In addition, there is a long list of substantive changes that
    have been made between Version 4.1 and 6.0.  These include:

        - provision for multiple margins sectors (to accommodate post version
          5 GTAP data)
        - introduction of the new theory of final demand following McDougall's
          GTAP Technical Paper #20
        - modification of the welfare decomposition following McDougall's
          Technical Paper #20
        - correction to the treatment of technical change, ao, in the presence
          of ESUBT > 0 (affects equations VADEMAND and INTDEMAND)
        - addition of import-augmenting "technical change" in the Armington
          nest: ams which can be used to lower the effective price of imported
          products
        - specification of Baldwin-type capital accmulation effects following
          Francois' Technical Paper #7
        - introduction of uniform consumption tax instrument, tp(r)


    ----------------------------------
    Overview of the GTAP.TAB Structure
    ----------------------------------
    Preliminaries:
        FILES
        SETS
        READ statements of Base Data
        Common VARIABLES
        Common COEFFICIENTS

    Modules:
        1.  Government Consumption
        2.  Private Consumption
        3.  Firms
        4.  Investment, Global Bank, and Savings
        5.  International Trade
        6.  International Transport Services
        7.  Regional Household
        8.  Equilibrium Conditions

    Appendices:
        A.  Summary Indices
        B.  Equivalent Variation
        C.  Welfare Decomposition
        D.  Terms of Trade Decomposition
>!


!<
    -----
    FILES
    -----
>!

File
    GTAPSETS # file with set specification #;
File
    GTAPDATA # file containing all base data #;
File
    GTAPPARM # file containing behavioral parameters #;

! EC edits for new shocks !
File 
    GTAPSUPP # Supplementary file with additional set and mapping #;
File 
    (new) GTAPSUM # Summary results file #;
! !
!<
    ----
    SETS
    ----

    Sets define relevant groupings of entities over which we will be
    performing operations in the model.  SUBSETS are defined in order to
    facilitate summation over only a portion of a given group, e.g., tradeable
    commodities.  Users with access to GEMPACK can increase the maximum set
    sizes to permit larger aggregations, provided you have sufficient RAM for
    implementing them The standard model (10x10 aggregation with decomposition
    code) is designed to run with 16 Mb of RAM.


    Aide-Memoire for Sets
    ---------------------

    |--------------------------------------|
    |         DEMD_COMM                    |
    |----------------------------------------------------|
    | ENDW_COMM |  LAND_COMM |  NLAND_COMM |  CGDS_COMM  |
    |----------------------------------------------------|
    |        NSAV_COMM                                   |
    |----------------------------------------------------|
                |                     PROD_COMM          |
                |----------------------------------------|

    For Endowments,
    |-------------------------|
    | ENDW_COMM               |
    |-------------------------|
    | ENDWM_COMM | ENDWS_COMM |
    |-------------------------|
    | NAEZ_COMM    | AEZ_COMM |
    |-------------------------|
>!


Coefficient
    VERNUM # version of GTAP data #;
Read
    VERNUM from file GTAPDATA header "DVER";
Update (change)
    VERNUM = 0.0; ! force it to be copied to update file !

Set
    REG # regions in the model #
    maximum size 10 read elements from file GTAPSETS header "H1";
Set
    TRAD_COMM # traded commodities #
    maximum size 10 read elements from file GTAPSETS header "H2";


Set
    MARG_COMM # margin commodities #
    maximum size 10 read elements from file GTAPSETS header "MARG";
Subset
    MARG_COMM is subset of TRAD_COMM;
Set
    NMRG_COMM # non-margin commodities # = TRAD_COMM - MARG_COMM;
Set
    CGDS_COMM # capital goods commodities #
    maximum size 1 read elements from file GTAPSETS header "H9";
Set
    ENDW_COMM # endowment commodities #
    maximum size 5 read elements from file GTAPSETS header "H6";
Set
    PROD_COMM # produced commodities # = TRAD_COMM union CGDS_COMM;
Set
    DEMD_COMM # demanded commodities # = ENDW_COMM union TRAD_COMM;
Set
    NSAV_COMM # non-savings commodities # = DEMD_COMM union CGDS_COMM;
Subset
    PROD_COMM is subset of NSAV_COMM;

!<
    Check for non-overlapping sets (users can't use the same name for elements
    of ENDW_COMM, TRAD_COMM or CGDS_COMM).
>!

Coefficient
    SIZE_TRAD # size of TRAD_COMM set #;
Formula
    SIZE_TRAD = sum(i,TRAD_COMM, 1);
Coefficient
    SIZE_ENDW # size of ENDW_COMM set #;
Formula
    SIZE_ENDW = sum(i,ENDW_COMM, 1);
Coefficient
    SIZE_DEMD # size of DEMD_COMM set #;
Formula
    SIZE_DEMD = sum(i,DEMD_COMM, 1);
Assertion (initial)
    SIZE_DEMD = SIZE_TRAD + SIZE_ENDW;

Coefficient
    SIZE_CGDS # size of CGDS_COMM set #;
Formula
    SIZE_CGDS = sum(i,CGDS_COMM, 1);
Coefficient
    SIZE_PROD # size of PROD_COMM set #;
Formula
    SIZE_PROD = sum(i,PROD_COMM, 1);
Assertion (initial)
    SIZE_PROD = SIZE_TRAD + SIZE_CGDS;

!<
    The sluggish endowments are now defined dynamically, based on the variable
    SLUG.  SLUG is a binary variable that is zero for mobile endowments and
    one for sluggish endowments.  We must define and read in this variable
    before proceeding further.
>!
Coefficient (integer,parameter) (all,i,ENDW_COMM)
    SLUG(i) # sluggish primary factor endowments #;
Read
    SLUG from file GTAPPARM header "SLUG";

Set
    ENDWS_COMM
    # sluggish endowment commodities # = (all,i,ENDW_COMM: SLUG(i)>0);
Set
    ENDWM_COMM
    # mobile endowment commodities # = ENDW_COMM - ENDWS_COMM;
Set
    ENDWC_COMM # capital endowment commodity # (capital);
!<
    This single element set is defined for the sake of convenience.
>!
Subset
    ENDWC_COMM is subset of ENDW_COMM;

!< GTAP-AEZ SETS!

!< GTAP-AEZ SETS:
Modeling agricultural demand for land (splitted in AEZs) requires breaking down
the set of produced commodities (PROD_COMM) in land and non-land using produced
commodities. The land using commodities, in turn, are broken down in forests,
crops, and pasture-grown animals.
 
A potential problem is that each of these sets can have different elements, de-
pending ond the desired aggregation. For example, the first 13  GTAP sectors use
land, so they are part of LAND_COMM. It is possible, that these 13 products are
arbitrarily grouped, so we need a flexible mechanism that recognizes
this and assigns the arbitray grouping to LAND_COMM. We accomplish this by
defining the sets dynamically.>!
 
!<Dynamic definition of the set of land-commodities>!
 
Set !Typically this set is not read, but I need it for dynamic set definitions!
    DTRAD_COMM # traded commodities #
    maximum size 10 read elements from file GTAPSETS header "DH2";
 
!Sectoral mapping from GTAP to model TRAD_COMM commodities is needed for other
mapping subsets of TRAD_COMM onto the land comm, forests, crops, graze, etc.!
 
Mapping G2TRAD_COMM from DTRAD_COMM to TRAD_COMM;
Read (BY_ELEMENTS) G2TRAD_COMM from file GTAPSETS header "MCOM";
 
!<Dynamic Agregated Land Commodity Set Definition>!
Set
    DLAND_COMM #Land commodities in GTAP Version 7 that have Land rents#
    (pdr,wht,gro,v_f,osd,c_b,pfb,ocr,ctl,rmk,wol,frs);
 
Subset DLAND_COMM is subset of DTRAD_COMM;
 
COEFFICIENT (all,c,DTRAD_COMM) UNITVEC0(c) #Auxiliary unit vector#;
FORMULA (all,c,DTRAD_COMM) UNITVEC0(c)=1;
 
COEFFICIENT (all,i,TRAD_COMM) ISLAND(i) #Is a land Commodity?#;
FORMULA (all,i,TRAD_COMM)
ISLAND(i)= sum{c, DLAND_COMM: G2TRAD_COMM(c) eq i, UNITVEC0(c) };
 
SET LAND_COMM = (all, i, TRAD_COMM : ISLAND(i)>0);
 
MAPPING G2LAND_COMM from  DLAND_COMM to LAND_COMM;
FORMULA (all,c,DLAND_COMM) G2LAND_COMM(c)=G2TRAD_COMM(c);
Subset
    LAND_COMM is subset of PROD_COMM;
!<End of Dynamic Agregated Land Commodity Set Definition>!
 
Set
    NLNDP_COMM # non-land produced commodities # = PROD_COMM - LAND_COMM;
 
!<Other sets needed in the land nests>!
 
 
!<Dynamic Agregated Crop Commodity Set Definition>!
Set
    DCROP_COMM #The GTAP Crops#  (pdr,wht,gro,v_f,osd,c_b,pfb,ocr);
Subset DCROP_COMM is subset of DTRAD_COMM;
Subset DCROP_COMM is subset of DLAND_COMM;
 
COEFFICIENT (all,c,DTRAD_COMM) UNITVEC(c) #Auxiliary unit vector#;
FORMULA (all,c,DTRAD_COMM) UNITVEC(c)=1;
 
COEFFICIENT (all,i,TRAD_COMM) ISCROP(i) #Is a crop Commodity?#;
FORMULA (all,i,TRAD_COMM)
ISCROP(i)= sum{c, DCROP_COMM: G2TRAD_COMM(c) eq i, UNITVEC(c) };
 
SET CROP_COMM = (all, i, TRAD_COMM : ISCROP(i)>0);
 
MAPPING G2CROP_COMM from  DCROP_COMM to CROP_COMM;
FORMULA (all,c,DCROP_COMM) G2CROP_COMM(c)=G2TRAD_COMM(c);
Subset
    CROP_COMM is subset of PROD_COMM;
Subset
    CROP_COMM is subset of LAND_COMM;
!<End of Dynamic Agregated Crop Commodity Set Definition>!
 
!<Dynamic Agregated Forest Commodity Set Definition>!
Set    DFOREST_COMM #The GTAP Forests# (frs);
Subset DFOREST_COMM is subset of DTRAD_COMM;
Subset DFOREST_COMM is subset of DLAND_COMM;
 
COEFFICIENT (all,i,TRAD_COMM) ISFOR(i) #Is a Forest Commodity?#;
FORMULA (all,i,TRAD_COMM)
ISFOR(i)= sum{c, DFOREST_COMM: G2TRAD_COMM(c) eq i, UNITVEC(c) };
 
SET FOREST_COMM = (all, i, TRAD_COMM : ISFOR(i)>0);
 
MAPPING G2FOREST_COM from  DFOREST_COMM to FOREST_COMM;
FORMULA (all,c,DFOREST_COMM) G2FOREST_COM(c)=G2TRAD_COMM(c);
Subset
    FOREST_COMM is subset of PROD_COMM;
Subset
    FOREST_COMM is subset of LAND_COMM;
!<End of Dynamic Agregated Forest Commodity Set Definition>!
 
 
!<Dynamic Agregated Grazing Commodity Set Definition>!
Set
    DGRAZE_COMM #The GTAP grazing livestock commodities#
        (ctl,rmk,wol);
Subset DGRAZE_COMM is subset of DTRAD_COMM;
Subset DGRAZE_COMM is subset of DLAND_COMM;
 
COEFFICIENT (all,i,TRAD_COMM) ISGRAZE(i) #Is a Grazing Commodity?#;
FORMULA (all,i,TRAD_COMM)
ISGRAZE(i)= sum{c, DGRAZE_COMM: G2TRAD_COMM(c) eq i, UNITVEC(c) };
 
SET GRAZE_COMM = (all, i, TRAD_COMM : ISGRAZE(i)>0);
 
MAPPING G2GRAZE_COMM from  DGRAZE_COMM to GRAZE_COMM;
FORMULA (all,c,DGRAZE_COMM) G2GRAZE_COMM(c)=G2TRAD_COMM(c);
Subset
    GRAZE_COMM is subset of PROD_COMM;
Subset
    GRAZE_COMM is subset of LAND_COMM;
!<End of Dynamic Agregated Grazing Commodity Set Definition>!
 
Set
    AGRLAND_COMM # land-using agr commodities # = LAND_COMM - FOREST_COMM;
 
Set !This is NLNDP_COMM + FOREST_COMM  -- see equatio FORLAND_SUPPLY!
    NAGLAND_COMM # non-agr land produced commodities # =
    PROD_COMM - AGRLAND_COMM;
 
!<In turn, the land using commodities are produced using land (AEZz) and non-
land endowments. Because we're modeling competition between AEZs directly we
need to breakdown the set ENDW_COMM in AEZ_COMM and NAEZ_COMM>!
Set
    AEZ_COMM # set of land endowments, disaggregated by AEZ #
    maximum size 18 read elements from file GTAPSETS header "AEZS";
Subset
    AEZ_COMM is subset of ENDW_COMM;
Subset
    AEZ_COMM is subset of ENDWS_COMM;
Set
    NAEZ_COMM # set of non-aez endowments # = ENDW_COMM - AEZ_COMM;
!<The supply of sluggish commodities by the regional household (ENDWS_COMM)
also need to be differentiated between land and non-land endowments>!
Set
    SNAEZ_COMM # non-land, sluggish commodities # = ENDWS_COMM - AEZ_COMM;
Subset
    SNAEZ_COMM is subset of NAEZ_COMM;

Set 
    NCROP_COMM # non-crop commodities produced using land # =
    PROD_COMM - CROP_COMM;

!<END OF GTAP-AEZ SETS>!

!<END OF GTAP-AEZ SETS>!

!<
    ----------------------------
    READ Statements of Base Data
    ----------------------------

    We read in here almost all the base data, and define variables and
    coefficients associated with them.  A few data arrays used each in a
    single module are read in those modules: VKB, VTMFSD, and DPARSUM.

    The READ statements are divided into six sections:

    Saving
    Government Consumption
    Private Consumption
    Firms
    Global Bank
    International Trade and Transport

    Since these are invariant for each solution of the model, they are termed
    coefficients.  Coefficients are assigned upper case to distinguish them
    from variables.  (This is purely cosmetic, as GEMPACK is not
    case-sensitive.) Variables in GEMPACK are assigned lower case labels to
    denote the fact that they are percentage changes.  In some cases, original
    levels values for selected variables are defined to permit the user to
    compare post-simulation levels values across several simulations.

    The updating command indicates how the new level of the coefficient will
    be computed based on the previous solution of the linearized equations.
    Note that the notation used in the update commands is a shorthand for
    total differentials of these coefficient values.  Thus, w * v indicates
    that we want to take the total differential of W * V, plug in the
    calculated values of w and v, and add this to the base level in order to
    obtain a revised value for this product.
>!


!<
    Saving
    ------
>!

Variable (all,r,REG)
    psave(r) # price of savings in region r #;
Variable (all,r,REG)
    qsave(r) # regional demand for NET savings #;
Coefficient (all,r,REG)
    SAVE(r) # expenditure on NET savings in region r valued at agent's prices #;
Update (all,r,REG)
    SAVE(r) = psave(r) * qsave(r);
Read
    SAVE from file GTAPDATA header "SAVE";


!<
    Government Consumption
    ----------------------
>!

Variable (all,i,TRAD_COMM)(all,s,REG)
    pgd(i,s) # price of domestic i in government consumption in s #;
Variable (orig_level=VDGM)(all,i,TRAD_COMM)(all,s,REG)
    qgd(i,s) # government hhld demand for domestic i in region s #;
Coefficient (ge 0)(all,i,TRAD_COMM)(all,r,REG)
    VDGA(i,r) # government consumption expenditure on domestic i in r #;
Update (all,i,TRAD_COMM)(all,r,REG)
    VDGA(i,r) = pgd(i,r) * qgd(i,r);
Read
    VDGA from file GTAPDATA header "VDGA";

Variable (orig_level=1.0)(all,i,NSAV_COMM)(all,r,REG)
    pm(i,r) # market price of commodity i in region r #;
Coefficient (ge 0)(all,i,TRAD_COMM)(all,r,REG)
    VDGM(i,r) # government consumption expenditure on domestic i in r #;
Update (all,i,TRAD_COMM)(all,r,REG)
    VDGM(i,r) = pm(i,r) * qgd(i,r);
Read
    VDGM from file GTAPDATA header "VDGM";

Variable (all,i,TRAD_COMM)(all,s,REG)
    pgm(i,s) # price of imports of i in government consumption in s #;
Variable (orig_level=VIGM)(all,i,TRAD_COMM)(all,s,REG)
    qgm(i,s) # government hhld demand for imports of i in region s #;
Coefficient (ge 0)(all,i,TRAD_COMM)(all,r,REG)
    VIGA(i,r) # government consumption expenditure on imported i #;
Update (all,i,TRAD_COMM)(all,r,REG)
    VIGA(i,r) = pgm(i,r) * qgm(i,r);
Read
    VIGA from file GTAPDATA header "VIGA";

Variable (orig_level=1.0)(all,i,TRAD_COMM)(all,r,REG)
    pim(i,r) # market price of composite import i in region r #;
Coefficient (ge 0)(all,i,TRAD_COMM)(all,r,REG)
    VIGM(i,r) # gov't consumption expenditure on i in r #;
Update (all,i,TRAD_COMM)(all,r,REG)
    VIGM(i,r) = pim(i,r) * qgm(i,r);
Read
    VIGM from file GTAPDATA header "VIGM";


!<
    Private Consumption
    -------------------
>!

Variable (all,i,TRAD_COMM)(all,s,REG)
    ppd(i,s) # price of domestic i to private households in s #;
Variable (orig_level=VDPM)(all,i,TRAD_COMM)(all,s,REG)
    qpd(i,s) # private hhld demand for domestic i in region s #;
Coefficient (ge 0)(all,i,TRAD_COMM)(all,r,REG)
    VDPA(i,r) # private consumption expenditure on domestic i in r #;
Update (all,i,TRAD_COMM)(all,r,REG)
    VDPA(i,r) = ppd(i,r) * qpd(i,r);
Read
    VDPA from file GTAPDATA header "VDPA";

Coefficient (ge 0)(all,i,TRAD_COMM)(all,r,REG)
    VDPM(i,r) # private consumption expenditure on domestic i in r #;
Update (all,i,TRAD_COMM)(all,r,REG)
    VDPM(i,r) = pm(i,r) * qpd(i,r);
Read
    VDPM from file GTAPDATA header "VDPM";

Variable (all,i,TRAD_COMM)(all,s,REG)
    ppm(i,s) # price of imports of i by private households in s #;
Variable (orig_level=VIPM)(all,i,TRAD_COMM)(all,s,REG)
    qpm(i,s) # private hhld demand for imports of i in region s #;
Coefficient (ge 0)(all,i,TRAD_COMM)(all,r,REG)
    VIPA(i,r) # private consumption expenditure on imported i in r #;
Update (all,i,TRAD_COMM)(all,r,REG)
    VIPA(i,r) = ppm(i,r) * qpm(i,r);
Read
    VIPA from file GTAPDATA header "VIPA";

Coefficient (ge 0)(all,i,TRAD_COMM)(all,r,REG)
    VIPM(i,r) # private consumption expenditure on i in r #;
Update (all,i,TRAD_COMM)(all,r,REG)
    VIPM(i,r)= pim(i,r) * qpm(i,r);
Read
    VIPM from file GTAPDATA header "VIPM";


!<
    Firms
    -----
>!

Variable (all,i,NSAV_COMM)(all,r,REG)
    ps(i,r) # supply price of commodity i in region r #;
Variable (orig_level=VOM)(all,i,NSAV_COMM)(all,r,REG)
    qo(i,r) # industry output of commodity i in region r #;
Coefficient (ge 0)(all,i,ENDW_COMM)(all,r,REG)
    EVOA(i,r) # value of commodity i output in region r #;
Update (all,i,ENDW_COMM)(all,r,REG)
    EVOA(i,r) = ps(i,r) * qo(i,r);
Read
    EVOA from file GTAPDATA header "EVOA";

Variable (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    pfe(i,j,r) # firms' price for endowment commodity i in ind. j, region r #;
Variable (orig_level=VFM)(all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    qfe(i,j,r)  # demand for endowment i for use in ind. j in region r #;
Coefficient (ge 0)(all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    EVFA(i,j,r) # producer expenditure on i by j in r at agent's prices #;
Update (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    EVFA(i,j,r) = pfe(i,j,r) * qfe(i,j,r);
Read
    EVFA from file GTAPDATA header "EVFA";

Variable (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    pfd(i,j,s) # price index for domestic purchases of i by j in region s #;
Variable (orig_level=VDFM)(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    qfd(i,j,s)  # domestic good i demanded by industry j in region s #;
Coefficient (ge 0)(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VDFA(i,j,r) # purchases of domestic i for use by j in region r #;
Update (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VDFA(i,j,r) = pfd(i,j,r) * qfd(i,j,r);
Read
    VDFA from file GTAPDATA header "VDFA";

Variable (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    pfm(i,j,s) # price index for imports of i by j in region s #;
Variable (orig_level=VIFM)(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    qfm(i,j,s) # demand for i by industry j in region s #;
Coefficient (ge 0)(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VIFA(i,j,r) # purchases of imported i for use by j in region r #;
Update (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VIFA(i,j,r) = pfm(i,j,r) * qfm(i,j,r);
Read
    VIFA from file GTAPDATA header "VIFA";

Variable (all,i,ENDWS_COMM)(all,j,PROD_COMM)(all,r,REG)
    pmes(i,j,r) # market price of sluggish endowment i used by j in r #;
Coefficient (ge 0)(all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    VFM(i,j,r) # producer expenditure on i by j in r valued at mkt prices #;
Update (all,i,ENDWM_COMM)(all,j,PROD_COMM)(all,r,REG)
    VFM(i,j,r) = pm(i,r) * qfe(i,j,r);
Update (all,i,ENDWS_COMM)(all,j,PROD_COMM)(all,r,REG)
    VFM(i,j,r) = pmes(i,j,r) * qfe(i,j,r);
Read
    VFM from file GTAPDATA header "VFM";

Coefficient (ge 0)(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VIFM(i,j,r) # purchases of imports i for use by j in region r #;
Update (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VIFM(i,j,r) = pim(i,r) * qfm(i,j,r);
Read
    VIFM from file GTAPDATA header "VIFM";

Coefficient (ge 0)(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VDFM(i,j,r) # purchases of domestic i for use by j in region r #;
Update (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VDFM(i,j,r) = pm(i,r) * qfd(i,j,r);
Read
    VDFM from file GTAPDATA header "VDFM";


!<
    Global Bank
    -----------
>!

Variable (all,r,REG)
    kb(r) # beginning-of-period capital stock in r #;
Variable (all,r,REG)
    pcgds(r) # price of investment goods = ps("cgds",r) #;
Coefficient (ge 0)(all,r,REG)
    VDEP(r) # value of capital depeciation in r (exogenous) #;
Update (all,r,REG)
    VDEP(r) = kb(r) * pcgds(r);
Read
    VDEP from file GTAPDATA header "VDEP";
!<
    Value of capital depreciation is used to compute the (exogenous)
    depreciation rate and therefore does not appear in update.
>!


!<
    International Trade and Transport
    ---------------------------------
>!

Variable (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    pms(i,r,s) # domestic price for good i supplied from r to region s #;
Variable (orig_level=VXMD)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    qxs(i,r,s) # export sales of commodity i from r to region s #;
Coefficient (ge 0)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VIMS(i,r,s) # imports of i from r to s valued at domestic mkt prices #;
Update (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VIMS(i,r,s) = pms(i,r,s) * qxs(i,r,s);
Read
    VIMS from file GTAPDATA header "VIMS";

Variable (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    pcif(i,r,s) # CIF world price of commodity i supplied from r to s #;
Coefficient (ge 0)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VIWS(i,r,s) # imports of i from r to s valued CIF (tradeables only) #;
Update (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VIWS(i,r,s) = pcif(i,r,s) * qxs(i,r,s);
Read
    VIWS from file GTAPDATA header "VIWS";

Variable (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    pfob(i,r,s) # FOB world price of commodity i supplied from r to s #;
Coefficient (ge 0)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VXWD(i,r,s) # exports of i from r to s valued FOB (tradeables only) #;
Update (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VXWD(i,r,s) = pfob(i,r,s) * qxs(i,r,s);
Read
    VXWD from file GTAPDATA header "VXWD";

Coefficient (ge 0)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VXMD(i,r,s)
    # exports of i from r to s valued at mkt prices (tradeables only) #;
Update (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VXMD(i,r,s) = pm(i,r) * qxs(i,r,s);
Read
    VXMD from file GTAPDATA header "VXMD";

Variable (orig_level=VST)(all,m,MARG_COMM)(all,r,REG)
    qst(m,r) # sales of m from r to international transport #;
Coefficient (ge 0)(all,m,MARG_COMM)(all,r,REG)
    VST(m,r)
    # exprts of m from r for int'l trnsport valued at mkt p (tradeables only) #;
Update (all,m,MARG_COMM)(all,r,REG)
    VST(m,r) = pm(m,r) * qst(m,r);
Read
    VST from file GTAPDATA header "VST";


!<
    ----------------
    Common VARIABLES
    ----------------

    Common variables are defined as variables which are used in more than one
    module.  For example, the variable y(r) is used in the Goverment
    Consumption, Private Consumption, Firms, Regional Household and
    Investment, Global Bank and Savings modules.  Appendices, e.g., Summary
    Indices, are not included in this definition.
>!

Variable (all,r,REG)
    y(r) # regional household income in region r #;
Variable (all,r,REG)
    pop(r) # regional population #;
Variable (all,i,ENDWS_COMM)(all,j,PROD_COMM)(all,r,REG)
    qoes(i,j,r) # supply of sluggish endowment i used by j in r #;
Variable (all,i,ENDW_COMM)(all,r,REG)
    endwslack(i,r) # slack variable in endowment market clearing condition #;
!<
    This is exogenous, unless the user wishes to employ a partial equilibrium
    closure in which the price of one or more of the primary factors is fixed.
>!

Variable (all,r,REG)
    pgov(r) # price index for gov't hhld expenditure in region r #;
Variable (all,r,REG)
    yg(r)
    # regional government consumption expenditure in region r #;
Variable (all,r,REG)
    ug(r) # per capita utility from gov't expend. in region r #;
Variable (all,r,REG)
    ppriv(r)
    # price index for private consumption expenditure in region r #;
Variable (all,r,REG)
    uepriv(r)
    # elasticity of cost wrt utility from private consumption #;
Variable (all,r,REG)
    yp(r) # regional private consumption expenditure in region r #;
Variable (all,r,REG)
    up(r) # per capita utility from private expend. in region r #;
Variable (orig_level=1) (all,i,NSAV_COMM)(all,r,REG)
    to(i,r) # output (or income) tax in region r #;
![[! UB edits starts !]]!

Variable (orig_level=1) (all,i,NSAV_COMM)(all,r,REG)
    to_1(i,r) # output (or income) tax in region r : SNSAV_COMM #;

Variable (orig_level=1) (all,r,REG)
    to_2(r) # output (or income) tax in region r : AEZ_COMM #;

Equation E_to_1 # to equation NSAV_COMM # (all,i,NSAV_COMM)(all,r,REG)
    to(i,r) = to_1(i,r) + IF(i in AEZ_COMM, to_2(r));  

![[! UB edits stops !]]!


!<
    Note:  It is important that the user NOT shock the tax on capital goods
    output, as this will cause an inconsistency in the update relationship for
    VOM(cgds).
>!
Variable (orig_level=VIM)(all,i,TRAD_COMM)(all,s,REG)
    qim(i,s) # aggregate imports of i in region s, market price weights #;
Variable
    globalcgds # global supply of capital goods for NET investment #;
Variable
    pcgdswld # world average price of capital goods (net investment weights) #;

!<
    The following variables could be dropped when converting to levels
    equation for income.  They are only needed for the linearized equation.
    The idea here is to look at the ratio of taxes to income in order to
    preserve homogeneity in prices.  (We could also look at changes in tax
    revenue, but then a uniform price increase would change this variable.)
    Obviously a simple percentage change variable doesn't work, since many
    taxes are initially zero.  The basic logic of this approach is as follows:

    Let R be the ratio of taxes to income: R = T/Y, then:
        dR = d(T/Y) = R(t - y)/100
    multiply through by Y to get:
        YdR = dT - Ty/100
    This ratio change is computed for each tax type and for total taxes.  Then
    the change in tax revenue itself may be computed as:
        dT = YdR + Ty/100
    in order to determine regional income.
>!

Variable (change) (all,r,REG)
    del_taxrgc(r) # change in ratio of government consumption tax to INCOME #;
Variable (change) (all,r,REG)
    del_taxrpc(r) # change in ratio of private consumption tax to INCOME #;
Variable (change) (all,r,REG)
    del_taxriu(r) # change in ratio of tax on intermediate usage to INCOME #;
Variable (change) (all,r,REG)
    del_taxrfu(r) # change in ratio of tax on primary factor usage to INCOME #;
Variable (change) (all,r,REG)
    del_taxrout(r) # change in ratio of output tax to INCOME #;
Variable (change) (all,r,REG)
    del_taxrexp(r) # change in ratio of export tax to INCOME #;
Variable (change) (all,r,REG)
    del_taxrimp(r) # change in ratio of import tax to INCOME #;
Variable (change) (all,r,REG)
    del_taxrinc(r) # change in ratio of income tax to INCOME #;


!<
    -------------------
    Common COEFFICIENTS
    -------------------

    Key Derivatives of the Base Data
    Regional Expenditure and Income
    Indirect Tax Receipts
    Miscellaneous Coefficients

    Common Coefficients are defined as coefficients which are used in more
    than one module.  For example, ESUBD(i) is used in the Goverment
    Household, Private Household, and Firms modules.
>!


!<
    Key Derivatives of the Base Data
    --------------------------------
>!

Coefficient (all,i,DEMD_COMM)(all,j,PROD_COMM)(all,r,REG)
    VFA(i,j,r) # producer expenditure on i by j in r valued at agent's prices #;
Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    VFA(i,j,r) = EVFA(i,j,r);
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    VFA(i,j,s) = VDFA(i,j,s) + VIFA(i,j,s);

Coefficient (all,i,NSAV_COMM)(all,r,REG)
    VOA(i,r) # value of commodity i output in region r at agent's prices #;
Formula (all,i,ENDW_COMM)(all,r,REG)
    VOA(i,r) = EVOA(i,r);
Formula (all,i,PROD_COMM)(all,r,REG)
    VOA(i,r) = sum(j,DEMD_COMM, VFA(j,i,r));

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VDM(i,r) # domestic sales of i in r at mkt prices (tradeables only) #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    VDM(i,r) = VDPM(i,r) + VDGM(i,r) + sum(j,PROD_COMM, VDFM(i,j,r));

Coefficient (all,i,NSAV_COMM)(all,r,REG)
    VOM(i,r) # value of commodity i output in region r at market prices #;
Formula (all,i,ENDW_COMM)(all,r,REG)
    VOM(i,r) = sum(j,PROD_COMM, VFM(i,j,r));
Formula (all,m,MARG_COMM)(all,r,REG)
    VOM(m,r) = VDM(m,r) + sum(s,REG, VXMD(m,r,s)) + VST(m,r);
Formula (all,i,NMRG_COMM)(all,r,REG)
    VOM(i,r) = VDM(i,r) + sum(s,REG, VXMD(i,r,s));
Formula (all,h,CGDS_COMM)(all,r,REG)
    VOM(h,r) = VOA(h,r);


!<
    Regional Expenditure and Income
    -------------------------------

    Regional income is allocated between private consumption expenditure,
    government consumption expenditure, and saving.


    Expenditure of Regional Household
    .................................

               Regional household(h.h.)
                Expenditure = Income
                         _/|\_
                       _/  |  \_
                     _/    |    \_
                   _/      |      \_
                 _/        |        \_
       Private h.h.        |        Government h.h.
       ( PRIVEXP )         |          ( GOVEXP )
          _/\_             |              _/\_
        _/    \_           |            _/    \_
       /        \          |           /        \
    Domestic  Imported   Savings    Domestic    Imported
     Goods     Goods     ( SAVE )    Goods       Goods
    ( VDPA )  ( VIPA )              ( VDGA )    ( VIGA )

    Note: The coefficients at the ends of branches are
          Base Data, e.g., VDPA, SAVE.
>!

! government consumption expenditure, GOVEXP !
Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VGA(i,r) # government consn expenditure on i in r at agent's prices #;
Formula (all,i,TRAD_COMM)(all,s,REG)
    VGA(i,s) = VDGA(i,s) + VIGA(i,s);
Coefficient (all,r,REG)
    GOVEXP(r) # government expenditure in region r #;
Formula (all,r,REG)
    GOVEXP(r) = sum(i,TRAD_COMM, VGA(i,r));

! private consumption expenditure, PRIVEXP !
Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VPA(i,r) # private hhld expenditure on i in r valued at agent's prices #;
Formula (all,i,TRAD_COMM)(all,s,REG)
    VPA(i,s) = VDPA(i,s) + VIPA(i,s);
Coefficient (all,r,REG)
    PRIVEXP(r) # private consumption expenditure in region r #;
Formula (all,r,REG)
    PRIVEXP(r) = sum(i,TRAD_COMM, VPA(i,r));

! aggregate expenditure + saving = aggregate income !
Coefficient (all,r,REG)
    INCOME(r) # level of expenditure, which equals NET income in region r #;
Formula (all,r,REG)
    INCOME(r) = PRIVEXP(r) + GOVEXP(r) + SAVE(r) ;

! aggregate expenditure + saving = aggregate income !
Variable (levels) (all,r,REG)
    INCOME_track(r) # tracking level of expenditure #;
Formula (initial) (all,r,REG) INCOME_track(r) = INCOME(r);
     Equation E_INCOME_track (all,r,REG) 
     p_INCOME_track(r) = y(r);



!<
    Indirect Tax Receipts
    ---------------------
>!

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    DGTAX(i,r) # tax on government consumption of domestic good i in region r #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    DGTAX(i,r) = VDGA(i,r) - VDGM(i,r);
Coefficient (all,i,TRAD_COMM)(all,r,REG)
    IGTAX(i,r) # tax on government consumption of imported good i in region r #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    IGTAX(i,r) = VIGA(i,r) - VIGM(i,r);
Coefficient (all,r,REG)
    TGC(r) # government consumption tax payments in r #;
Formula (all,r,REG)
    TGC(r) = sum(i,TRAD_COMM, DGTAX(i,r) + IGTAX(i,r));

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    DPTAX(i,r) # tax on private consumption of domestic good i in region r #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    DPTAX(i,r) = VDPA(i,r) - VDPM(i,r);
Coefficient (all,i,TRAD_COMM)(all,r,REG)
    IPTAX(i,r) # tax on private consumption of imported good i in region r #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    IPTAX(i,r) = VIPA(i,r) - VIPM(i,r);
Coefficient (all,r,REG)
    TPC(r) # private consumption tax payments in r #;
Formula (all,r,REG)
    TPC(r) = sum(i,TRAD_COMM, DPTAX(i,r) + IPTAX(i,r));

Coefficient (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    DFTAX(i,j,r) # tax on use of domestic intermediate good i by j in r #;
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    DFTAX(i,j,r) = VDFA(i,j,r) - VDFM(i,j,r);
Coefficient (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    IFTAX(i,j,r) # tax on use of imported intermediate good i by j in r #;
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    IFTAX(i,j,r) = VIFA(i,j,r) - VIFM(i,j,r);
Coefficient (all,r,REG)
    TIU(r) # firms' tax payments on intermediate goods usage in r #;
Formula (all,r,REG)
    TIU(r) = sum(i,TRAD_COMM, sum(j,PROD_COMM, DFTAX(i,j,r) + IFTAX(i,j,r)));

Coefficient (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    ETAX(i,j,r) # tax on use of endowment good i by industry j in region r #;
Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    ETAX(i,j,r) = VFA(i,j,r) - VFM(i,j,r);
Coefficient (all,r,REG)
    TFU(r) # firms' tax payments on primary factor usage in r #;
Formula (all,r,REG)
    TFU(r) = sum(i,ENDW_COMM, sum(j,PROD_COMM, ETAX(i,j,r)));

Coefficient (all,i,NSAV_COMM)(all,r,REG)
    PTAX(i,r) # output tax on good i in region r #;
Formula (all,i,NSAV_COMM)(all,r,REG)
    PTAX(i,r) = VOM(i,r) - VOA(i,r);
Coefficient (all,r,REG)
    TOUT(r) # production tax payments in r #;
Formula (all,r,REG)
    TOUT(r) = sum(i,PROD_COMM, PTAX(i,r));

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    XTAXD(i,r,s) # tax on exports of good i from source r to destination s #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    XTAXD(i,r,s) = VXWD(i,r,s) - VXMD(i,r,s);
Coefficient (all,r,REG)
    TEX(r) # export tax payments in r #;
Formula (all,r,REG)
    TEX(r) = sum(i,TRAD_COMM, sum(s,REG, XTAXD(i,r,s)));

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    MTAX(i,r,s) # tax on imports of good i from source r in destination s #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    MTAX(i,r,s) = VIMS(i,r,s) - VIWS(i,r,s);
Coefficient (all,r,REG)
    TIM(r) # import tax payments in r #;
Formula (all,r,REG)
    TIM(r) = sum(i,TRAD_COMM, sum(s,REG, MTAX(i,s,r)));


!<
    Miscellaneous Coefficients
    --------------------------
>!

! domestic/imported substitution elasticity !
Coefficient (parameter)(all,i,TRAD_COMM)(all,r,REG)
    ESUBD(i,r)
    # region-generic el. of sub. domestic/imported for all agents #;
Read
    ESUBD from file GTAPPARM header "ESBD";

! net global investment expenditure !
Coefficient (all,r,REG)
    REGINV(r) # regional GROSS investment in r (value of "cgds" output) #;
Formula (all,r,REG)
    REGINV(r) = sum(k,CGDS_COMM, VOA(k,r));
Coefficient (all,r,REG)
    NETINV(r) # regional NET investment in region r #;
Formula (all,r,REG)
    NETINV(r) = sum(k,CGDS_COMM, VOA(k,r)) - VDEP(r);
Coefficient
    GLOBINV # global expenditures on net investment #;
Formula
    GLOBINV = sum(r,REG, NETINV(r));

! elasticity of cost wrt utility from private consumption !
Coefficient (all,i,TRAD_COMM)(all,r,REG)
    CONSHR(i,r) # share of private hhld consumption devoted to good i in r #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    CONSHR(i,r) = VPA(i,r) / sum(k,TRAD_COMM, VPA(k,r));
Coefficient (parameter)(all,i,TRAD_COMM)(all,r,REG)
    INCPAR(i,r)
    # expansion parameter in the CDE minimum expenditure function #;
Read
    INCPAR from file GTAPPARM header "INCP";
Coefficient (all,r,REG)
    UELASPRIV(r)
    # elasticity of cost wrt utility from private consumption #;
Formula (all,r,REG)
    UELASPRIV(r) = sum(i,TRAD_COMM, CONSHR(i,r) * INCPAR(i,r));


!<
    =======
    Modules
    =======

    1. Government Consumption
    2. Private Consumption
    3. Firms
    4. Investment, Global Bank, and Savings
    5. International Trade
    6. International Transport Services
    7. Regional Household
    8. Equilibrium Conditions
>!


!<
    -------------------------
    1. Government Consumption
    -------------------------

    1-0. Module-Specific Variables
    1-1. Demands for Composite Goods
    1-2. Composite Tradeables
>!


!<
    1-0. Module-Specific Variables
    ------------------------------

    only used in this Government Consumption module
>!

Variable (all,i,TRAD_COMM)(all,r,REG)
    pg(i,r) # government consumption price for commodity i in region r #;
Variable (all,i,TRAD_COMM)(all,r,REG)
    qg(i,r) # government hhld demand for commodity i in region r #;


!<
    1-1. Demands for Composite Goods
    --------------------------------
>!

Equation GPRICEINDEX
# definition of price index for aggregate gov't purchases (HT 40) #
(all,r,REG)
    pgov(r) = sum(i,TRAD_COMM, [VGA(i,r) / GOVEXP(r)] * pg(i,r));

Equation GOVDMNDS
# government consumption demands for composite commodities (HT 41) #
(all,i,TRAD_COMM)(all,r,REG)
    qg(i,r) - pop(r) = ug(r) - [pg(i,r) - pgov(r)];

Equation GOVU
# utility from government consumption in r #
(all,r,REG)
    yg(r) - pop(r) = pgov(r) + ug(r);


!<
    1-2. Composite Tradeables
    -------------------------
>!

Variable (all,i,TRAD_COMM)(all,r,REG)
    tgd(i,r) # tax on domestic i purchased by government hhld in r #;

Variable (all,i,TRAD_COMM)(all,r,REG)
    tgm(i,r) # tax on imported i purchased by gov't hhld in r #;

Equation GHHDPRICE
# eq'n links domestic market and government consumption prices (HT 19) #
(all,i,TRAD_COMM)(all,r,REG)
    pgd(i,r) = tgd(i,r) + pm(i,r);

Equation GHHIPRICES
# eq'n links domestic market and government consumption prices (HT 22) #
(all,i,TRAD_COMM)(all,r,REG)
    pgm(i,r) = tgm(i,r) + pim(i,r);

Coefficient (all,i,TRAD_COMM)(all,s,REG)
    GMSHR(i,s) # share of imports for gov't hhld at agent's prices #;
Formula (all,i,TRAD_COMM)(all,s,REG)
    GMSHR(i,s) = VIGA(i,s) / VGA(i,s);

Equation GCOMPRICE
# government consumption price for composite commodities (HT 42) #
(all,i,TRAD_COMM)(all,s,REG)
    pg(i,s) =  GMSHR(i,s) * pgm(i,s) + [1 - GMSHR(i,s)] * pgd(i,s);

Equation GHHLDAGRIMP
# government consumption demand for aggregate imports (HT 43) #
(all,i,TRAD_COMM)(all,s,REG)
    qgm(i,s) = qg(i,s) + ESUBD(i,s) * [pg(i,s) - pgm(i,s)];

Equation GHHLDDOM
# government consumption demand for domestic goods (HT 44) #
(all,i,TRAD_COMM)(all,s,REG)
    qgd(i,s) = qg(i,s) + ESUBD(i,s) * [pg(i,s) - pgd(i,s)];

Equation TGCRATIO
# change in ratio of government consumption tax payments to regional income #
(all,r,REG)
    100.0 * INCOME(r) * del_taxrgc(r) + TGC(r) * y(r)
        = sum(i,TRAD_COMM,
            VDGA(i,r) * tgd(i,r) + DGTAX(i,r) * [pm(i,r) + qgd(i,r)])
        + sum(i,TRAD_COMM,
            VIGA(i,r) * tgm(i,r) + IGTAX(i,r) * [pim(i,r) + qgm(i,r)]);


!<
    -----------------------------
    2. Private Consumption Module
    -----------------------------

    2-0. Module-Specific Variables
    2-1. Utility from Private Consumption
    2-2. Allen Partials, Price and Income Elasticities, Composite Demand
    2-3. Composite Tradeables
>!


!<
    2-0. Module-Specific Variables
    ------------------------------

    only used in this Private Consumption module
>!

Variable (all,i,TRAD_COMM)(all,r,REG)
    pp(i,r) # private consumption price for commodity i in region r #;
Variable (all,i,TRAD_COMM)(all,r,REG)
    qp(i,r) # private hhld demand for commodity i in region r #;


!<
    2-1. Utility from Private Consumption
    -------------------------------------
>!

Equation PHHLDINDEX
# price index for private consumption expenditure #
(all,r,REG)
    ppriv(r) = sum(i,TRAD_COMM, CONSHR(i,r) * pp(i,r));

Equation PRIVATEU
# computation of utility from private consumption in r (HT 45) #
(all,r,REG)
    yp(r) - pop(r) = ppriv(r) + UELASPRIV(r) * up(r);
!<
    This equation determines private consumption utility for a representative
    household in region r, based on the per capita private expenditure
    function.  (HT 45)
>!

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    XWCONSHR(i,r)
    # expansion-parameter-weighted consumption share #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    XWCONSHR(i,r) = CONSHR(i,r) * INCPAR(i,r) / UELASPRIV(r);

Equation UTILELASPRIV
# elasticity of expenditure wrt utility from private consumption #
(all,r,REG)
    uepriv(r)
        = sum(i,TRAD_COMM, XWCONSHR(i,r) * [pp(i,r) + qp(i,r) - yp(r)]);


!<
    2-2. Allen Partials, Price and Income Elasticities, Composite Demand
    --------------------------------------------------------------------
>!

Coefficient (parameter)(all,i,TRAD_COMM)(all,r,REG)
    SUBPAR(i,r)
    # substitution parameter in CDE minimum expenditure function #;
Read
    SUBPAR from file GTAPPARM header "SUBP";

!<
    (HT F1 - F3)
>!
Coefficient (all,i,TRAD_COMM)(all,r,REG)
    ALPHA(i,r) # 1 - sub. parameter in the CDE minimum expenditure function #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    ALPHA(i,r) = 1 - SUBPAR(i,r);

Coefficient (all,i,TRAD_COMM)(all,k,TRAD_COMM)(all,r,REG)
    APE(i,k,r) # Allen partial elst. of sub. between composite i and k in r #;
Formula (all,i,TRAD_COMM)(all,k,TRAD_COMM)(all,r,REG)
    APE(i,k,r)
        = ALPHA(i,r) + ALPHA(k,r) - sum(n,TRAD_COMM, CONSHR(n,r) * ALPHA(n,r));
Formula (all,i,TRAD_COMM)(all,r,REG)
    APE(i,i,r)
        = 2.0 * ALPHA(i,r)
        - sum(n,TRAD_COMM, CONSHR(n,r) * ALPHA(n,r))
        - ALPHA(i,r) / CONSHR(i,r);

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    EY(i,r) # income elast. of private hhld demand for i in r (HT F4) #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    EY(i,r)
        =   [1.0 / sum(n,TRAD_COMM, CONSHR(n,r) * INCPAR(n,r))]
            * [INCPAR(i,r) * [1.0 - ALPHA(i,r)]
                + sum(n,TRAD_COMM, CONSHR(n,r) * INCPAR(n,r) * ALPHA(n,r))]
        + [ALPHA(i,r) - sum(n,TRAD_COMM, CONSHR(n,r) * ALPHA(n,r))];

Coefficient (all,i,TRAD_COMM)(all,k,TRAD_COMM)(all,r,REG)
    EP(i,k,r)
    # uncomp. elast. of private hhld demand for i wrt price of k in r (HT F5) #;
Formula (all,i,TRAD_COMM)(all,k,TRAD_COMM)(all,r,REG)
    EP(i,k,r) = 0;
Formula (all,i,TRAD_COMM)(all,k,TRAD_COMM)(all,r,REG)
    EP(i,k,r) = [APE(i,k,r) - EY(i,r)] * CONSHR(k,r);

Equation PRIVDMNDS
# private consumption demands for composite commodities (HT 46) #
(all,i,TRAD_COMM)(all,r,REG)
    qp(i,r) - pop(r)
        = sum(k,TRAD_COMM, EP(i,k,r) * pp(k,r)) + EY(i,r) * [yp(r) - pop(r)];
!<
    Private consumption demands for composite commodities.  Demand system is
    on a per capita basis.  Here, yp(r) - pop(r) is % change in per capita
    income.  (HT 46)
>!


!<
    2-3. Composite Tradeables
    -------------------------
>!

Variable (all,r,REG)
    tp(r) # comm.-, source-gen. shift in tax on private cons. #;
!<
    The variable tp(r) can be swapped with del_ttaxr(r) in order to generate a
    tax replacement scenario, whereby taxes remain a constant share of
    national income.
>!

Variable (all,i,TRAD_COMM)(all,r,REG)
    tpd(i,r) # comm.-, source-spec. shift in tax on private cons. of dom. #;
Variable (all,i,TRAD_COMM)(all,r,REG)
    atpd(i,r) # power of tax on domestic i purchased by private hhld in r #;
Equation TPDSHIFT
# permits uniform consumption tax change #
(all,i,TRAD_COMM)(all,r,REG)
    atpd(i,r) = tpd(i,r) + tp(r);

Equation PHHDPRICE
# eq'n links domestic market and private consumption prices (HT 18) #
(all,i,TRAD_COMM)(all,r,REG)
    ppd(i,r) = atpd(i,r) + pm(i,r);

Variable (all,i,TRAD_COMM)(all,r,REG)
    tpm(i,r) # comm.-, source-spec. shift in tax on private cons. of imp. #;
Variable (all,i,TRAD_COMM)(all,r,REG)
    atpm(i,r) # power of tax on imported i purchased by private hhld in r #;
Equation TPMSHIFT
# permits uniform consumption tax change #
(all,i,TRAD_COMM)(all,r,REG)
    atpm(i,r) = tpm(i,r) + tp(r);

Equation PHHIPRICES
# eq'n links domestic market and private consumption prices (HT 21) #
(all,i,TRAD_COMM)(all,r,REG)
    ppm(i,r) = atpm(i,r) + pim(i,r);

Equation TPCRATIO
# change in ratio of private consumption tax payments to regional income #
(all,r,REG)
    100.0 * INCOME(r) * del_taxrpc(r) + TPC(r) * y(r)
        = sum(i,TRAD_COMM,
            VDPA(i,r) * atpd(i,r) + DPTAX(i,r) * [pm(i,r) + qpd(i,r)])
        + sum(i,TRAD_COMM,
            VIPA(i,r) * atpm(i,r) + IPTAX(i,r) * [pim(i,r) + qpm(i,r)]);

Coefficient (all,i,TRAD_COMM)(all,s,REG)
    PMSHR(i,s) # share of imports for priv hhld at agent's prices #;
Formula (all,i,TRAD_COMM)(all,s,REG)
    PMSHR(i,s) = VIPA(i,s) / VPA(i,s);

Equation PCOMPRICE
# private consumption price for composite commodities (HT 47) #
(all,i,TRAD_COMM)(all,s,REG)
    pp(i,s) = PMSHR(i,s) * ppm(i,s) + [1 - PMSHR(i,s)] * ppd(i,s);

Equation PHHLDDOM
# private consumption demand for domestic goods (HT 48) #
(all,i,TRAD_COMM)(all,s,REG)
    qpd(i,s) = qp(i,s) + ESUBD(i,s) * [pp(i,s) - ppd(i,s)];

Equation PHHLDAGRIMP
# private consumption demand for aggregate imports (HT 49) #
(all,i,TRAD_COMM)(all,s,REG)
    qpm(i,s) = qp(i,s) + ESUBD(i,s) * [pp(i,s) - ppm(i,s)];


!<
    ---------
    3. Firms
    ---------

    We now turn to the behavioral equations for firms.  The following picture
    describes factor demands.  The first set of equations describe demands for
    primary factors.  (See table 4 of Hertel and Tsigas.)

    Production structure
    --------------------

                             qo(j,r)  [ao(j,r)]
                             /\
                            /  \  <----- CES
                           /    \
                          /      \
                         /        \
      [ava(j,r)]  qva(j,r)        qf(i,j,r)  [af(i,j,r)]
                  /|\                    /\
 CES ----->      / | \                  /  \  <------ CES
                /  |  \                /    \
               /   |   \              /      \
              /    |    \            /        \
          Land   Labor  Capital  Domestic     Foreign
               qfe(i,j,r)         qfd(i,j,r)  qfm(i,j,r)
             [afe(i,j,r)]



    3-0. Module-Specific Variables
    3-1. Total Output Nest
    3-2. Composite Intermediates Nest
    3-3. Value-Added Nest
    3-4. Zero Profits Equations
>!


!<
    3-0. Module-Specific Variables
    ------------------------------

    only used in this Firms module or the Summary Indices or Welfare
    appendices
>!

Variable (all,j,PROD_COMM)(all,r,REG)
    pva(j,r) # firms' price of value added in industry j of region r #;
Variable (all,j,PROD_COMM)(all,r,REG)
    qva(j,r) # value added in industry j of region r #;
Variable (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    pf(i,j,r) # firms' price for commodity i for use by j in r #;
Variable (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    qf(i,j,r) # demand for commodity i for use by j in region r #;
Variable (all,j,PROD_COMM)(all,r,REG)
    ao(j,r) # output augmenting technical change in sector j of r #;
Variable (all,i,PROD_COMM)(all,r,REG)
    ava(i,r) # value added augmenting tech change in sector i of r #;
Variable (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    af(i,j,r) # composite intermed. input i augmenting tech change by j of r #;
Variable (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    afe(i,j,r) # primary factor i augmenting tech change by j of r #;
Variable (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    ams(i,r,s) # import i from region r augmenting tech change in region s #;




!<
    3-2. Composite Intermediates Nest
    ---------------------------------
>!

Variable (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    tfd(i,j,r) # tax on domestic i purchased by j in r #;
Equation DMNDDPRICE
# eq'n links domestic market and firm prices (HT 20) #
(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    pfd(i,j,r) = tfd(i,j,r) + pm(i,r);
!<
    This equation links domestic market and firm prices.  It holds only for
    domestic goods and it captures the effect of commodity taxation of firms.
    (HT 20)
>!

Variable (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    tfm(i,j,r) # tax on imported i purchased by j in r #;
Equation DMNDIPRICES
# eq'n links domestic market and firm prices (HT 23) #
(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    pfm(i,j,r) = tfm(i,j,r) + pim(i,r);
!<
    This equation links domestic market and firm prices.  It holds only for
    imported goods and it captures the effect of commodity taxation of firms.
    (HT 23)
>!

Equation TIURATIO
# change in ratio of tax payments on intermediate goods to regional income #
(all,r,REG)
    100.0 * INCOME(r) * del_taxriu(r) + TIU(r) * y(r)
        = sum(i,TRAD_COMM, sum(j,PROD_COMM,
            VDFA(i,j,r) * tfd(i,j,r) + DFTAX(i,j,r) * [pm(i,r) + qfd(i,j,r)]))
        + sum(i,TRAD_COMM, sum(j,PROD_COMM,
            VIFA(i,j,r) * tfm(i,j,r) + IFTAX(i,j,r) * [pim(i,r) + qfm(i,j,r)]));

Coefficient (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    FMSHR(i,j,s) # share of firms' imports in dom. composite, agent's prices #;
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    FMSHR(i,j,s) = VIFA(i,j,s) / VFA(i,j,s);
Equation ICOMPRICE
# industry price for composite commodities (HT 30) #
(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    pf(i,j,r) = FMSHR(i,j,r) * pfm(i,j,r) + [1 - FMSHR(i,j,r)] * pfd(i,j,r);

Equation INDIMP
# industry j demands for composite import i (HT 31) #
(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    qfm(i,j,s) = qf(i,j,s) - ESUBD(i,s) * [pfm(i,j,s) - pf(i,j,s)];

Equation INDDOM
# industry j demands for domestic good i (HT 32) #
(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,s,REG)
    qfd(i,j,s) = qf(i,j,s) - ESUBD(i,s) * [pfd(i,j,s) - pf(i,j,s)];


!<
    3-3. Value-Added Nest
    ---------------------
>!

Variable (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    tf(i,j,r) # tax on primary factor i used by j in region r #;

Equation MPFACTPRICE
# eq'n links domestic and firm demand prices (HT 16) #
(all,i,ENDWM_COMM)(all,j,PROD_COMM)(all,r,REG)
    pfe(i,j,r) = tf(i,j,r) + pm(i,r);

Equation SPFACTPRICE
# eq'n links domestic and firm demand prices (HT 17) #
(all,i,ENDWS_COMM)(all,j,PROD_COMM)(all,r,REG)
    pfe(i,j,r) = tf(i,j,r) + pmes(i,j,r);

Variable (all,i,ENDW_COMM)
    afecom(i) # factor input tech change of input i, worldwide #;
Variable (all,j,PROD_COMM)
    afesec(j) # factor input tech change of sector j, worldwide #;
Variable (all,r,REG)
    afereg(r) # factor input tech change in region r #;
Variable (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    afeall(i,j,r) # primary factor i augmenting tech change sector j in r #;
Equation AFEWORLD
# sector/region specific average rate of prim. factor i augmenting tech change #
(all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    afe(i,j,r) = afecom(i) + afesec(j) + afereg(r) + afeall(i,j,r);

Coefficient (all,j,PROD_COMM)(all,r,REG)
    VVA(j,r) # value added in activity j in region r #;
Formula (all,j,PROD_COMM)(all,r,REG)
    VVA(j,r) = sum(i,ENDW_COMM, VFA(i,j,r));
Coefficient (all,i,ENDW_COMM)
    SVADEFAULT(i) #zerodivide default for SVA#;
Formula (all,i,ENDW_COMM)
    SVADEFAULT(i)
        = sum(j,PROD_COMM, sum(r,REG, VFA(i,j,r)))
        / sum(j,PROD_COMM, sum(r,REG, VVA(j,r)));
Coefficient (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    SVA(i,j,r) # share of i in total value added in j in r #;
Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG: VVA(j,r) <> 0)
    SVA(i,j,r) = VFA(i,j,r) / VVA(j,r);
Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG: VVA(j,r) = 0)
    SVA(i,j,r) = SVADEFAULT(i);
Equation VAPRICE
# effective price of primary factor composite in each sector/region (HT 33) #
(all,j,PROD_COMM)(all,r,REG)
    pva(j,r) = sum(k,ENDW_COMM, SVA(k,j,r) * [pfe(k,j,r) - afe(k,j,r)]);

Equation TFURATIO
# change in ratio of tax payments on factor usage to regional income #
(all,r,REG)
    100.0 * INCOME(r) * del_taxrfu(r) + TFU(r) * y(r)
        = sum(i,ENDWM_COMM, sum(j,PROD_COMM,
            VFA(i,j,r) * tf(i,j,r) + ETAX(i,j,r) * [pm(i,r) + qfe(i,j,r)]))
        + sum(i,ENDWS_COMM, sum(j,PROD_COMM,
            VFA(i,j,r) * tf(i,j,r) + ETAX(i,j,r) * [pmes(i,j,r) + qfe(i,j,r)]));


!<
    3-3.1 GTAP-AEZ Value-Added Nest
    -------------------------------
>!

!Begin Calibration of Yield Response to Price!


ZERODIVIDE (ZERO_BY_ZERO) DEFAULT 0 ;
ZERODIVIDE DEFAULT 1 ;

Coefficient (all,j,PROD_COMM)(all,r,reg)
    TOTALC(j,r);
Formula (all,j,PROD_COMM)(all,r,reg)
    TOTALC(j,r) = 
            sum(i, demd_comm,(VFA(i,j,r)));
Coefficient (all,j,PROD_COMM)(all,r,reg)
    LANDCOST(j,r);
Formula (all,j,PROD_COMM)(all,r,reg)
    LANDCOST(j,r) = 
            sum(a, AEZ_COMM,(VFA(a,j,r)));

Coefficient(all,j,PROD_COMM)(all,r,reg)
    THETAi(j,r) #Endowment's cost share in value-added#;
Formula(all,j,PROD_COMM)(all,r,reg)
    THETAi(j,r) = LANDCOST(j,r)/TOTALC(j,r);

Coefficient (parameter)(all,j,PROD_COMM)(all,r,REG)
    YDONOFF(j,r) #Turn off the yield calibrated ESUBVA's by setting to zero#;
Read YDONOFF from file GTAPPARM header "YD01";

Coefficient (parameter)(all,r,REG)
    YDREGSCALE(r) #Scale up or down the target yield elast for a region#;
Read YDREGSCALE from file GTAPPARM header "YDRS";

Coefficient (parameter)
    YDE_Target #Scalar yield elasticity target, read from PARM for SSA#;
Read YDE_Target from file GTAPPARM header "YDEL";

Coefficient (parameter) (all,j,PROD_COMM)(all,r,REG)
    ESUBVA1(j,r)
    # elasticity of substitution in value-added-energy subproduction #;
Read
    ESUBVA1 from file GTAPPARM header "ESBV";

Coefficient (parameter) (all,j,PROD_COMM)(all,r,REG)
    ESUBVA(j,r)
    # elasticity of substitution in value-added-energy subproduction #;
Formula(initial) (all,j,PROD_COMM)(all,r,REG)
    ESUBVA(j,r) = if(THETAi(j,r)*YDONOFF(j,r) le 0, ESUBVA1(j,r))
                + if(THETAi(j,r)*YDONOFF(j,r) gt 0,
                     YDREGSCALE(r)*YDE_Target/
                                        [(1/THETAi(j,r))-1]);

!End calibration of Yield Response to Price!


!< GTAP-AEZ: modify derived demand for land in land-using sectors!

Equation ENDWDEMAND
# demands for endowment commodities in non-land sectors (HT 34) #
(all,i,ENDW_COMM)(all,j,NLNDP_COMM)(all,r,REG)
    qfe(i,j,r)
        = - afe(i,j,r) + qva(j,r)
        - ESUBVA1(j,r) * [pfe(i,j,r) - afe(i,j,r) - pva(j,r)];

Coefficient (all,i,AEZ_COMM)(all,j,LAND_COMM)(all,r,REG)
    SAEZLAND(i,j,r) # the share of AEZ i in total land in sector j of r #;
Formula (all,i,AEZ_COMM)(all,j,LAND_COMM)(all,r,REG)
    SAEZLAND(i,j,r)
        = VFA(i,j,r)/ sum(k,AEZ_COMM, VFA(k,j,r));

Variable (all,j,LAND_COMM)(all,r,REG)
    pfeland(j,r) # composite land price in sector j, region r #;
Variable (all,j,LAND_COMM)(all,r,REG)
    qfeland(j,r)  # composite land demand in sector j, region r #;

Coefficient (parameter)(all,j,PROD_COMM)(all,r,REG)
    ESUBAEZ(j,r)
    # elst. of sub. amongst AEZs in land composite of land using sector j #;
Read
    ESUBAEZ from file GTAPPARM header "EAEZ";

Equation LANDPRICE
# effective price of land composite in each sector/region (HT 33) #
(all,j,LAND_COMM)(all,r,REG)
    pfeland(j,r) = 
        sum(k,AEZ_COMM, SAEZLAND(k,j,r) * [pfe(k,j,r) - afe(k,j,r)]);

Equation LANDDEMAND
# demands for land in the value added nest #
(all,j, LAND_COMM)(all,r,REG)
    qfeland(j,r)
        = qva(j,r)
        - ESUBVA(j,r) * [pfeland(j,r) - pva(j,r)];

Equation AEZDEMAND
# demands for endowment commodities (HT 34) #
(all,i,AEZ_COMM)(all,j, LAND_COMM)(all,r,REG)
    qfe(i,j,r)
        = - afe(i,j,r) + qfeland(j,r)
        - ESUBAEZ(j,r) * [pfe(i,j,r) - afe(i,j,r) - pfeland(j,r)];

Equation NLANDDEMAND
# demands for endowment commodities in land using sectors (HT 34) #
(all,i,NAEZ_COMM)(all,j,LAND_COMM)(all,r,REG)
    qfe(i,j,r)
        = - afe(i,j,r) + qva(j,r)
        - ESUBVA(j,r) * [pfe(i,j,r) - afe(i,j,r) - pva(j,r)];

!<
    End of GTAP-AEZ Value-Added Nest
    --------------------------------
>!
!<
    3-1. Total Output Nest
    ----------------------
>!

Variable (all,j,PROD_COMM)
    aosec(j) # output tech change of sector j, worldwide #;
Variable (all,r,REG)
    aoreg(r) # output tech change in region r #;
Variable (all,j,PROD_COMM)(all,r,REG)
    aoall(j,r) # output augmenting technical change in sector j of r #;
Variable (all,j,PROD_COMM)(all,r,REG)
    aoall2(j,r) # output augmenting technical change in sector j of r #;
Variable (all,j,PROD_COMM)(all,r,REG)
    aoall3(j,r) # output augmenting technical change in sector j of r #;
Variable (all,j,PROD_COMM)(all,r,REG)
    aoall4(j,r) # output augmenting technical change in sector j of r #;
Equation AOWORLD
# sector/region specific average rate of output augmenting tech change #
(all,j,PROD_COMM)(all,r,REG)
    ao(j,r) = aosec(j) + aoreg(r) + aoall(j,r) + aoall2(j,r)
                                  + aoall3(j,r) + aoall4(j,r);

Variable (all,j,PROD_COMM)
    avasec(j) # value added tech change of sector j, worldwide #;
Variable (all,r,REG)
    avareg(r) # value added tech change in region r #;
Variable (all,j,PROD_COMM)(all,r,REG)
    avaall(j,r) # value added augmenting technical change in sector j of r #;
Equation AVAWORLD
# sector/region specific average rate of value added augmenting tech change #
(all,j,PROD_COMM)(all,r,REG)
    ava(j,r) = avasec(j) + avareg(r) + avaall(j,r);

Coefficient (parameter)(all,j,PROD_COMM)
    ESUBT1(j)
    # elst. of sub. among composite intermediate inputs in production #;
Read
    ESUBT1 from file GTAPPARM header "ESBT";

Coefficient (Parameter)(all,j,PROD_COMM)(all,r,REG)
    ESUBT(j,r);
Formula(Initial) (all,j,PROD_COMM)(all,r,REG)
    ESUBT(j,r) = if(YDONOFF(j,r) le 0, ESUBT1(j))
                    +if(YDONOFF(j,r) gt 0, ESUBVA(j,r));


Equation VADEMAND
# sector demands for primary factor composite #
(all,j,PROD_COMM)(all,r,REG)
    qva(j,r)
        = -ava(j,r) + qo(j,r) - ao(j,r)
        - ESUBT(j,r) * [pva(j,r) - ava(j,r) - ps(j,r) - ao(j,r)];
!<
    Sector demands for primary factor composite.  This equation differs from
    HT 35 due to the presence of intermediate input substitution.
>!

Variable (all,i,TRAD_COMM)
    afcom(i) # intermediate tech change of input i, worldwide #;
Variable (all,j,PROD_COMM)
    afsec(j) # intermediate tech change of sector j, worldwide #;
Variable (all,r,REG)
    afreg(r) # intermediate tech change in region r #;
Variable (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    afall(i,j,r) # intermediate input i augmenting tech change by j in r #;
Equation AFWORLD
# sector/region specific average rate of intermediates augmenting tech change #
(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    af(i,j,r) = afcom(i) + afsec(j) + afreg(r) + afall(i,j,r);

Equation INTDEMAND
# industry demands for intermediate inputs, including cgds #
(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    qf(i,j,r)
        = - af(i,j,r) + qo(j,r) - ao(j,r)
        - ESUBT(j,r) * [pf(i,j,r) - af(i,j,r) - ps(j,r) - ao(j,r)];
!<
    Industry demands for intermediate inputs, including cgds.  This equation
    differs from HT 36 due to the presence of intermediate input substitution.
>!

!<
    3-4. Zero Profits Equations
    ---------------------------
>!

Equation OUTPUTPRICES
# eq'n links pre- and post-tax supply prices for all industries  (HT 15) #
(all,i,PROD_COMM)(all,r,REG)
    ps(i,r) = to(i,r) + pm(i,r);
!<
    This equation links pre- and post-tax supply prices for all industries.
    This captures the effect of output taxes.  TO(i,r) < 1 in the case of a
    tax.  (HT 15)
>!

Equation TOUTRATIO
# change in ratio of output tax payments to regional income #
(all,r,REG)
    100.0 * INCOME(r) * del_taxrout(r) + TOUT(r) * y(r)
        = sum(i,PROD_COMM,
            VOA(i,r) * [-to(i,r)] + PTAX(i,r) * [pm(i,r) + qo(i,r)]);

Variable (all,j,PROD_COMM)(all,r,REG)
    profitslack(j,r) # slack variable in the zero profit equation #;
!<
    This is exogenous, unless the user wishes to specify output in a given
    region exogenously.
>!
Coefficient (all,i,DEMD_COMM)(all,j,PROD_COMM)(all,r,REG)
    STC(i,j,r) # share of i in total costs of j in r #;
Formula (all,i,DEMD_COMM)(all,j,PROD_COMM)(all,r,REG)
    STC(i,j,r) = VFA(i,j,r) / sum(k,DEMD_COMM, VFA(k,j,r));

Equation ZEROPROFITS
# industry zero pure profits condition (HT 6) #
(all,j,PROD_COMM)(all,r,REG)
    ps(j,r) + ao(j,r)
        = sum(i,ENDW_COMM, STC(i,j,r) * [pfe(i,j,r) - afe(i,j,r) - ava(j,r)])
        + sum(i,TRAD_COMM, STC(i,j,r) * [pf(i,j,r)  - af(i,j,r)])
        + profitslack(j,r);
!<
    Industry zero pure profits condition (HT 6).  This condition permits us to
    determine the endogenous output level for each of the non-endowment
    sectors, excepting when profitslack is itself endogenous.  The level of
    activity in the endowment sectors is exogenously determined.
>!


!<
    ---------------------------------------
    4. Investment, Global Bank, and Savings
    ---------------------------------------

    Capital stock and rate of return equations follow.  They correspond to the
    Investment Equations of Table 8 in Hertel and Tsigas.

    4-0. Module-Specific Variables
    4-1. Equations of Notational Convenience
    4-2. Rate of Return Equations
    4-3. Capital Accumulation
    4-4. Global Bank
    4-5. Price Index of Aggregate Global Composite Capital Goods
    4-6. Price of Saving
>!


!<
    4-0. Module-Specific Variables
    ------------------------------

    only used in this Investment, Global Bank and Savings module
>!

Variable (all,r,REG)
    rental(r) # rental rate on capital = ps("capital",r) #;
Variable (all,r,REG)
    ke(r) # end-of-period capital stock in r #;
Variable (all,r,REG)
    rore(r) # expected net rate of return on capital stock in r #;
Variable (all,r,REG)
    rorc(r) # current net rate of return on capital stock in r #;
Variable (all,r,REG)
    qcgds(r) # output of capital goods sector = qo("cgds",r) #;


!<
    4-1. Equations of Notational Convenience
    ----------------------------------------
>!

Variable (all,r,REG)
    ksvces(r) # capital services = qo("capital",r) #;
Equation KAPSVCES
# eq'n defines a variable for capital services (HT 52) #
(all,r,REG)
    ksvces(r)
        = sum(h,ENDWC_COMM, [VOA(h,r) / sum(k,ENDWC_COMM, VOA(k,r))] * qo(h,r));
!<
    This equation defines a variable for capital services, for convenience.
    (There is really only one capital services item.) (HT 52)
>!

Equation KAPRENTAL
# eq'n defines a variable for capital rental rate (HT 53) #
(all,r,REG)
    rental(r)
        = sum(h,ENDWC_COMM, [VOA(h,r) / sum(k,ENDWC_COMM, VOA(k,r))] * ps(h,r));

Equation CAPGOODS
# eq'n defines a variable for gross investment (HT 54) #
(all,r,REG)
    qcgds(r)
        = sum(h,CGDS_COMM, [VOA(h,r) / REGINV(r)] * qo(h,r));
!<
    This equation defines a variable for gross investment, for convenience.
    There is really only one capital goods item.
>!

Equation PRCGOODS
# eq'n defines the price of cgds (HT 55) #
(all,r,REG)
    pcgds(r) = sum(h,CGDS_COMM, [VOA(h,r) / REGINV(r)] * ps(h,r));

Equation KBEGINNING
# associates change in cap. services w/ change in cap. stock (HT 56) #
(all,r,REG)
    kb(r) = ksvces(r);
!<
    This equation associates any change in capital services during the period
    with a change in capital stock.  Full capacity utilization is assumed.
>!

Coefficient (ge 0)(all,r,REG)
    VKB(r) # value of beginning-of-period capital stock in region r #;
Update (all,r,REG)
    VKB(r) = pcgds(r) * kb(r);
Read
    VKB from file GTAPDATA header "VKB";
!<
    VKB(r) is used only in this sub-module, so its data are read here.  The
    associated variables, pcgds(r) and kb(r), are used across modules, so they
    have been defined as common variables.
>!

Coefficient (all,r,REG)
    INVKERATIO(r)
    # ratio of gross investment to end-of-period capital stock in r #;
Formula (all,r,REG)
    INVKERATIO(r) = REGINV(r) / [VKB(r) + NETINV(r)];

Equation KEND
# Ending capital stock equals beginning stock plus net investment.  (HT 10) #
(all,r,REG)
    ke(r) = INVKERATIO(r) * qcgds(r) + [1.0 - INVKERATIO(r)] * kb(r);


!<
    4-2. Rate of Return Equations
    -----------------------------
>!

Coefficient (all,r,REG)
    GRNETRATIO(r) # ratio of GROSS/NET rates of return on capital in r #;
Formula (all,r,REG)
    GRNETRATIO(r)
        = sum(h,ENDWC_COMM, VOA(h,r))
        / [sum(h,ENDWC_COMM, VOA(h,r)) - VDEP(r)];
!<
    NOTE: VOA("capital",r) is GROSS returns to capital.
>!
Equation RORCURRENT
# current rate of return on capital in region r (HT 57) #
(all,r,REG)
    rorc(r) = GRNETRATIO(r) * [rental(r) - pcgds(r)];

Coefficient (parameter)(all,r,REG)
    RORFLEX(r)
    # flexibility of expected net ROR on capital stock in r wrt investment #;
Read
    RORFLEX from file GTAPPARM header "RFLX";
!<
    RORFLEX is the flexibility of the expected net rate of return on the
    capital stock, in region r, with respect to investment.  If a region's
    capital stock increases by 1%, then it is expected that the net rate of
    return on capital will decline by RORFLEX %.  A larger absolute value for
    this parameter will reduce the tendency of international investment flows
    to respond to changes in expected rates of return.
>!

Equation ROREXPECTED
# expected rate of return depends on the current return and investment (HT 58) #
(all,r,REG)
    rore(r) = rorc(r) - RORFLEX(r) * [ke(r) - kb(r)];


!<
    4-3. Capital Accumulation
    -------------------------

    based on the theory laid out in GTAP Technical Paper #7
>!

Variable (all,i,ENDWC_COMM)(all,r,REG)
    EXPAND(i,r) # change in investment levels relative to endowment stock #;
Equation BALDWIN
# change in investment levels relative to endowment stock #
(all,i,ENDWC_COMM)(all,r,REG)
    EXPAND(i,r) = qcgds(r) - qo(i,r);


!<
    4-4. Global Bank
    ----------------
>!

Coefficient (integer,parameter)
    RORDELTA
    # binary coefficient to switch mechanism of allocating investment funds #;
Read
    RORDELTA from file GTAPPARM header "RDLT";
!<
    RORDELTA is a binary coefficient which determines the mechanism of
    allocating investment funds across regions.  When RORDELTA = 1, investment
    funds are allocated across regions to equate the change in the expected
    rates of return (i.e., rore(r)).  When RORDELTA = 0, investment funds are
    allocated across regions to maintain the existing composition of capital
    stocks.
>!

Variable
    rorg # global net rate of return on capital stock #;
Variable (all,r,REG)
    cgdslack(r) # slack variable for qcgds(r) #;
!<
    This is exogenous, unless the user wishes to exogenously fix the level of
    gross investment in a region, in which case it can be swapped with
    cgdslack.
>!
Equation RORGLOBAL
# either gross investment or expected rate of return in region r (HT 59) #
(all,r,REG)
    RORDELTA * rore(r)
    + [1 - RORDELTA]
    * [[REGINV(r) / NETINV(r)] * qcgds(r) - [VDEP(r) / NETINV(r)] * kb(r)]
        = RORDELTA * rorg + [1 - RORDELTA] * globalcgds + cgdslack(r);
!<
    This equation determines either gross investment or the expected rate of
    return in each region, depending on the setting for the binary RORDELTA
    parameter.
>!

Equation GLOBALINV
# either expected global rate of return or global net investment (HT 11) #
    RORDELTA * globalcgds + [1 - RORDELTA] * rorg
        = RORDELTA
        * sum(r,REG,
            [REGINV(r) / GLOBINV] * qcgds(r) - [VDEP(r) / GLOBINV] * kb(r))
        + [1 - RORDELTA] * sum(r,REG, [NETINV(r) / GLOBINV] * rore(r));
!<
    This equation computes either the change in global net investment (when
    RORDELTA = 1), or the change in the expected global rate of return on
    capital (when RORDELTA = 0).
>!


!<
    4-5. Price Index of Aggregate Global Composite Capital Goods
    ------------------------------------------------------------
>!

Equation PRICGDS
# eq'n generates a price index for the aggregate global cgds composite (HT 60) #
    pcgdswld = sum(r,REG, [NETINV(r) / GLOBINV] * pcgds(r));


!<
    4-6. Price of Saving
    --------------------
>!

Variable (all,r,REG)
    psaveslack(r) # slack variable for the savings price equation #;
!<
    This is exogenous under the normal closure.  However, in order to
    replicate simulations from the GTAP book, where there was a single savings
    price, psaveslack may be swapped with psave in order to fix the latter.
    In this case, pcgdswld must be the numeraire so that each region faces the
    same price of savings.
>!

Equation SAVEPRICE
# savings price #
(all,r,REG)
    psave(r)
        = pcgds(r)
        + sum(s,REG, [[NETINV(s) - SAVE(s)] / GLOBINV] * pcgds(s))
        + psaveslack(r);
!<
    In contrast to the GTAP book, the price of savings is now region-specific
    and it changes at the same rate as the price of regional investment, plus
    an adjustment factor which accounts for the fact that savings and
    investment are not equal at the regional level.
>!


!<
    ----------------------
    5. International Trade
    ----------------------

    5-1. Export Prices
    5-2. Demand for Imports
>!


!<
    5-1. Export Prices
    ------------------
>!

Variable (all,i,TRAD_COMM)(all,r,REG)
    tx(i,r) # dest.-gen. change in subsidy on exports of i from r #;
Variable (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    txs(i,r,s) # dest.-spec. change in subsidy on exports of i from r to s #;
!<
    The variable txs captures changes in the power of bilateral export taxes.
    However, the presence of a destination-generic export subsidy shift (tx)
    also permits the user to swap a single export tax shock with another
    target variable.  It is most naturally swapped with the variable qo to
    insulate domestic producers from the world market.
>!

Equation EXPRICES
# eq'n links agent's and world prices (HT 27) #
(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    pfob(i,r,s) = pm(i,r) - tx(i,r) - txs(i,r,s);

Equation TEXPRATIO
# change in ratio of export tax payments to regional income #
(all,r,REG)
    100.0 * INCOME(r) * del_taxrexp(r) + TEX(r) * y(r)
        = sum(i,TRAD_COMM, sum(s,REG,
            VXMD(i,r,s) * [-tx(i,r) - txs(i,r,s)]
            + XTAXD(i,r,s) * [pfob(i,r,s) + qxs(i,r,s)]));


!<
    5-2. Demand for Imports
    -----------------------

    Composite Imports Nest: Table 3 of Hertel and Tsigas
>!

Variable (all,i,TRAD_COMM)(all,s,REG)
    tm(i,s) # source-gen. change in tax on imports of i into s #;
Variable (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    tms(i,r,s) # source-spec. change in tax on imports of i from r into s #;
!<
    The variable tms captures changes in the power of bilateral import taxes.
    However, the presence of a source-generic import tariff shift (tm) also
    permits the user to swap a single import tariff shock with another target
    variable.  In particular, to insulate domestic producers from import price
    changes, it may be swapped with the relative price variable pr -- see
    below.
>!

Equation MKTPRICES
# eq'n links domestic and world prices (HT 24) #
(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    pms(i,r,s) = tm(i,s) + tms(i,r,s) + pcif(i,r,s);

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    MSHRS(i,r,s) # share of imports from r in import bill of s at mkt prices #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    MSHRS(i,r,s) = VIMS(i,r,s) / sum(k,REG, VIMS(i,k,s));
Equation DPRICEIMP
# price for aggregate imports (HT 28) #
(all,i,TRAD_COMM)(all,s,REG)
    pim(i,s) = sum(k,REG, MSHRS(i,k,s) * [pms(i,k,s) - ams(i,k,s)]);

Variable (orig_level=1.0)(all,i,TRAD_COMM)(all,r,REG)
    pr(i,r) # ratio of domestic to imported prices in r #;
Equation PRICETGT
# eq'n defines target price ratio to be attained via the variable levy (HT 25) #
(all,i,TRAD_COMM)(all,s,REG)
    pr(i,s) = pm(i,s) - pim(i,s);
!<
    This equation defines the target price ratio to be attained via the
    variable levy.  This price ratio is the ratio of domestic to average
    imported goods' price.  Note that the way this price ratio is defined, it
    includes intraregional imports as well.  In most applications, regions
    will represent groups of individual countries.  However, in the case of
    the EU, this is problematic, since recent versions of the database have
    incorporated intra-EU trade flows.  Therefore, when aggregated to the EU
    level, the composite import price includes both intra-EU and outside
    imports.  So some modification is needed to handle the EU case.
>!

Coefficient (parameter)(all,i,TRAD_COMM)(all,s,REG)
    ESUBM(i,s)
    # region-generic el. of sub. among imports of i in Armington structure #;
Read
    ESUBM from file GTAPPARM header "ESBM";

Equation IMPORTDEMAND
# regional demand for disaggregated imported commodities by source (HT 29)  #
(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    qxs(i,r,s)
        = -ams(i,r,s) + qim(i,s)
        - ESUBM(i,r) * [pms(i,r,s) - ams(i,r,s) - pim(i,s)];

Equation TIMPRATIO
# change in ratio of import tax payments to regional income #
(all,r,REG)
    100.0 * INCOME(r) * del_taxrimp(r) + TIM(r) * y(r)
        = sum(i,TRAD_COMM, sum(s,REG,
            VIMS(i,s,r) * [tm(i,r) + tms(i,s,r)]
            + MTAX(i,s,r) * [pcif(i,s,r) + qxs(i,s,r)]));


!<
    -----------------------------------
    6. International Transport Services
    -----------------------------------

    6-0. Module-Specific Variables and Coefficients
    6-1. Demand for Global Transport Services
    6-2. Supply of Transport Services
>!


!<
    6-0. Module-Specific Variables and Coefficients
    -----------------------------------------------

    only used in this International Transport Services module
>!

Variable (all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    qtmfsd(m,i,r,s) # international usage margin m on i from r to s #;
!<
    International margin usage, by Margin, Freight, Source, and Destination,
    i.e., the percent change in usage of m in transport of i from r to s.
>!
Variable (all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    atmfsd(m,i,r,s) # tech change in m's shipping of i from region r to s #;
!<
    Technical progress in shipping by Margin, Freight, Source, and
    Destination.  This is endogenous and driven by the following mode-,
    product-, source-, and destination-specific determinants.
>!
Variable (all,m,TRAD_COMM)
    atm(m) # tech change in mode m, worldwide #;
Variable (all,i,TRAD_COMM)
    atf(i) # tech change shipping of i, worldwide #;
Variable (all,r,REG)
    ats(r) # tech change shipping from region r #;
Variable (all,s,REG)
    atd(s) # tech change shipping to s #;
Variable (all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    atall(m,i,r,s) # tech change in m's shipping of i from region r to s #;
Variable (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    ptrans(i,r,s) # cost index for international transport of i from r to s #;
!<
    average cost index for margin services used in getting i from r to s
>!
Variable (all,m,MARG_COMM)
    qtm(m) # global margin usage #;
Variable (all,m,MARG_COMM)
    pt(m) # price of composite margins services, type #;
!<
    price index for commodity m in margin services usage
>!

Coefficient (ge 0) (all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VTMFSD(m,i,r,s)
    # int'l margin usage, by margin, freight, source, and destination #;
Update (all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VTMFSD(m,i,r,s) = pt(m) * qtmfsd(m,i,r,s);
Read
    VTMFSD from file GTAPDATA header "VTWR";

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VTFSD(i,r,s) # aggregate value of svces in the shipment of i from r to s #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VTFSD(i,r,s) = sum(m,MARG_COMM, VTMFSD(m,i,r,s));
!<
    In a balanced data base, (all,i,TRAD_COMM)(all,r,REG)(all,s,REG),
    VIWS(i,r,s) = VXWD(i,r,s) + VTFSD(i,r,s).
>!

Coefficient (all,m,MARG_COMM)
    VTMUSE(m) # international margin services usage, by type #;
Formula (all,m,MARG_COMM)
    VTMUSE(m) = sum(i,TRAD_COMM, sum(r,REG, sum(s,REG, VTMFSD(m,i,r,s))));

Coefficient (all,m,MARG_COMM)
    VTMPROV(m) # international margin services provision #;
Formula (all,m,MARG_COMM)
    VTMPROV(m) = sum(r,REG, VST(m,r));
!<
    In a balanced data base, VTMPROV = VTMUSE.
>!

Coefficient (all,r,REG)
    VTRPROV(r) # international margin supply, by region #;
Formula (all,r,REG)
    VTRPROV(r) = sum(m,MARG_COMM, VST(m,r));

Coefficient
    VT # international margin supply #;
Formula
    VT = sum(m,MARG_COMM, sum(r,REG, VST(m,r)));


!<
    6-1. Demand for Global Transport Services
    -----------------------------------------
>!

Equation QTRANS_MFSD
# bilateral demand for transport services  #
(all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    qtmfsd(m,i,r,s) = qxs(i,r,s) - atmfsd(m,i,r,s);
!<
    This equation computes the bilateral demand for international
    transportation services.  It reflects the fact that the demand for
    services along any particular route is proportional to the quantity of
    merchandise shipped [i.e., QXS(i,r,s)].  It is here that we introduce the
    potential for input-augmenting tech change, atmfsd(m,i,r,s), which is
    commodity- and route-specific.  Thus, in the levels:
            ATMFSD(m,i,r,s) * QTMFSD(m,i,r,s) = QXS(i,r,s)
    where QTMFSD is the amount of composite margins services m used along this
    route.  Technological improvements are reflected by atmfsd(i,r,s) > 0, and
    these reduce the margins services required for this i,r,s triplet.  Tech.
    change also dampens the cost of shipping, thereby lowering the CIF price
    implied by a given FOB value (see 6-2).
>!

Coefficient (all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VTMUSESHR(m,i,r,s) # share of i,r,s usage in global demand for m #;
Formula (all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VTMUSESHR(m,i,r,s) = VTFSD(i,r,s) / VT;
Formula
    (all,m,MARG_COMM: VTMUSE(m) <> 0.0)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VTMUSESHR(m,i,r,s) = VTMFSD(m,i,r,s) / VTMUSE(m);

Equation TRANS_DEMAND
# global demand for margin m #
(all,m,MARG_COMM)
    qtm(m)
        = sum(i,TRAD_COMM, sum(r,REG, sum(s,REG,
          VTMUSESHR(m,i,r,s) * qtmfsd(m,i,r,s))));


!<
    6-2. Supply of Transport Services
    ---------------------------------
>!

Coefficient (all,m,MARG_COMM)(all,r,REG)
    VTSUPPSHR(m,r) # share of region r in global supply of margin m #;
Formula (all,m,MARG_COMM)(all,r,REG)
    VTSUPPSHR(m,r) = VTRPROV(r) / VT;
Formula (all,m,MARG_COMM: VTMPROV(m) <> 0.0)(all,r,REG)
    VTSUPPSHR(m,r) = VST(m,r) / VTMPROV(m);

Equation PTRANSPORT
# generate price index for composite transportation services #
(all,m,MARG_COMM)
    pt(m) = sum(r,REG, VTSUPPSHR(m,r) * pm(m,r));
!<
    This equation generates a price index for transportation services based on
    zero profits.  NOTE:
    (1) Sales to international transportation are not subject to export tax.
        This is why we base the costs to the transport sector on market prices
        of the goods sold to international transportation.
    (2) We assume that the supply shares for margin services are uniform
        across freight, source of freight, and destination.
    (cf. HT#7)
>!

Coefficient
    VTUSE # international margin services usage #;
Formula
    VTUSE
        = sum(m,MARG_COMM, sum(i,TRAD_COMM, sum(r,REG, sum(s,REG,
            VTMFSD(m,i,r,s)))));

Coefficient (all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VTFSD_MSH(m,i,r,s) # share of margin m in cost of getting i from r to s #;
Formula (all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)
        (all,s,REG: VTFSD(i,r,s) > 0.0)
    VTFSD_MSH(m,i,r,s) = VTMFSD(m,i,r,s) / VTFSD(i,r,s);
Formula (all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)
        (all,s,REG: VTFSD(i,r,s) = 0.0)
    VTFSD_MSH(m,i,r,s) = VTMUSE(m) / VTUSE;

Equation TRANSCOSTINDEX
#  generates flow-specific modal average cost of transport index (cf. HT7) #
(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    ptrans(i,r,s)
        = sum(m,MARG_COMM, VTFSD_MSH(m,i,r,s) * [pt(m) - atmfsd(m,i,r,s)]);

Equation TRANSTECHANGE
#  generates flow-specific average rate of technical change #
(all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    atmfsd(m,i,r,s) = atm(m) + atf(
i) + ats(r) + atd(s) + atall(m,i,r,s);

Equation TRANSVCES
# generate demand for regional supply of global transportation service (HT 61) #
(all,m,MARG_COMM)(all,r,REG)
    qst(m,r) = qtm(m) + [pt(m) - pm(m,r)];
!<
    This equation generates the international transport sector's derived
    demand for regional supplies of transportation services.  It reflects a
    unitary elasticity of substitution between transportation services inputs
    from different regions.
>!

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VIWSCOST(i,r,s) # value of imports calculated as total cost of imports #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VIWSCOST(i,r,s) = VXWD(i,r,s) + VTFSD(i,r,s);

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    FOBSHR(i,r,s) # FOB share in VIW #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    FOBSHR(i,r,s) = VXWD(i,r,s) / VIWSCOST(i,r,s);

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    TRNSHR(i,r,s) # transport share in VIW #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    TRNSHR(i,r,s) = VTFSD(i,r,s) / VIWSCOST(i,r,s);

Equation FOBCIF
# eq'n links FOB and CIF prices for good i shipped from region r to s (HT 26') #
(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    pcif(i,r,s)
       = FOBSHR(i,r,s) * pfob(i,r,s)
       + TRNSHR(i,r,s) * ptrans(i,r,s);
!<
    This equation links export and import prices for each commodity/route
    triplet.  Note that technical change is embodied in ptrans(i,r,s) which is
    now a cost index, as opposed to (HT 26') where it represented the price of
    margins services.
>!


!<
    ---------------------
    7. Regional Household
    ---------------------

    7-0. Module-Specific Coefficients
    7-1. Supply of Endowments by the Regional Household
    7-2. Computation of Regional Income
    7-3. Regional Household Demand System
    7-4. Aggregate Utility
>!


!<
    7-0. Module-Specific Coefficients
    ---------------------------------

    only used in this Regional Household module
>!

Coefficient (all,r,REG)
    XSHRPRIV(r) # private expenditure share in regional income #;
Formula (all,r,REG)
    XSHRPRIV(r) = PRIVEXP(r) / INCOME(r);

Coefficient (all,r,REG)
    XSHRGOV(r) # government expenditure share in regional income #;
Formula (all,r,REG)
    XSHRGOV(r) = GOVEXP(r) / INCOME(r);

Coefficient (all,r,REG)
    XSHRSAVE(r) # saving share in regional income #;
Formula (all,r,REG)
    XSHRSAVE(r) = SAVE(r) / INCOME(r);

Variable (all,r,REG)
    uelas(r) # elasticity of cost of utility wrt utility #;

Variable (all,r,REG)
    dppriv(r) # private consumption distribution parameter #;
Variable (all,r,REG)
    dpgov(r) # government consumption distribution parameter #;
Variable (all,r,REG)
    dpsave(r) # saving distribution parameter #;


!<
    7-1. Supply of Endowments by the Regional Household
    ---------------------------------------------------
>!
Equation FACTORINCPRICES
# eq'n links pre- and post-tax endowment supply prices (HT 15) #
(all,i,ENDW_COMM)(all,r,REG)
    ps(i,r) = to(i,r) + pm(i,r);

Coefficient (all,r,REG)
    TINC(r) # income tax payments in r #;
Formula (all,r,REG)
    TINC(r) = sum(i,ENDW_COMM, PTAX(i,r));

Equation TINCRATIO
# change in ratio of income tax payments to regional income #
(all,r,REG)
    100.0 * INCOME(r) * del_taxrinc(r) + TINC(r) * y(r)
        = sum(i,ENDW_COMM,
            VOA(i,r) * [-to(i,r)] + PTAX(i,r) * [pm(i,r) + qo(i,r)]);

Coefficient (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    REVSHR(i,j,r);
Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    REVSHR(i,j,r) = VFM(i,j,r) / sum(k,PROD_COMM, VFM(i,k,r));
Equation ENDW_PRICE
# eq'n generates the composite price for sluggish endowments (HT 50) #
(all,i,ENDWS_COMM)(all,r,REG)
    pm(i,r) = sum(k,PROD_COMM, REVSHR(i,k,r) * pmes(i,k,r));

Coefficient (parameter)(all,i,ENDW_COMM)(all,r,REG)
    ETRAE(i,r)
    # elst. of transformation for sluggish primary factor endowments #;
Read
    ETRAE from file GTAPPARM header "ETRE";

!<
    ETRAE is the elasticity of transformation for sluggish primary factor
    endowments.  It is non-positive, by definition.
>!

!<
    GTAP-AEZ supply of sluggish endowments
    --------------------------------------
>!

Equation NAEZEND_SUPPLY
# eq'n distributes the sluggish non-AEZ endowments across sectors (HT 51) #
(all,i,SNAEZ_COMM)(all,j,PROD_COMM)(all,r,REG)
 qoes(i,j,r) = qo(i,r) - endwslack(i,r) + ETRAE(i,r) * [pm(i,r) - pmes(i,j,r)];

!<
    Land supply is nested: first land cover, then harvested area (crops)
>!

!<
    Land cover (forestry, grazing, crops)
>!

Coefficient (parameter)(all,i,AEZ_COMM) (all,r,REG)
    ETRAEL1(i,r)
    # elst. of transformation for land cover at bottom of supply tree #;
Read
    ETRAEL1 from file GTAPPARM header "ETL1";

Coefficient
    SIZE_LAND # size of LAND_COMM set #;
Formula
    SIZE_LAND = sum(i,LAND_COMM, 1);

Variable (all,i,AEZ_COMM)(all,r,REG)
    pmland(i,r) # market price of sluggish endowment in r #;
Coefficient (all,i,AEZ_COMM)(all,j,LAND_COMM)(all,r,REG)
    REVSHR1(i,j,r);
Formula (all,i,AEZ_COMM)(all,j,LAND_COMM)(all,r,REG: VOM(i,r) = 0)
    REVSHR1(i,j,r) = 1.0 / SIZE_LAND;
Formula (all,i,AEZ_COMM)(all,j,LAND_COMM)(all,r,REG: VOM(i,r) > 0)
    REVSHR1(i,j,r) = VFM(i,j,r) / sum(k,LAND_COMM, VFM(i,k,r));

Equation LAND_PRICE
# eq'n generates the composite price for sluggish endowments (HT 50) #
(all,i,AEZ_COMM)(all,r,REG)
    pmland(i,r) = sum(k,LAND_COMM, REVSHR1(i,k,r) * pmes(i,k,r));

Coefficient (all,i,AEZ_COMM)(all,r,REG)
    CROPRENTS(i,r);
Formula (all,i,AEZ_COMM)(all,r,REG)
    CROPRENTS(i,r) = sum(k,AGRLAND_COMM, VFM(i,k,r));

Coefficient
    SIZE_CROP # size of CROP_COMM set #;
Formula
    SIZE_CROP = sum(i,CROP_COMM, 1);

Variable (all,i,AEZ_COMM)(all,r,REG)
    qocropland(i,r) # supply of sluggish endowment in r #;
Variable (all,i,AEZ_COMM)(all,r,REG)
    pmcropland(i,r) # market price of sluggish endowment in r #;
Coefficient (all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    REVSHR2(i,j,r);
Formula (all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG: CROPRENTS(i,r) = 0)
    REVSHR2(i,j,r) = 1.0 / SIZE_CROP;
Formula (all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG: CROPRENTS(i,r) > 0)
    REVSHR2(i,j,r) = VFM(i,j,r) / sum(k,CROP_COMM, VFM(i,k,r));

Equation CROPLAND_PRICE
# eq'n generates the composite price for sluggish endowments (HT 50) #
(all,i,AEZ_COMM)(all,r,REG)
    pmcropland(i,r) = sum(k,CROP_COMM, REVSHR2(i,k,r) * pmes(i,k,r));

Equation AGRLAND_SUPPLY
# eq'n distributes the sluggish endowments across sectors (HT 51) #
(all,i,AEZ_COMM)(all,r,REG)
 qocropland(i,r) = qo(i,r) - endwslack(i,r) 
    + ETRAEL1(i,r) * [pmland(i,r) - pmcropland(i,r)];

Equation OTHLAND_SUPPLY
# eq'n distributes the sluggish endowments across forestry and grazing #
(all,i,AEZ_COMM)(all,j,NCROP_COMM)(all,r,REG)
 qoes(i,j,r) = qo(i,r) - endwslack(i,r) 
    + ETRAEL1(i,r) * [pmland(i,r) - pmes(i,j,r)];

!<
    Crop land supply
>!

Coefficient (parameter)(all,i,AEZ_COMM)(all,r,REG)
    ETRAEL2(i,r)
    # elst. of transformation for crop land in supply tree #;
Read
    ETRAEL2 from file GTAPPARM header "ETL2";

Equation INDCROPLAND_SUPPLY
# eq'n distributes the sluggish endowments across sectors (HT 51) #
(all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
 qoes(i,j,r) = qocropland(i,r) - endwslack(i,r)
    + ETRAEL2(i,r) * [pmcropland(i,r) - pmes(i,j,r)];

!<
    7-2. Computation of Regional Income
    -----------------------------------
>!

Coefficient (all,r,REG)
    FY(r) # primary factor income in r net of depreciation #;
Formula (all,r,REG)
    FY(r) = sum(i,ENDW_COMM, VOM(i,r)) - VDEP(r);
Variable (all,r,REG)
    fincome(r) # factor income at market prices net of depreciation #;
Equation FACTORINCOME
# factor income at market prices net of depreciation #
(all,r,REG)
    FY(r) * fincome(r)
        = sum(i,ENDW_COMM, VOM(i,r) * [pm(i,r) + qo(i,r)])
        - VDEP(r)  * [pcgds(r) + kb(r)];

Variable (change)(all,r,REG)
    del_indtaxr(r) # change in ratio of indirect taxes to INCOME in r #;
Equation DINDTAXRATIO
# change in ratio of indirect taxes to INCOME in r #
(all,r,REG)
    del_indtaxr(r)
        = del_taxrpc(r) + del_taxrgc(r) + del_taxriu(r) + del_taxrfu(r)
        + del_taxrout(r) + del_taxrexp(r) + del_taxrimp(r);

Variable (change)(all,r,REG)
    del_ttaxr(r) # change in ratio of taxes to INCOME in r #;
Equation DTAXRATIO
# change in ratio of taxes to INCOME in r #
(all,r,REG)
    del_ttaxr(r)
        = del_taxrpc(r) + del_taxrgc(r) + del_taxriu(r) + del_taxrfu(r)
        + del_taxrout(r) + del_taxrexp(r) + del_taxrimp(r) + del_taxrinc(r);
!<
   This variable can be swapped with the commodity- and source-generic
   consumption tax shift, tp(r), in order to generate a tax replacement
   scenario, whereby taxes remain a constant share of national income.
>!

Variable (all,r,REG)
    incomeslack(r) # slack variable in the expression for regional income #;
!<
    This is exogenous, unless the user wishes to fix regional income.
>!

![[!  !]]!

! Payments for Ecosystems Module !
! This module uses regional income shifters in order to simulate payments across
  countries. It requires exogenous information on amount of land reduction
  payments per hectare for land endowment reduction for each region 
  as well as the contribution of each region to the global pool used 
  to compenstate for the land reduction !

!--- Initialize ODA data --- !

Coefficient (parameter) (all,r,REG)                     PESHR(r)
    # share of PES contribution of region r #;
    Read PESHR from file GTAPDATA header "SPES";
    
Coefficient (parameter) (all,i,AEZ_COMM)(all,r,REG)   PESha(i,r)
    # cost per hectare of PES to region r #;
    Read PESha from file gtapSUPP header "PESH";

Variable (change)(all,r,REG)                       pes_inflow(r) 
   # inflow of PES needed in region r #;

Variable (change) (all,r,REG)                    pes_outflow(r) 
   # regional PES contribution #;
   ! Total change in PES in region r is endogenous and depends on the 
     global amount of PES and region r's contribution!   
!]]!

Variable (change)                                 pes_outflow_wld 
   # total global PES needs #;
   ! Total change in PES in region r endogenous and depends on the 
     amout of land being reduced in region r!   

![[! August 11, 2022 (EC): 
    Reformulate the SHARE of each donor country to the PES global pool as a     
    proportion of their incomes instead of fixed equal shares. We first define  
    the elements of the donor country SET at run-time (based on previous equal  
    share value), then derive donor country contribution shares in proportion to
    their initial incomes.                                                      
 !]]!
Set 
    PESDONORS # PES donor countries # = (all,r,REG:PESHR(r) > 0.0);
Formula (initial)(all,r,REG)
    PESHR(r) = 0.0 + IF[r in PESDONORS, INCOME(r)/sum{rr,PESDONORS, INCOME(rr)}];  
Write 
    (set) PESDONORS to file GTAPSUM header "PESD";
    PESHR  to file GTAPSUM header "PESH";
    INCOME to file GTAPSUM header "INCR";

![[! August 11, 2022 (EC): End of changes                                   !]]!    

Equation E_pes_contribution (all,r,REG) 
    pes_outflow(r) = PESHR(r) * pes_outflow_wld;

Equation E_pes_balance
     sum(r, REG, pes_inflow(r)) = sum(r, REG, pes_outflow(r));

Coefficient (all,r,REG)
    INDTAX(r) # indirect tax receipts in r #;
Formula (all,r,REG)
    INDTAX(r) = TPC(r) + TGC(r) + TIU(r) + TFU(r) + TOUT(r) + TEX(r) + TIM(r);
!
Equation REGIONALINCOME
# regional income = sum of primary factor income and indirect tax receipts #
(all,r,REG)
    INCOME(r) * y(r)
        = FY(r) * fincome(r)
        + 100.0 * INCOME(r) * del_indtaxr(r)
        + INDTAX(r) * y(r)
        + INCOME(r) * incomeslack(r);!

Equation REGIONALINCOME
# modified regional income = sum of primary factor income and indirect tax receipts #
(all,r,REG)
    INCOME(r) * y(r)
        = FY(r) * fincome(r)
        + 100.0 * INCOME(r) * del_indtaxr(r)
        + INDTAX(r) * y(r)
        + INCOME(r) * incomeslack(r) + [100*pes_inflow(r) - 100*pes_outflow(r)];

!<
    This equation computes regional income as the sum of primary factor
    payment and indirect tax receipts.  The first term computes the change in
    endowment income, net of depreciation.  The subsequent terms compute the
    change in indirect tax receipts for various transactions taxes.
>!


!<
    7-3. Regional Household Demand System
    -------------------------------------
>!

Variable (all,r,REG)
    dpav(r) # average distribution parameter shift, for EV calc. #;
Equation DPARAV
# average distribution parameter shift #
(all,r,REG)
    dpav(r)
        = XSHRPRIV(r) * dppriv(r)
        + XSHRGOV(r) * dpgov(r)
        + XSHRSAVE(r) * dpsave(r);

Equation UTILITELASTIC
# elasticity of cost of utility wrt utility #
(all,r,REG)
    uelas(r) = XSHRPRIV(r) * uepriv(r) - dpav(r);

Equation PRIVCONSEXP
# private consumption expenditure #
(all,r,REG)
    yp(r) - y(r) = -[uepriv(r) - uelas(r)] + dppriv(r);

Equation GOVCONSEXP
# government consumption expenditure #
(all,r,REG)
    yg(r) - y(r) = uelas(r) + dpgov(r);

Equation SAVING
# saving #
(all,r,REG)
    psave(r) + qsave(r) - y(r) = uelas(r) + dpsave(r);


!<
    7-4. Aggregate Utility
    ----------------------
>!

Variable (all,r,REG)
    p(r) # price index for disposition of income by regional household #;
Equation PRICEINDEXREG
    # price index for disposition of income by regional household #
    (all,r,REG)
    p(r)
        = XSHRPRIV(r) * ppriv(r)
        + XSHRGOV(r) * pgov(r)
        + XSHRSAVE(r) * psave(r);

Variable (all,r,REG)
    au(r) # input-neutral shift in utility function #;
Variable (all,r,REG)
    dpsum(r) # sum of the distribution parameters #;

Coefficient (all,r,REG)
    DPARSUM(r) # sum of distribution parameters #;
Read
    DPARSUM from file GTAPDATA header "DPSM";
Update (all,r,REG)
    DPARSUM(r) = dpsum(r);

Coefficient (all,r,REG)
    UTILELAS(r) # elasticity of cost of utility wrt utility #;
Formula (all,r,REG)
    UTILELAS(r)
        = [UELASPRIV(r) * XSHRPRIV(r) + XSHRGOV(r) + XSHRSAVE(r)] / DPARSUM(r);

Coefficient (all,r,REG)
    DPARPRIV(r) # private consumption distribution parameter #;
Formula (all,r,REG)
    DPARPRIV(r) = UELASPRIV(r) * XSHRPRIV(r) / UTILELAS(r);

Coefficient (all,r,REG)
    DPARGOV(r) # government consumption distribution parameter #;
Formula (all,r,REG)
    DPARGOV(r) = XSHRGOV(r) / UTILELAS(r);

Coefficient (all,r,REG)
    DPARSAVE(r) # saving distribution parameter #;
Formula (all,r,REG)
    DPARSAVE(r) = XSHRSAVE(r) / UTILELAS(r);

Coefficient (all,r,REG)
    UTILPRIV(r) # utility from private consumption #;
Formula (initial)(all,r,REG)
    UTILPRIV(r) = 1.0;
Update (all,r,REG)
    UTILPRIV(r) = up(r);

Coefficient (all,r,REG)
    UTILGOV(r) # utility from government consumption #;
Formula (initial)(all,r,REG)
    UTILGOV(r) = 1.0;
Update (all,r,REG)
    UTILGOV(r) = ug(r);

Coefficient (all,r,REG)
    UTILSAVE(r) # utility from saving #;
Formula (initial)(all,r,REG)
    UTILSAVE(r) = 1.0;
Update (change) (all,r,REG)
    UTILSAVE(r) = [[qsave(r) - pop(r)] / 100] * UTILSAVE(r);

! EC (Aug 2022): Add orig_level statement in u !
! Variable (all,r,REG) !
Variable (Orig_level=INCOMEEV)(all,r,REG)
    u(r) # per capita utility from aggregate hhld expend. in region r #;
! EC (Aug 2022): End of changes !
Equation UTILITY
# regional household utility #
(all,r,REG)
    u(r)
        = au(r)
        + DPARPRIV(r) * loge(UTILPRIV(r)) * dppriv(r)
        + DPARGOV(r) * loge(UTILGOV(r)) * dpgov(r)
        + DPARSAVE(r) * loge(UTILSAVE(r)) * dpsave(r)
        + [1.0 / UTILELAS(r)] * [y(r) - pop(r) - p(r)];

Equation DISTPARSUM
# sum of the distribution parameters #
(all,r,REG)
    DPARSUM(r) * dpsum(r)
        = DPARPRIV(r) * dppriv(r)
        + DPARGOV(r) * dpgov(r)
        + DPARSAVE(r) * dpsave(r);


!<
    -------------------------
    8. Equilibrium Conditions
    -------------------------

    8-1. Market Clearing Conditions
    8-2. Walras' Law
>!


!<
    8-1. Market Clearing Conditions
    -------------------------------
>!

Coefficient (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    SHRDFM(i,j,r) # share of dom. prod. i used by sector j in r at mkt prices #;
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    SHRDFM(i,j,r) = VDFM(i,j,r) / VDM(i,r);

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    SHRDPM(i,r) # share of domestic prod. of i used by private hhlds in r #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    SHRDPM(i,r) = VDPM(i,r) / VDM(i,r);

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    SHRDGM(i,r) # share of imports of i used by gov't hhlds in r #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    SHRDGM(i,r) = VDGM(i,r) / VDM(i,r);

Variable (orig_level=VDM)(all,i,TRAD_COMM)(all,r,REG)
    qds(i,r) # domestic sales of commodity i in r #;
Equation MKTCLDOM
# eq'n assures market clearing for domestic sales (HT 3) #
(all,i,TRAD_COMM)(all,r,REG)
    qds(i,r)
        = sum(j,PROD_COMM, SHRDFM(i,j,r) * qfd(i,j,r))
        + SHRDPM(i,r) * qpd(i,r)
        + SHRDGM(i,r) * qgd(i,r);

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    SHRDM(i,r) # share of domestic sales of i in r #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    SHRDM(i,r) = VDM(i,r) / VOM(i,r);

Coefficient (all,m,MARG_COMM)(all,r,REG)
    SHRST(m,r) # share of sales of m to global transport services in r #;
Formula (all,m,MARG_COMM)(all,r,REG)
    SHRST(m,r) = VST(m,r) / VOM(m,r);

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    SHRXMD(i,r,s) # share of export sales of i to s in r #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    SHRXMD(i,r,s) = VXMD(i,r,s) / VOM(i,r);

Variable (all,i,TRAD_COMM)(all,r,REG)
    tradslack(i,r) # slack variable in tradeables market clearing condition #;
!<
    This is exogenous unless the user wishes to specify the price of
    tradeables exogenously, in which case the analysis becomes partial
    equilibrium and walraslack must be exogenized.
>!

Equation MKTCLTRD_MARG
# eq'n assures market clearing for margins commodities (HT 1) #
(all,m,MARG_COMM)(all,r,REG)
    qo(m,r)
        = SHRDM(m,r) * qds(m,r)
        + SHRST(m,r) * qst(m,r)
        + sum(s,REG, SHRXMD(m,r,s) * qxs(m,r,s))
        + tradslack(m,r);
Equation MKTCLTRD_NMRG
# eq'n assures market clearing for the non-margins commodities (HT 1) #
(all,i,NMRG_COMM)(all,r,REG)
    qo(i,r)
        = SHRDM(i,r) * qds(i,r)
        + sum(s,REG, SHRXMD(i,r,s) * qxs(i,r,s))
        + tradslack(i,r);

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VIM(i,r) # value of imports of commodity i in r at domestic market prices #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    VIM(i,r) = sum(j,PROD_COMM, VIFM(i,j,r)) + VIPM(i,r) + VIGM(i,r);

Coefficient (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    SHRIFM(i,j,r) # share of import i used by sector j in r  #;
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    SHRIFM(i,j,r) = VIFM(i,j,r) / VIM(i,r);

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    SHRIPM(i,r) # share of import i used by private hhlds in r #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    SHRIPM(i,r) = VIPM(i,r) / VIM(i,r);

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    SHRIGM(i,r) # the share of import i used by gov't hhlds in r #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    SHRIGM(i,r) = VIGM(i,r) / VIM(i,r);

Equation MKTCLIMP
# eq'n assures mkt clearing for imported goods entering each region (HT 2) #
(all,i,TRAD_COMM)(all,r,REG)
    qim(i,r)
        = sum(j,PROD_COMM, SHRIFM(i,j,r) * qfm(i,j,r))
        + SHRIPM(i,r) * qpm(i,r)
        + SHRIGM(i,r) * qgm(i,r);

Coefficient (all,i,ENDWM_COMM)(all,j,PROD_COMM)(all,r,REG)
    SHREM(i,j,r) # share of mobile endowment i used by sector j at mkt prices #;
Formula (all,i,ENDWM_COMM)(all,j,PROD_COMM)(all,r,REG)
    SHREM(i,j,r) = VFM(i,j,r) / VOM(i,r);

Equation MKTCLENDWM
# eq'n assures mkt clearing for perfectly mobile endowments in each r (HT 4) #
(all,i,ENDWM_COMM)(all,r,REG)
    qo(i,r) = sum(j,PROD_COMM, SHREM(i,j,r) * qfe(i,j,r)) + endwslack(i,r);
!<
    This equation assures market clearing for perfectly mobile endowments (HT
    4)
>!

Equation MKTCLENDWS
# eq'n assures mkt clearing for imperfectly mobile endowments in each r (HT 5) #
(all,i,ENDWS_COMM)(all,j,PROD_COMM)(all,r,REG)
    qoes(i,j,r) = qfe(i,j,r);
!<
    This equation assures market clearing for sluggish endowments (HT 5)
>!


!<
    8-2. Walras' Law
    ----------------
>!

Variable
    walras_sup # supply in omitted market--global supply of cgds composite #;
Equation WALRAS_S
# Extra eq'n computes change in supply in the omitted market. #
    walras_sup = pcgdswld + globalcgds;
!<
    This is an extra equation which simply computes change in supply in the
    omitted market.  (modified from HT 12 to reflect the value as opposed to
    quantity change, in light of the new treatment of psave(r))
>!

Variable
    walras_dem # demand in the omitted market--global demand for savings #;
Equation WALRAS_D
# Extra eq'n computes change in demand in the omitted market. #
    GLOBINV * walras_dem = sum(r,REG, SAVE(r) * [psave(r) + qsave(r)]);
!<
    This is an extra equation which simply computes change in demand in the
    omitted market.  (modified from HT 13 to reflect the value as opposed to
    quantity change)
>!

Variable
    walraslack # slack variable in the omitted market #;
!<
    This is endogenous under normal, GE closure.  If the GE links are broken,
    then this must be swapped with the numeraire, thereby forcing global
    savings to explicitly equal global investment.
>!
Equation WALRAS
# Check Walras' Law.  Value of "walraslack" should be zero. (HT 14) #
    walras_sup = walras_dem + walraslack;
!<
    This equation checks Walras' Law.  The value of walraslack should be zero
    in any GE simulation.  (HT 14)
>!


!<
    ==========
    Appendices
    ==========

    A. Summary Indices
    B. Equivalent Variation
    C. Welfare Decomposition
    D. Terms of Trade Decomposition
>!


!<
    ------------------
    A. Summary Indices
    ------------------

    The following equations calculate many useful summary statistics.  They do
    not generally affect the equilibrium structure of the model, although they
    do include the equation for the usual numeraire variable, "pfactwld". Some
    are documented in Hertel and Tsigas, many are new.

    A-0. Appendix-Specific Variables and Coefficients
    A-1. Factor Price Indices
    A-2. Regional Terms of Trade
    A-3. GDP Indices (Value, Price and Quantity)
    A-4. Aggregate Trade Indices (Value, Price and Quantity)
    A-5. Trade Balance Indices
>!


!<
    A-0. Appendix-Specific Variables and Coefficients
    -------------------------------------------------

    only used in this Summary Indices appendix
>!

Variable (all,i,TRAD_COMM)(all,s,REG)
    vxwfob(i,s) # value of merchandise regional exports, by commodity, FOB #;
Variable (all,i,TRAD_COMM)(all,s,REG)
    viwcif(i,s) # value of merchandise regional imports, by commodity, CIF #;
Variable (all,r,REG)
    vxwreg(r) # value of merchandise exports, by region #;
Variable (all,r,REG)
    viwreg(r) # value of merchandise imports, by region, at world prices #;

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VXW(i,r) # value of exports by comm. i and region r at FOB prices #;
Formula (all,m,MARG_COMM)(all,r,REG)
    VXW(m,r) = sum(s,REG, VXWD(m,r,s)) + VST(m,r);
Formula (all,i,NMRG_COMM)(all,r,REG)
    VXW(i,r) = sum(s,REG, VXWD(i,r,s));

Coefficient (all,r,REG)
    VXWREGION(r) # value of exports by region r at FOB prices #;
Formula (all,r,REG)
    VXWREGION(r) = sum(i,TRAD_COMM, VXW(i,r));

Coefficient (all,i,TRAD_COMM)(all,s,REG)
    VIW(i,s) # value of commodity imports i into s at CIF prices #;
Formula (all,i,TRAD_COMM)(all,s,REG)
    VIW(i,s) = sum(r,REG, VIWS(i,r,s));
Coefficient (all,r,REG)
    VIWREGION(r) # value of commodity imports by region r at CIF prices #;
Formula (all,r,REG)
    VIWREGION(r) = sum(i,TRAD_COMM, VIW(i,r));


!<
    A-1. Factor Price Indices
    -------------------------
>!

Variable (orig_level=1.0)(all,i,ENDW_COMM)(all,r,REG)
    pfactreal(i,r) # ratio of return to primary factor i to CPI in r #;
Equation REALRETURN
# eq'n defines the real rate of return to primary factor i in region r #
(all,i,ENDW_COMM)(all,s,REG)
    pfactreal(i,s) = pm(i,s) - ppriv(s);
!<
    This equation defines the real rate of return to primary factor i in
    region r (new).
>!

Coefficient (all,r,REG)
    VENDWREG(r) # value of primary factors, at mkt prices, by region #;
Formula (all,r,REG)
    VENDWREG(r) = sum(i,ENDW_COMM, VOM(i,r));

Variable (orig_level=1.0)(all,r,REG)
    pfactor(r) # market price index of primary factors, by region #;
Equation PRIMFACTPR
# computes % change in price index of primary factors, by region #
(all,r,REG)
    VENDWREG(r) * pfactor(r) = sum(i,ENDW_COMM, VOM(i,r) * pm(i,r));

Coefficient
    VENDWWLD # value of primary factors, at mkt prices, worldwide #;
Formula
    VENDWWLD = sum(r,REG, VENDWREG(r));
Variable (orig_level=1.0)
    pfactwld # world price index of primary factors #;
Equation PRIMFACTPRWLD
# computes % change in global price index of primary factors #
    VENDWWLD * pfactwld = sum(r,REG, VENDWREG(r) * pfactor(r));


!<
    A-2. Regional Terms of Trade
    ----------------------------

    The next three equations correspond to Table 10 of Hertel and Tsigas on
    Regional Terms of Trade.
>!

Variable (orig_level=1.0)(all,r,REG)
    psw(r) # index of prices received for tradeables produced in r #;
Equation REGSUPRICE
# estimate change in index of prices received for tradeables i produced in r #
(all,r,REG)
    VXWREGION(r) * psw(r)
        = sum(i,TRAD_COMM, sum(s,REG, VXWD(i,r,s) * pfob(i,r,s)))
        + sum(m,MARG_COMM, VST(m,r) * pm(m,r));
!<
    This equation estimates the change in the index of prices received for
    tradeable products produced in r.  (modified from HT 64 to eliminate the
    investment component)
>!

Variable (orig_level=1.0)(all,r,REG)
    pdw(r) # index of prices paid for tradeables used in region r #;
Equation REGDEMPRICE
# estimate change in index of prices paid for tradeable products used in r #
(all,r,REG)
    VIWREGION(r) * pdw(r)
        = sum(i,TRAD_COMM, sum(k,REG, VIWS(i,k,r) * pcif(i,k,r)));
!<
    This equation estimates the change in the index of prices paid for
    tradeable products used in r.  (modified from HT 65 to eliminate savings)
>!

Variable (orig_level=1.0)(all,r,REG)
    tot(r) # terms of trade for region r: tot(r) = psw(r) - pdw(r) #;
Equation TOTeq
# terms of trade equation computed as difference in psw and pdw (HT 66) #
(all,r,REG)
    tot(r) = psw(r) - pdw(r);


!<
    A-3. GDP Indices (Value, Price and Quantity)
    --------------------------------------------
>!

Coefficient (all,r,REG)
    GDP(r) # Gross Domestic Product in region r #;
Formula (all,s,REG)
    GDP(s)
        = sum(i,TRAD_COMM, VPA(i,s))
        + sum(i,TRAD_COMM, VGA(i,s))
        + sum(k,CGDS_COMM, VOA(k,s))
        + sum(i,TRAD_COMM, sum(r,REG, VXWD(i,s,r)))
        + sum(m,MARG_COMM, VST(m,s))
        - sum(i,TRAD_COMM, sum(r,REG, VIWS(i,r,s)));
!<
    Gross Domestic Product in region r.  Trade is valued at FOB and CIF
    prices.
>!

Variable (all,r,REG)
    vgdp(r) # change in value of GDP #;
Equation VGDP_r
# change in value of GDP (HT 70) #
(all,r,REG)
    GDP(r) * vgdp(r)
        = sum(i,TRAD_COMM, VGA(i,r) * [qg(i,r) + pg(i,r)])
        + sum(i,TRAD_COMM, VPA(i,r) * [qp(i,r) + pp(i,r)])
        + REGINV(r) * [qcgds(r) + pcgds(r)]
        + sum(i,TRAD_COMM, sum(s,REG, VXWD(i,r,s) * [qxs(i,r,s) + pfob(i,r,s)]))
        + sum(m,MARG_COMM, VST(m,r) * [qst(m,r) + pm(m,r)])
        - sum(i,TRAD_COMM, sum(s,REG,
            VIWS(i,s,r) * [qxs(i,s,r) + pcif(i,s,r)]));

Variable (orig_level=1.0)(all,r,REG)
    pgdp(r) # GDP price index #;
Equation PGDP_r
# GDP price index (HT 71) #
(all,r,REG)
    GDP(r) * pgdp(r)
        = sum(i,TRAD_COMM, VGA(i,r) * pg(i,r))
        + sum(i,TRAD_COMM, VPA(i,r) * pp(i,r))
        + REGINV(r) * pcgds(r)
        + sum(i,TRAD_COMM, sum(s,REG, VXWD(i,r,s) * pfob(i,r,s)))
        + sum(m,MARG_COMM, VST(m,r) * pm(m,r))
        - sum(i,TRAD_COMM, sum(s,REG, VIWS(i,s,r) * pcif(i,s,r)));

Variable (orig_level=GDP)(all,r,REG)
    qgdp(r) # GDP quantity index #;
Equation QGDP_r
# GDP quantity index #
(all,r,REG)
    GDP(r) * qgdp(r)
        = sum(i,TRAD_COMM, VGA(i,r) * qg(i,r))
        + sum(i,TRAD_COMM, VPA(i,r) * qp(i,r))
        + REGINV(r) * qcgds(r)
        + sum(i,TRAD_COMM, sum(s,REG, VXWD(i,r,s) * qxs(i,r,s)))
        + sum(m,MARG_COMM, VST(m,r) * qst(m,r))
        - sum(i,TRAD_COMM, sum(s,REG, VIWS(i,s,r) * qxs(i,s,r)));
!<
    modified from HT 72 for use with AnalyzeGE
>!

Variable (all,i,PROD_COMM)(all,r,REG)
    compvalad(i,r) # composition of value added for good i and region r #;
Equation COMPVALADEQ
# track change in composition of value added #
(all,i,PROD_COMM)(all,r,REG)
    compvalad(i,r) = qo(i,r) - qgdp(r);


!<
    A-4. Aggregate Trade Indices (Value, Price and Quantity)
    --------------------------------------------------------
>!


!<
    Common Variables and Coefficients
    Value Indices for Aggregate Trade
    Price Indices for Aggregate Trade
    Quantity Indices for Aggregate Trade
>!


!<
    Common Variables and Coefficients
    .................................

    only used in this Aggregate Trade Indices section
>!

Variable (orig_level=1.0)(all,i,TRAD_COMM)(all,r,REG)
    pxw(i,r) # aggregate exports price index of i from region r #;

Coefficient (all,i,TRAD_COMM)
    VXWCOMMOD(i) # value of world exports by commodity i at FOB prices #;
Formula (all,i,TRAD_COMM)
    VXWCOMMOD(i) = sum(r,REG, VXW(i,r));

Coefficient
    VXWLD # value of commodity exports, FOB, globally #;
Formula
    VXWLD = sum(r,REG, VXWREGION(r));

Coefficient (all,i,TRAD_COMM)
    VIWCOMMOD(i) # global value of commodity imports, CIF, by commodity #;
Formula (all,i,TRAD_COMM)
    VIWCOMMOD(i) = sum(r,REG, VIW(i,r));

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    PW_PM(i,r) # ratio of world to domestic prices #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    PW_PM(i,r) = sum(s,REG, VXWD(i,r,s)) / sum(s,REG, VXMD(i,r,s));
Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VOW(i,r) # value of output in r at FOB including transportation services #;
Formula (all,m,MARG_COMM)(all,r,REG)
    VOW(m,r) = VDM(m,r) * PW_PM(m,r) + sum(s,REG, VXWD(m,r,s)) + VST(m,r);
Formula (all,i,NMRG_COMM)(all,r,REG)
    VOW(i,r) = VDM(i,r) * PW_PM(i,r) + sum(s,REG, VXWD(i,r,s));

Coefficient (all,i,TRAD_COMM)
    VWOW(i) # value of world supply at world prices for i #;
Formula (all,i,TRAD_COMM)
    VWOW(i) = sum(r,REG, VOW(i,r));

Coefficient (all,i,TRAD_COMM)
    VWOU(i) # value of world output of i at user prices #;
Formula (all,i,TRAD_COMM)
    VWOU(i) = sum(s,REG, [VPA(i,s) + VGA(i,s)] + sum(j,PROD_COMM, VFA(i,j,s)));


!<
    Value Indices for Aggregate Trade
    .................................
>!

Equation VREGEX_ir_MARG
# the change in FOB value of exports of m from r #
(all,m,MARG_COMM)(all,r,REG)
    VXW(m,r) * vxwfob(m,r)
        = sum(s,REG, VXWD(m,r,s) * [qxs(m,r,s) + pfob(m,r,s)])
        + VST(m,r) * [qst(m,r) + pm(m,r)];
Equation VREGEX_ir_NMRG
# the change in FOB value of exports of commodity i from r (HT 73) #
(all,i,NMRG_COMM)(all,r,REG)
    VXW(i,r) * vxwfob(i,r)
        = sum(s,REG, VXWD(i,r,s) * [qxs(i,r,s) + pfob(i,r,s)]);

Equation VREGEX_r
# computes % change in value of merchandise exports, by region (HT 75) #
(all,r,REG)
    VXWREGION(r) * vxwreg(r) = sum(i,TRAD_COMM, VXW(i,r) * vxwfob(i,r));

Variable (all,i,TRAD_COMM)
    vxwcom(i) # value of global merchandise exports by commodity #;
Equation VWLDEX_i
# computes % change in FOB value of global exports, by commodity (HT 77) #
(all,i,TRAD_COMM)
    VXWCOMMOD(i) * vxwcom(i) = sum(r,REG, VXW(i,r) * vxwfob(i,r));

Variable
    vxwwld # value of world trade #;
Equation VWLDEX
# computes % change in value of global exports (HT 79) #
    VXWLD * vxwwld = sum(r,REG, VXWREGION(r) * vxwreg(r));

Equation VREGIM_is
# the change in CIF value of imports of commodity i into s (HT 74) #
(all,i,TRAD_COMM)(all,s,REG)
    VIW(i,s) * viwcif(i,s)
        = sum(r,REG, VIWS(i,r,s) * [pcif(i,r,s) + qxs(i,r,s)]);

Equation VREGIM_s
# computes % change in value of imports, CIF basis, by region (HT 76) #
(all,s,REG)
    VIWREGION(s) * viwreg(s) = sum(i,TRAD_COMM, VIW(i,s) * viwcif(i,s));

Variable (all,i,TRAD_COMM)
    viwcom(i) # value of global merchandise imports i, at world prices #;
Equation VWLDIM_i
# computes % change in value of global imports, by commodity (HT 78) #
(all,i,TRAD_COMM)
    VIWCOMMOD(i) * viwcom(i) = sum(s,REG, VIW(i,s) * viwcif(i,s));

Variable (all,i,TRAD_COMM)
    valuew(i) # value of world supply of good i #;
Equation VWLDOUT
# change in value of world output of comm. i at FOB prices (HT 80) #
(all,i,TRAD_COMM)
    VWOW(i) * valuew(i) = sum(r,REG, VOW(i,r) * [pxw(i,r) + qo(i,r)]);

Variable (all,i,TRAD_COMM)
    valuewu(i) # value of world supply of good i at user prices #;
Equation VWLDOUTUSE
# change in value of world output of commodity i at user prices #
(all,i,TRAD_COMM)
    VWOU(i) * valuewu(i)
        = sum(s,REG, VPA(i,s) * [pp(i,s) + qp(i,s)]
        + VGA(i,s) * [pg(i,s) + qg(i,s)]
        + sum(j,PROD_COMM, VFA(i,j,s) * [pf(i,j,s) + qf(i,j,s)]));


!<
    Price Indices for Aggregate Trade
    .................................
>!

Equation PREGEX_ir_MARG
# change in FOB price index of exports of m from r #
(all,m,MARG_COMM)(all,r,REG)
    VXW(m,r) * pxw(m,r)
        = sum(s,REG, VXWD(m,r,s) * pfob(m,r,s)) + VST(m,r) * pm(m,r);

Equation PREGEX_ir_NMRG
# change in FOB price index of exports of commodity i from r (HT 81) #
(all,i,NMRG_COMM)(all,r,REG)
    VXW(i,r) * pxw(i,r) = sum(s,REG, VXWD(i,r,s) * pfob(i,r,s));

Variable (orig_level=1.0)(all,r,REG)
    pxwreg(r) # price index of merchandise exports, by region #;
Equation PREGEX_r
# computes % change in price index of exports, by region (HT 83) #
(all,r,REG)
    VXWREGION(r) * pxwreg(r) = sum(i,TRAD_COMM, VXW(i,r) * pxw(i,r));

Variable (orig_level=1.0)(all,i,TRAD_COMM)
    pxwcom(i) # price index of global merchandise exports by commodity #;
Equation PWLDEX_i
# computes % change in price index of exports, by commodity (HT 85) #
(all,i,TRAD_COMM)
    VXWCOMMOD(i) * pxwcom(i) = sum(r,REG, VXW(i,r) * pxw(i,r));

Variable (orig_level=1.0)
    pxwwld # price index of world trade #;
Equation PWLDEX
# computes % change in price index of global exports (HT 87) #
    VXWLD * pxwwld = sum(r,REG, VXWREGION(r) * pxwreg(r));

Variable (all,i,TRAD_COMM)(all,r,REG)
    piw(i,r) # world price of composite import i in region r #;
Equation PREGIM_is
# change in CIF price index of imports of commodity i into s (HT 82) #
(all,i,TRAD_COMM)(all,s,REG)
    VIW(i,s) * piw(i,s) = sum(r,REG, VIWS(i,r,s) * pcif(i,r,s));

Variable (orig_level=1.0)(all,r,REG)
    piwreg(r) # price index of merchandise imports, by region #;
Equation PREGIM_s
# computes % change in price index of imports, by region (HT 84) #
(all,s,REG)
    VIWREGION(s) * piwreg(s) = sum(i,TRAD_COMM, VIW(i,s) * piw(i,s));

Variable (orig_level=1.0)(all,i,TRAD_COMM)
    piwcom(i) # price index of global merchandise imports by commodity #;
Equation PWLDIM_i
# computes % change in price index of imports, by commodity (HT 86) #
(all,i,TRAD_COMM)
    VIWCOMMOD(i) * piwcom(i) = sum(s,REG, VIW(i,s) * piw(i,s));

Variable (all,i,TRAD_COMM)
    pw(i) # world price index for total good i supplies #;
Equation PWLDOUT
# change in index of world prices, FOB, for total production of i (HT 88) #
(all,i,TRAD_COMM)
    VWOW(i) * pw(i) = sum(r,REG, VOW(i,r) * pxw(i,r));

Variable (orig_level=1.0)(all,i,TRAD_COMM)
    pwu(i) # world price index for total good i supplies at user prices #;
Equation PWLDUSE
# change in index of user prices for deflating world production of i #
(all,i,TRAD_COMM)
    VWOU(i) * pwu(i)
        = sum(s,REG, VPA(i,s) * pp(i,s)
        + VGA(i,s) * pg(i,s)
        + sum(j,PROD_COMM, VFA(i,j,s) * pf(i,j,s)));


!<
    Quantity Indices for Aggregate Trade
    ....................................
>!

Variable (orig_level=VXW)(all,i,TRAD_COMM)(all,r,REG)
    qxw(i,r) # aggregate exports of i from region r, FOB weights #;
Equation QREGEX_ir_MARG
# change in volume of exports of margin commodity m from r #
(all,m,MARG_COMM)(all,r,REG)
    VXW(m,r) * qxw(m,r)
        = sum(s,REG, VXWD(m,r,s) * qxs(m,r,s)) + VST(m,r) * qst(m,r);
Equation QREGEX_ir_NMRG
# change in volume of exports of non-margin commodity i from r #
(all,i,NMRG_COMM)(all,r,REG)
    VXW(i,r) * qxw(i,r) = sum(s,REG, VXWD(i,r,s) * qxs(i,r,s));
!<
    modified from HT 89 for use with AnalyzeGE
>!

Variable (orig_level=VXWREGION)(all,r,REG)
    qxwreg(r) # volume of merchandise exports, by region #;
Equation QREGEX_r
# computes % change in quantity index of exports, by region #
(all,r,REG)
    VXWREGION(r) * qxwreg(r) = sum(i,TRAD_COMM, VXW(i,r) * qxw(i,r));
!<
    modified from HT 91 for use with AnalyzeGE
>!

Variable (orig_level=VXWCOMMOD)(all,i,TRAD_COMM)
    qxwcom(i) # volume of global merchandise exports by commodity #;
Equation QWLDEX_i
# computes % change in quantity index of exports, by commodity #
(all,i,TRAD_COMM)
    VXWCOMMOD(i) * qxwcom(i) = sum(r,REG, VXW(i,r) * qxw(i,r));
!<
    modified from HT 93 for use with AnalyzeGE
>!

Variable (orig_level=VXWLD)
    qxwwld # volume of world trade #;
Equation QWLDEX
# computes % change in quantity index of global exports #
    VXWLD * qxwwld = sum(r,REG, VXWREGION(r) * qxwreg(r));
!<
    modified from HT 95 for use with AnalyzeGE
>!

Variable (all,i,TRAD_COMM)(all,s,REG)
    qiw(i,s) # aggregate imports of i into region s, CIF weights #;
Equation QREGIM_is
# change in volume of imports of commodity i into s #
(all,i,TRAD_COMM)(all,s,REG)
    VIW(i,s) * qiw(i,s) = sum(r,REG, VIWS(i,r,s) * qxs(i,r,s));
!<
    modified from HT 90 for use with AnalyzeGE
>!

Variable (orig_level=VIWREGION)(all,r,REG)
    qiwreg(r) # volume of merchandise imports, by region #;
Equation QREGIM_s
# computes % change in quantity index of imports, by region #
(all,s,REG)
    VIWREGION(s) * qiwreg(s) = sum(i,TRAD_COMM, VIW(i,s) * qiw(i,s));
!<
    modified from HT 92 for use with AnalyzeGE
>!

Variable (orig_level=VIWCOMMOD)(all,i,TRAD_COMM)
    qiwcom(i) # volume of global merchandise imports by commodity #;
Equation QWLDIM_i
# computes % change in quantity index of imports, by commodity #
(all,i,TRAD_COMM)
    VIWCOMMOD(i) * qiwcom(i) = sum(s,REG, VIW(i,s) * qiw(i,s));
!<
    modified from HT 94 for use with AnalyzeGE
>!

Variable (orig_level=VWOW) (all,i,TRAD_COMM)
    qow(i) # quantity index for world supply of good i #;
Equation QWLDOUT
# change in index of world production of i #
(all,i,TRAD_COMM)
    VWOW(i) * qow(i) = sum(r,REG, VOW(i,r) * qo(i,r));


Variable (all,r,REG)
    qo_crops(r) # quantity index for crop supply by region #;
Equation QCROPOUT 
# change in index of crop production of i by region #
(all,r,REG)
    qo_crops(r) = sum(i, CROP_COMM, [VOW(i,r)/ sum(j,CROP_COMM, VOW(j,r))] * qo(i,r));

Variable 
    qow_crops # quantity index for world crop supply #;
Equation QCROPWOUT 
# change in index of world crop production #
    qow_crops = 
    sum(r, REG, sum[i, CROP_COMM, VOW(i,r) / sum(j, REG, sum(k, CROP_COMM, VOW(k,j)))] * qo_crops(r));

Variable (all,r,REG)
    qo_live(r) # quantity index for crop supply by region #;
Equation QLIVEOUT 
# change in index of crop production of i by region #
(all,r,REG)
    qo_live(r) = 
  VOW("ruminant",r) / (VOW("ruminant",r) + VOW("nonruminant",r)) * qo("ruminant",r)  + 
  VOW("nonruminant",r) / (VOW("ruminant",r) + VOW("nonruminant",r)) * qo("nonruminant",r);

Variable 
    qow_live # quantity index for world crop supply #;
Equation QLIVEWOUT 
# change in index of world crop production #
    qow_live =
sum(r, REG, (VOW("ruminant",r) + VOW("nonruminant",r)) / sum(j, REG, (VOW("ruminant",j) + VOW("nonruminant",j))) * qo_live(r));
 

![[!
Variable 
    qo_live # quantity index for world livestock supply  #;
Equation QWLDliveOUT
# change in index of world production of i #
    qo_live = VOW("ruminant",r)/ [VOW("ruminant",r) + VOW("nonruminant",r)]
sum(i,CROP_COMM, sum(r,REG,  * qo(i,r)));
!]]!

!<
    modified from HT 96 for use with AnalyzeGE
>!

Variable (orig_level=VWOU)(all,i,TRAD_COMM)
    qowu(i) # quantity index for world supply of good i at user prices #;
Equation QWLDOUTU
# change in index of world production of i evaluated at user prices #
(all,i,TRAD_COMM)
    VWOU(i) * qowu(i)
        = sum(s,REG, VPA(i,s) * qp(i,s)
        + VGA(i,s) * qg(i,s)
        + sum(j,PROD_COMM, VFA(i,j,s) * qf(i,j,s)));


!<
    A-5. Trade Balance Indices
    --------------------------
>!

Variable (change)(all,i,TRAD_COMM)(all,r,REG)
    DTBALi(i,r) # change in trade balance by i and by r, $ US million #;
!<
    A positive value indicates that the change in exports exceeds the change
    in imports.
>!
Equation TRADEBAL_i
# computes change in trade balance by commodity and by region (HT 97) #
(all,i,TRAD_COMM)(all,r,REG)
    DTBALi(i,r)
        = [VXW(i,r) / 100] * vxwfob(i,r) - [VIW(i,r) / 100] * viwcif(i,r);

Variable (change)(all,r,REG)
    DTBAL(r) # change in trade balance X - M, $ US million #;
Equation TRADEBALANCE
# computes change in trade balance (X - M), by region (HT 98) #
(all,r,REG)
    DTBAL(r)
        = [VXWREGION(r) / 100] * vxwreg(r) - [VIWREGION(r) / 100] * viwreg(r);

!<
    In order to maintain homogeneity in the presence of a fixed trade balance,
    it is useful to have a nominal variable which this is measured against.
    The next equation provides this, and we recommend users fix DTBALR instead
    of fixing DTBAL in future simulations.  The strategy is the same one used
    above for taxes.
>!

Coefficient (all,r,REG)
    TBAL(r) # trade balance for region r #;
Formula (all,r,REG)
    TBAL(r) = VXWREGION(r) - VIWREGION(r);

Variable (change)(all,r,REG)
    DTBALR(r) # change in ratio of trade balance to regional income #;
Equation DTBALRATIO
# change in ratio of trade balance to regional income #
(all,r,REG)
    100 * INCOME(r) * DTBALR(r) = 100 * DTBAL(r) - TBAL(r) * y(r);


!<
    -----------------------
    B. Equivalent Variation
    -----------------------

    B-0. Appendix-Specific Variables and Coefficients
    B-1. Government Consumption Shadow Demand System
    B-2. Private Consumption Shadow Demand System
    B-3. Regional Household Shadow Demand System
    B-4. Equivalent Variation

    This appendix calculates equivalent variation "EV" and world equivalent
    variation, "WEV", by determining the income "yev" that would be required
    to achieve the current actual utility level "u" in a shadow demand system
    in which prices are fixed.

    Section B-2 calculates the utility elasticity of private consumption
    expenditure, "ueprivev", within a shadow demand system for private
    consumption, for use in section B-3.  B-3 calculates private consumption
    expenditure "ypev" for use in B-2, and regional income "yev" for use in
    B-4, within a shadow demand system for the regional household.  B-4
    calculates "EV" and "WEV".
>!


!<
    B-0. Appendix-Specific Variables and Coefficients
    -------------------------------------------------

    only used in this Equivalent Variation section of the Summary Indices
    appendix
>!

Variable (all,r,REG)
    uelasev(r)
    # elasticity of cost of utility wrt utility, for EV calc. #;
Variable (all,r,REG)
    ueprivev(r)
    # utility elasticity of private consn expenditure, for EV calc. #;
Variable (all,r,REG)
    ugev(r) # per capita utility from gov't expend., for EV calc. #;
Variable (all,r,REG)
    upev(r) # per capita utility from private expend., for EV calc. #;
Variable (all,r,REG)
    qsaveev(r) # total quantity of savings demanded, for EV calc. #;
Variable (all,r,REG)
    yev(r) # regional household income in region r, for EV calc. #;
Variable (all,r,REG)
    ypev(r)
    # private consumption expenditure in region r, for EV calc. #;
Variable (all,r,REG)
    ygev(r)
    # government consumption expenditure in region r, for EV calc. #;

Coefficient (all,r,REG)
    INCOMEEV(r) # regional income, for EV calc. #;
Formula (initial) (all,r,REG)
    INCOMEEV(r) = INCOME(r);
Update (all,r,REG)
    INCOMEEV(r) = yev(r);

Coefficient (all,r,REG)
    UTILPRIVEV(r) # utility from private consumption, for EV calcs #;
Formula (initial) (all,r,REG)
    UTILPRIVEV(r) = UTILPRIV(r);
Update (all,r,REG)
    UTILPRIVEV(r) = upev(r);

Coefficient (all,r,REG)
    UTILGOVEV(r) # utility from private consumption, for EV calcs #;
Formula (initial) (all,r,REG)
    UTILGOVEV(r) = UTILGOV(r);
Update (all,r,REG)
    UTILGOVEV(r) = ugev(r);

Coefficient (all,r,REG)
    UTILSAVEEV(r) # utility from private consumption, for EV calcs #;
Formula (initial) (all,r,REG)
    UTILSAVEEV(r) = UTILSAVE(r);
Update (change) (all,r,REG)
    UTILSAVEEV(r) = [[qsaveev(r) - pop(r)] / 100] * UTILSAVEEV(r);


!<
    B-1. Government Consumption Shadow Demand System
    ------------------------------------------------
>!

Equation GOVUSHD
# utility from government consumption in r #
(all,r,REG)
    ygev(r) - pop(r) = ugev(r);


!<
    B-2. Private Consumption Shadow Demand System
    ---------------------------------------------
>!

Variable (all,i,TRAD_COMM)(all,r,REG)
    qpev(i,r)
    # private hhld demand for commodity i in region r, for EV calc. #;

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    VPAEV(i,r)
    # private hhld expend. on i in r valued at agent's prices, for EV calc. #;
Formula (initial) (all,i,TRAD_COMM)(all,r,REG)
    VPAEV(i,r) = VPA(i,r);
Update (all,i,TRAD_COMM)(all,r,REG)
    VPAEV(i,r) = qpev(i,r);

Coefficient (all,r,REG)
    VPAREGEV(r) # private consumption expenditure in region r, for EV calc. #;
Formula (all,r,REG)
    VPAREGEV(r) = sum(i,TRAD_COMM, VPAEV(i,r));
!<
    VPAREGEV should agree with PRIVEXPEV.
>!

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    CONSHREV(i,r)
    # share of private hhld consn devoted to good i in r, for EV calc. #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    CONSHREV(i,r) = VPAEV(i,r) / VPAREGEV(r);
Coefficient (all,i,TRAD_COMM)(all,r,REG)
    EYEV(i,r)
    # expend. elast. of private hhld demand for i in r, for EV calc. #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    EYEV(i,r)
        = [1.0 / sum(n,TRAD_COMM, CONSHREV(n,r) * INCPAR(n,r))]
        * [INCPAR(i,r) * [1.0 - ALPHA(i,r)]
            + sum(n,TRAD_COMM, CONSHREV(n,r) * INCPAR(n,r) * ALPHA(n,r))]
        + ALPHA(i,r)
        - sum(n,TRAD_COMM, CONSHREV(n,r) * ALPHA(n,r));

Equation PRIVDMNDSEV
    # private hhld demands for composite commodities, for EV calc. #
    (all,i,TRAD_COMM)(all,r,REG)
    qpev(i,r) - pop(r) = EYEV(i,r) * [ypev(r) - pop(r)];
!<
    Prices are held constant for the EV calculation and so do not appear here.
>!

Coefficient (all,r,REG)
    UELASPRIVEV(r)
    # elast. of cost wrt utility from private consn, for EV calc. #;
Formula (all,r,REG)
    UELASPRIVEV(r) = sum(i,TRAD_COMM, CONSHREV(i,r) * INCPAR(i,r));

Equation PRIVATEUEV
# computation of utility from private consumption in r (HT 45) #
(all,r,REG)
    ypev(r) - pop(r) = UELASPRIVEV(r) * upev(r);

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    XWCONSHREV(i,r)
    # expansion-parameter-weighted consumption share, for EV calc. #;
Formula (all,i,TRAD_COMM)(all,r,REG)
    XWCONSHREV(i,r) = CONSHREV(i,r) * INCPAR(i,r) / UELASPRIVEV(r);

Equation UTILELASPRIVEV
    # elasticity of cost wrt utility from private consn, for EV calc. #
    (all,r,REG)
    ueprivev(r)
        = sum(i,TRAD_COMM, XWCONSHREV(i,r) * [qpev(i,r) - ypev(r)]);
!<
    Prices are held constant for the EV calculation and so do not appear here.
>!


!<

    B-3. Regional Household Shadow Demand System
    --------------------------------------------
>!

Variable (all,r,REG)
    ysaveev(r) # NET savings expenditure, for EV calc. #;

Coefficient (all,r,REG)
    PRIVEXPEV(r)
    # private consumption expenditure in region r, for EV calc. #;
Formula (initial) (all,r,REG)
    PRIVEXPEV(r) = PRIVEXP(r);
Update (all,r,REG)
    PRIVEXPEV(r) = ypev(r);
!<
    PRIVEXPEV should agree with VPAREGEV.
>!

Coefficient (all,r,REG)
    GOVEXPEV(r)
    # government consumption expenditure in region r, for EV calc. #;
Formula (initial) (all,r,REG)
    GOVEXPEV(r) = GOVEXP(r);
Update (all,r,REG)
    GOVEXPEV(r) = ygev(r);

Coefficient (all,r,REG)
    SAVEEV(r)
    # saving in region r, for EV calc. #;
Formula (initial) (all,r,REG)
    SAVEEV(r) = SAVE(r);
Update (all,r,REG)
    SAVEEV(r) = ysaveev(r);

Coefficient (all,r,REG)
    XSHRPRIVEV(r)
    # private expenditure share in regional income, for EV calc. #;
Formula (all,r,REG)
    XSHRPRIVEV(r) = PRIVEXPEV(r) / INCOMEEV(r);

Coefficient (all,r,REG)
    XSHRGOVEV(r)
    # government expenditure share in regional income, for EV calc. #;
Formula (all,r,REG)
    XSHRGOVEV(r) = GOVEXPEV(r) / INCOMEEV(r);

Coefficient (all,r,REG)
    XSHRSAVEEV(r) # saving share in regional income, for EV calc. #;
Formula (all,r,REG)
    XSHRSAVEEV(r) = SAVEEV(r) / INCOMEEV(r);

Variable (all,r,REG)
    dpavev(r) # average distribution parameter shift, for EV calc. #;
Equation DPARAVEV
# average distribution parameter shift, for EV calc. #
    (all,r,REG)
    dpavev(r)
        = XSHRPRIVEV(r) * dppriv(r)
        + XSHRGOVEV(r) * dpgov(r)
        + XSHRSAVEEV(r) * dpsave(r);

Equation UTILITELASTICEV
    # elasticity of cost of utility wrt utility, for EV calc. #
    (all,r,REG)
    uelasev(r) = XSHRPRIV(r) * ueprivev(r) - dpavev(r);

Equation PCONSEXPEV
# private consumption expenditure, for EV calc. #
    (all,r,REG)
    ypev(r) - yev(r) = -[ueprivev(r) - uelasev(r)] + dppriv(r);

Equation GOVCONSEXPEV
# government consumption expenditure #
(all,r,REG)
    ygev(r) - yev(r) = uelasev(r) + dpgov(r);

Equation SAVINGEV
# saving #
(all,r,REG)
    ysaveev(r) - yev(r) = uelasev(r) + dpsave(r);

Equation SAVEUEV
# saving #
(all,r,REG)
    qsaveev(r) = ysaveev(r);

!<
    Note that because psave doesn't change, qsaveev moves with ysaveev.
>!

Coefficient (all,r,REG)
    UTILELASEV(r)
    # elasticity of cost of utility wrt utility, for EV calc. #;
Formula (initial) (all,r,REG)
    UTILELASEV(r) = UTILELAS(r);
Update (all,r,REG)
    UTILELASEV(r) = uelasev(r);

Equation INCOME_EQUIV
# equivalent income #
(all,r,REG)
    u(r)
        = au(r)
        + DPARPRIV(r) * loge(UTILPRIVEV(r)) * dppriv(r)
        + DPARGOV(r) * loge(UTILGOVEV(r)) * dpgov(r)
        + DPARSAVE(r) * loge(UTILSAVEEV(r)) * dpsave(r)
        + [1.0 / UTILELASEV(r)] * [yev(r) - pop(r)];


!<
    B-4. Equivalent Variation
    -------------------------
>!

Variable (change)(all,r,REG)
    EV(r) # equivalent variation, $ US million #;
Equation EVREG
# regional EV (HT 67) #
(all,r,REG)
    EV(r) = [INCOMEEV(r) / 100] * yev(r);

Variable (change)
    WEV # equivalent variation for the world #;
Equation EVWLD
# EV for the world (HT 68) #
    WEV = sum(r,REG, EV(r));


!<
    ------------------------
    C. Welfare Decomposition
    ------------------------
    See GTAP Technical Paper No. 5 for derivation and interpretation.
>!

Coefficient (all,r,REG)
    EVSCALFACT(r) # equivalent variation scaling factor #;
Formula (all,r,REG)
    EVSCALFACT(r) = [UTILELASEV(r) / UTILELAS(r)] * [INCOMEEV(r) / INCOME(r)];

Coefficient (all,m,MARG_COMM)(all,s,REG)
    VTMD(m,s) # aggregate value of svce m in shipments to s #;
Formula (all,m,MARG_COMM)(all,s,REG)
    VTMD(m,s) = sum(i,TRAD_COMM, sum(r,REG, VTMFSD(m,i,r,s)));

Variable (linear,change)(all,r,REG)
    EV_ALT(r) # regional EV computed in alternative way #;
Equation EV_DECOMPOSITION
# decomposition of Equivalent Variation #
(all,r,REG)
    EV_ALT(r)
        = -[0.01 * UTILELASEV(r) * INCOMEEV(r)]
        * [DPARPRIV(r) * loge(UTILPRIVEV(r) / UTILPRIV(r)) * dppriv(r)
            + DPARGOV(r) * loge(UTILGOVEV(r) / UTILGOV(r)) * dpgov(r)
            + DPARSAVE(r) * loge(UTILSAVEEV(r) / UTILSAVE(r)) * dpsave(r)]
        + [0.01 * EVSCALFACT(r)]
        * [sum(i,NSAV_COMM, PTAX(i,r) * [qo(i,r) - pop(r)])
            + sum(i,ENDW_COMM, sum(j,PROD_COMM,
                ETAX(i,j,r) * [qfe(i,j,r) - pop(r)]))
            + sum(i,TRAD_COMM, sum(j,PROD_COMM,
                IFTAX(i,j,r) * [qfm(i,j,r) - pop(r)]))
            + sum(i,TRAD_COMM, sum(j,PROD_COMM,
                DFTAX(i,j,r) * [qfd(i,j,r) - pop(r)]))
            + sum(i,TRAD_COMM, IPTAX(i,r) * [qpm(i,r) - pop(r)])
            + sum(i,TRAD_COMM, DPTAX(i,r) * [qpd(i,r) - pop(r)])
            + sum(i,TRAD_COMM, IGTAX(i,r) * [qgm(i,r) - pop(r)])
            + sum(i,TRAD_COMM, DGTAX(i,r) * [qgd(i,r) - pop(r)])
            + sum(i,TRAD_COMM, sum(s,REG, XTAXD(i,r,s) * [qxs(i,r,s) - pop(r)]))
            + sum(i,TRAD_COMM, sum(s,REG, MTAX(i,s,r) * [qxs(i,s,r) - pop(r)]))
            + sum(i,ENDW_COMM, VOA(i,r) * [qo(i,r) - pop(r)])
            - VDEP(r) * [kb(r) - pop(r)]
            + sum(i,PROD_COMM, VOA(i,r) * ao(i,r))
            + sum(j,PROD_COMM, VVA(j,r) * ava(j,r))
            + sum(j,PROD_COMM, sum(i,ENDW_COMM, VFA(i,j,r) * afe(i,j,r)))
            + sum(j,PROD_COMM, sum(i,TRAD_COMM, VFA(i,j,r) * af(i,j,r)))
            + sum(m,MARG_COMM, sum(i,TRAD_COMM, sum(s,REG,
                VTMFSD(m,i,s,r) * atmfsd(m,i,s,r))))
            + sum(i,TRAD_COMM, sum(s,REG, VIMS(i,s,r) * ams(i,s,r)))
            + sum(i,TRAD_COMM, sum(s,REG, VXWD(i,r,s) * pfob(i,r,s)))
            + sum(m,MARG_COMM, VST(m,r) * pm(m,r))
            + NETINV(r) * pcgds(r)
            - sum(i,TRAD_COMM, sum(s,REG, VXWD(i,s,r) * pfob(i,s,r)))
            - sum(m,MARG_COMM, VTMD(m,r) * pt(m))
            - SAVE(r) * psave(r)
! EC (Apr 2022): Add global PES !
            + 100 * [pes_inflow(r) - pes_outflow(r)]
            ]
! End: Add global PES !
        + 0.01 * INCOMEEV(r) * pop(r);

Variable (linear,change)
    WEV_ALT # expression for WEV computed in alternative way #;
Equation WORLDEV
# Equivalent Variation for the world #
    WEV_ALT = sum(r,REG, EV_ALT(r));

Variable (linear,change) (all,r,REG)
    CNTdpar(r) # contribution to EV of change in distribution parameters #;
Equation CNT_WEV_dpar
(all,r,REG)
    CNTdpar(r)
        = -0.01 * UTILELASEV(r) * INCOMEEV(r)
        * [DPARPRIV(r) * loge(UTILPRIVEV(r) / UTILPRIV(r)) * dppriv(r)
            + DPARGOV(r) * loge(UTILGOVEV(r) / UTILGOV(r)) * dpgov(r)
            + DPARSAVE(r) * loge(UTILSAVEEV(r) / UTILSAVE(r)) * dpsave(r)];

Variable (linear,change) (all,r,REG)
    CNTpopr(r) # contribution to EV in region r of change in population #;
Equation CONT_EV_pop
(all,r,REG)
    CNTpopr(r) = 0.01 * INCOMEEV(r) * pop(r);

Variable (linear,change) (all,r,REG)
    CNTqor(r) # contribution to EV in region r of output changes #;
Equation CONT_EV_qor
(all,r,REG)
    CNTqor(r)
        = sum(i,NSAV_COMM,
            0.01 * EVSCALFACT(r) * PTAX(i,r) * [qo(i,r) - pop(r)]);

Variable (linear,change) (all,i,NSAV_COMM)(all,r,REG)
    CNTqoir(i,r)
    # contribution to EV of changes in output of NSAV_COMM i in reg. r #;
Equation CONT_EV_qoir
(all,i,NSAV_COMM)(all,r,REG)
    CNTqoir(i,r) = PTAX(i,r) * [0.01 * EVSCALFACT(r)] * [qo(i,r) - pop(r)];

Variable (linear,change) (all,r,REG)
    CNTqfer(r)
    # cont. to EV of changes in use of all ENDW_COMM in all ind. in reg. r #;
Equation CONT_EV_qfer
(all,r,REG)
    CNTqfer(r)
        = sum(i,ENDW_COMM, sum(j,PROD_COMM,
            ETAX(i,j,r) * [0.01 * EVSCALFACT(r)] * [qfe(i,j,r) - pop(r)]));

Variable (linear,change) (all,i,ENDW_COMM)(all,r,REG)
    CNTqfeir(i,r)
    # contribution to EV of changes in use of ENDW_COMM i in all ind. in r #;
Equation CONT_EV_qfeir
(all,i,ENDW_COMM)(all,r,REG)
    CNTqfeir(i,r)
        = sum(j,PROD_COMM,
            ETAX(i,j,r) * [0.01 * EVSCALFACT(r)] * [qfe(i,j,r) - pop(r)]);

Variable (linear,change) (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    CNTqfeijr(i,j,r)
    # cont. to EV of changes in use of ENDW_COMM i in ind. j of reg. r #;
Equation CONT_EV_qfeijr
(all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    CNTqfeijr(i,j,r)
        = ETAX(i,j,r) * [0.01 * EVSCALFACT(r)] * [qfe(i,j,r) - pop(r)];

Variable (linear,change) (all,r,REG)
    CNTqfmr(r)
    # cont. to EV of changes in use of imported int. in all ind. in reg. r #;
Equation CONT_EV_qfmr
(all,r,REG)
    CNTqfmr(r)
        = sum(i,TRAD_COMM, sum(j,PROD_COMM,
            IFTAX(i,j,r) * [0.01 * EVSCALFACT(r)] * [qfm(i,j,r) - pop(r)]));

Variable (linear,change) (all,i,TRAD_COMM)(all,r,REG)
    CNTqfmir(i,r)
    # cont. to EV of changes in use of imported int. i in all ind. in r #;
Equation CONT_EV_qfmir
(all,i,TRAD_COMM)(all,r,REG)
    CNTqfmir(i,r)
        = sum(j,PROD_COMM,
            IFTAX(i,j,r) * [0.01 * EVSCALFACT(r)] * [qfm(i,j,r) - pop(r)]);

Variable (linear,change) (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    CNTqfmijr(i,j,r)
    # cont. to EV of changes in use of imported int. i in ind. j of reg. r #;
Equation CONT_EV_qfmijr
(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    CNTqfmijr(i,j,r)
        = IFTAX(i,j,r) * [0.01 * EVSCALFACT(r)] * [qfm(i,j,r) - pop(r)];

Variable (linear,change) (all,r,REG)
    CNTqfdr(r)
    # cont. to EV of changes in use of domestic int. in all ind. in reg. r #;
Equation CONT_EV_qfdr
(all,r,REG)
    CNTqfdr(r)
        = sum(i,TRAD_COMM, sum(j,PROD_COMM,
            DFTAX(i,j,r) * [0.01 * EVSCALFACT(r)] * [qfd(i,j,r) - pop(r)]));

Variable (linear,change) (all,i,TRAD_COMM)(all,r,REG)
    CNTqfdir(i,r)
    # contribution to EV of changes in use of domestic i in all ind. in r #;
Equation CONT_EV_qfdir
(all,i,TRAD_COMM)(all,r,REG)
    CNTqfdir(i,r)
        = sum(j,PROD_COMM,
            DFTAX(i,j,r) * [0.01 * EVSCALFACT(r)] * [qfd(i,j,r) - pop(r)]);

Variable (linear,change) (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    CNTqfdijr(i,j,r)
    # cont. to EV of changes in use of domestic int. i in ind. j of reg. r #;
Equation CONT_EV_qfdijr
(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    CNTqfdijr(i,j,r)
        = DFTAX(i,j,r) * [0.01 * EVSCALFACT(r)] * [qfd(i,j,r) - pop(r)];

Variable (linear,change) (all,r,REG)
    CNTqpmr(r)
    # contribution to EV of changes in consumption of imported goods in r #;
Equation CONT_EV_qpmr
(all,r,REG)
    CNTqpmr(r)
        = sum(i,TRAD_COMM,
            IPTAX(i,r) * [0.01 * EVSCALFACT(r)] * [qpm(i,r) - pop(r)]);

Variable (linear,change) (all,i,TRAD_COMM)(all,r,REG)
    CNTqpmir(i,r)
    # cont. to EV of changes in consumption of imported good i in reg. r #;
Equation CONT_EV_qpmir
(all,i,TRAD_COMM)(all,r,REG)
    CNTqpmir(i,r) = IPTAX(i,r) * [0.01 * EVSCALFACT(r)] * [qpm(i,r) - pop(r)];

Variable (linear,change) (all,r,REG)
    CNTqpdr(r)
    # contribution to EV of changes in consumption of domestic goods in r #;
Equation CONT_EV_qpdr
(all,r,REG)
    CNTqpdr(r)
        = sum(i,TRAD_COMM,
            DPTAX(i,r) * [0.01 * EVSCALFACT(r)] * [qpd(i,r) - pop(r)]);

Variable (linear,change) (all,i,TRAD_COMM)(all,r,REG)
    CNTqpdir(i,r)
    # cont. to EV of changes in consumption of domestic good i in reg. r #;
Equation CONT_EV_qpdir
(all,i,TRAD_COMM)(all,r,REG)
    CNTqpdir(i,r) = DPTAX(i,r) * [0.01 * EVSCALFACT(r)] * [qpd(i,r) - pop(r)];

Variable (linear,change) (all,r,REG)
    CNTqgmr(r)
    # cont. to EV of changes in gov't consumption of imports in reg. r #;
Equation CONT_EV_qgmr
(all,r,REG)
    CNTqgmr(r)
        = sum(i,TRAD_COMM,
            IGTAX(i,r) * [0.01 * EVSCALFACT(r)] * [qgm(i,r) - pop(r)]);

Variable (linear,change) (all,i,TRAD_COMM)(all,r,REG)
    CNTqgmir(i,r)
    # cont. to EV of changes in gov't consumption of import i in reg. r #;
Equation CONT_EV_qgmir
(all,i,TRAD_COMM)(all,r,REG)
    CNTqgmir(i,r) = IGTAX(i,r) * [0.01 * EVSCALFACT(r)] * [qgm(i,r) - pop(r)];

Variable (linear,change) (all,r,REG)
    CNTqgdr(r)
    # cont. to EV of changes in gov't consumption of domestics in reg. r #;
Equation CONT_EV_qgdr
(all,r,REG)
    CNTqgdr(r)
        = sum(i,TRAD_COMM,
            DGTAX(i,r) * [0.01 * EVSCALFACT(r)] * [qgd(i,r) - pop(r)]);

Variable (linear,change) (all,i,TRAD_COMM)(all,r,REG)
    CNTqgdir(i,r)
    # cont. to EV of changes in gov't consumption of domestic i in reg. r #;
Equation CONT_EV_qgdir
(all,i,TRAD_COMM)(all,r,REG)
    CNTqgdir(i,r) = DGTAX(i,r) * [0.01 * EVSCALFACT(r)] * [qgd(i,r) - pop(r)];

Variable (linear,change) (all,r,REG)
    CNTqxsr(r)
    # cont. to EV of changes in exports of all goods from SRCE r to all DEST #;
Equation CONT_EV_qxsr
(all,r,REG)
    CNTqxsr(r)
        = sum(i,TRAD_COMM, sum(s,REG,
            XTAXD(i,r,s) * [0.01 * EVSCALFACT(r)] * [qxs(i,r,s) - pop(r)]));

Variable (linear,change) (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    CNTqxsirs(i,r,s)
    # cont. to EV of changes in exports of i from SRCE r to DEST s #;
Equation CONT_EV_qxsirs
(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    CNTqxsirs(i,r,s)
        = XTAXD(i,r,s) * [0.01 * EVSCALFACT(r)] * [qxs(i,r,s) - pop(r)];

Variable (linear,change) (all,r,REG)
    CNTqimr(r)
    # cont. to EV of changes in imports of all goods from all SRCE to DEST r #;
Equation CONT_EV_qimr
(all,r,REG)
    CNTqimr(r)
        = sum(i,TRAD_COMM, sum(s,REG,
            MTAX(i,s,r) * [0.01 * EVSCALFACT(r)] * [qxs(i,s,r) - pop(r)]));

Variable (linear,change) (all,i,TRAD_COMM)(all,s,REG)(all,r,REG)
    CNTqimisr(i,s,r)
    # cont. to EV of changes in imports of i from SRCE s to DEST r #;
Equation CONT_EV_qimisr
(all,i,TRAD_COMM)(all,s,REG)(all,r,REG)
    CNTqimisr(i,s,r)
        = MTAX(i,s,r) * [0.01 * EVSCALFACT(r)] * [qxs(i,s,r) - pop(r)];

Variable (linear,change) (all,r,REG)
    CNTalleffr(r) # total contribution to regional EV of allocative effects #;
Equation CONT_EV_alleffr
(all,r,REG)
    CNTalleffr(r)
        = [0.01 * EVSCALFACT(r)]
        * [sum(i,NSAV_COMM, PTAX(i,r) * [qo(i,r) - pop(r)])
            + sum(i,ENDW_COMM, sum(j,PROD_COMM,
                ETAX(i,j,r) * [qfe(i,j,r) - pop(r)]))
            + sum(i,TRAD_COMM, sum(j,PROD_COMM,
                IFTAX(i,j,r) * [qfm(i,j,r) - pop(r)]))
            + sum(i,TRAD_COMM, sum(j,PROD_COMM,
                DFTAX(i,j,r) * [qfd(i,j,r) - pop(r)]))
            + sum(i,TRAD_COMM, IPTAX(i,r) * [qpm(i,r) - pop(r)])
            + sum(i,TRAD_COMM, DPTAX(i,r) * [qpd(i,r) - pop(r)])
            + sum(i,TRAD_COMM, IGTAX(i,r) * [qgm(i,r) - pop(r)])
            + sum(i,TRAD_COMM, DGTAX(i,r) * [qgd(i,r) - pop(r)])
            + sum(i,TRAD_COMM, sum(s,REG, XTAXD(i,r,s) * [qxs(i,r,s) - pop(r)]))
            + sum(i,TRAD_COMM, sum(s,REG,
                MTAX(i,s,r) * [qxs(i,s,r) - pop(r)]))];

Variable (linear,change) (all,i,DEMD_COMM)(all,r,REG)
    CNTalleffir(i,r)
    # total contribution to regional EV of allocative effects #;
Equation CONT_EV_alleffir_E
(all,i,ENDW_COMM)(all,r,REG)
    CNTalleffir(i,r)
        = [0.01 * EVSCALFACT(r)]
        * [PTAX(i,r) * [qo(i,r) - pop(r)]
            + sum(j,PROD_COMM, ETAX(i,j,r) * [qfe(i,j,r) - pop(r)])];
Equation CONT_EV_alleffir_T
(all,i,TRAD_COMM)(all,r,REG)
    CNTalleffir(i,r)
        = [0.01 * EVSCALFACT(r)]
        * [PTAX(i,r) * [qo(i,r) - pop(r)]
            + sum(j,PROD_COMM, IFTAX(i,j,r) * [qfm(i,j,r) - pop(r)])
            + sum(j,PROD_COMM, DFTAX(i,j,r) * [qfd(i,j,r) - pop(r)])
            + IPTAX(i,r) * [qpm(i,r) - pop(r)]
            + DPTAX(i,r) * [qpd(i,r) - pop(r)]
            + IGTAX(i,r) * [qgm(i,r) - pop(r)]
            + DGTAX(i,r) * [qgd(i,r) - pop(r)]
            + sum(s,REG, XTAXD(i,r,s) * [qxs(i,r,s) - pop(r)])
            + sum(s,REG, MTAX(i,s,r) * [qxs(i,s,r) - pop(r)])];

Variable (linear,change) (all,r,REG)
    CNTtotr(r)
    # contribution to regional EV of changes in its terms of trade #;
Equation CONT_EV_totr
(all,r,REG)
    CNTtotr(r)
        = [0.01 * EVSCALFACT(r)]
        * [sum(i,TRAD_COMM, sum(s,REG, VXWD(i,r,s) * [pfob(i,r,s) - pxwwld]))
            + sum(m,MARG_COMM, VST(m,r) * [pm(m,r) - pxwwld])
            - sum(i,TRAD_COMM, sum(s,REG, VXWD(i,s,r) * [pfob(i,s,r) - pxwwld]))
            - sum(m,MARG_COMM, VTMD(m,r) * [pt(m) - pxwwld])];

Variable (linear,change) (all,r,REG)
    CNTcgdsr(r) # contribution to regional EV of changes in the price of cgds #;
Equation CNT_EV_cgdsr
(all,r,REG)
    CNTcgdsr(r)
        = [0.01 * EVSCALFACT(r)]
        * [NETINV(r) * [pcgds(r) - pxwwld] - SAVE(r) * [psave(r) - pxwwld]];

Variable (linear,change) (all,r,REG)
    CNTendwr(r) # contribution to regional EV of changes in all ENDW_COMM #;
Equation CONT_EV_endwr
(all,r,REG)
    CNTendwr(r)
        = [0.01 * EVSCALFACT(r)]
        * [sum(i,ENDW_COMM, VOA(i,r) * [qo(i,r) - pop(r)])
            - VDEP(r) * [kb(r) - pop(r)]];

Variable (linear,change) (all,i,ENDW_COMM)(all,r,REG)
    CNTendwir(i,r) # contribution to regional EV of changes in ENDW_COMM i #;
Equation CONT_EV_endwir
(all,i,ENDW_COMM)(all,r,REG)
    CNTendwir(i,r) = [0.01 * EVSCALFACT(r)] * [VOA(i,r) * [qo(i,r) - pop(r)]];

Variable (linear,change) (all,r,REG)
    CNTtechr(r) # contribution to regional EV of all technical change #;
Equation CONT_EV_techr
(all,r,REG)
    CNTtechr(r)
        = [0.01 * EVSCALFACT(r)]
        * [sum(i,PROD_COMM, VOA(i,r) * ao(i,r))
            + sum(j,PROD_COMM, sum(i,ENDW_COMM, VFA(i,j,r) * afe(i,j,r)))
            + sum(j,PROD_COMM, VVA(j,r) * ava(j,r))
            + sum(j,PROD_COMM, sum(i,TRAD_COMM,
                [VIFA(i,j,r) + VDFA(i,j,r)] * af(i,j,r)))
            + sum(m,MARG_COMM, sum(i,TRAD_COMM, sum(s,REG,
                VTMFSD(m,i,s,r) * atmfsd(m,i,s,r))))
            + sum(i,TRAD_COMM, sum(s,REG, VIMS(i,s,r) * ams(i,s,r)))];

Variable (linear,change) (all,r,REG)
    CNTtech_aor(r)
    # contribution to regional EV of output augmenting technical change #;
Equation CONT_EV_tech_aor
(all,r,REG)
    CNTtech_aor(r)
        = [0.01 * EVSCALFACT(r)] * sum(i,PROD_COMM, VOA(i,r) * ao(i,r));

Variable (linear,change) (all,i,PROD_COMM)(all,r,REG)
    CNTtech_aoir(i,r)
    # cont. to regional EV of output augmenting tech change in TRAD_COMM i #;
Equation CONT_EV_tech_aoir
(all,i,PROD_COMM)(all,r,REG)
    CNTtech_aoir(i,r) = [0.01 * EVSCALFACT(r)] * VOA(i,r) * ao(i,r);

Variable (linear,change) (all,r,REG)
    CNTtech_afer(r)
    # contribution to regional EV of primary factor augmenting tech change #;
Equation CONT_EV_tech_afer
(all,r,REG)
    CNTtech_afer(r)
        = [0.01 * EVSCALFACT(r)]
        * sum(j,PROD_COMM, sum(i,ENDW_COMM, VFA(i,j,r) * afe(i,j,r)));

Variable (linear,change) (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    CNTtech_afeijr(i,j,r)
    # cont. to EV of primary factor i augmenting tech change in sector j #;
Equation CONT_EV_tech_afeijr
(all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    CNTtech_afeijr(i,j,r) = [0.01 * EVSCALFACT(r)] * VFA(i,j,r) * afe(i,j,r);

Variable (linear,change) (all,r,REG)
    CNTtech_avar(r)
    # contribution to regional EV of value added augmenting tech change #;
Equation CONT_EV_tech_avar
(all,r,REG)
    CNTtech_avar(r)
        = [0.01 * EVSCALFACT(r)] * sum(j,PROD_COMM, VVA(j,r) * ava(j,r));

Variable (linear,change) (all,j,PROD_COMM)(all,r,REG)
    CNTtech_avajr(j,r)
    # cont. to EV of value added augmenting tech change in sector j #;
Equation CONT_EV_tech_avajr
(all,j,PROD_COMM)(all,r,REG)
    CNTtech_avajr(j,r) = [0.01 * EVSCALFACT(r)] * VVA(j,r) * ava(j,r);

Variable (linear,change) (all,r,REG)
    CNTtech_afr(r)
    # cont. to regional EV of comp. int. input augmenting tech change #;
Equation CONT_EV_tech_afr
(all,r,REG)
    CNTtech_afr(r)
        = [0.01 * EVSCALFACT(r)]
        * sum(j,PROD_COMM, sum(i,TRAD_COMM,
            [VIFA(i,j,r) + VDFA(i,j,r)] * af(i,j,r)));

Variable (linear,change) (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    CNTtech_afijr(i,j,r)
    # cont. to EV of composite i input augmenting tech change in sector j #;
Equation CONT_EV_tech_afijr
(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    CNTtech_afijr(i,j,r)
        = [0.01 * EVSCALFACT(r)] * [VIFA(i,j,r) + VDFA(i,j,r)] * af(i,j,r);

Variable (linear,change) (all,r,REG)
    CNTtech_atrr(r)
    # contribution to regional EV of technical change in transportation #;
Equation CONT_EV_tech_atrr
(all,r,REG)
    CNTtech_atrr(r)
        = [0.01 * EVSCALFACT(r)]
        * sum(m,MARG_COMM, sum(i,TRAD_COMM, sum(s,REG,
            VTMFSD(m,i,s,r) * atmfsd(m,i,s,r))));

Variable (linear,change)(all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    CNTtech_afmfdsd(m,i,r,s)
    # cont. to EV of tech change in transportation efficiency #;
Equation CONT_EV_tech_afmfdsd
(all,m,MARG_COMM)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    CNTtech_afmfdsd(m,i,r,s)
        = [0.01 * EVSCALFACT(s)] * VTMFSD(m,i,r,s) * atmfsd(m,i,r,s);

Variable (linear,change)(all,r,REG)
    CNTtech_amsr(r) # cont. to EV of bilateral import-augmenting tech change #;
Equation CONT_EV_tech_amsr
(all,r,REG)
    CNTtech_amsr(r)
        = [0.01 * EVSCALFACT(r)]
        * sum(i,TRAD_COMM, sum(s,REG, VIMS(i,s,r) * ams(i,s,r)));

Variable (linear,change) (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    CNTtech_amsirs(i,r,s)
 # cont. to EV of bilateral import augmenting tech change for TRAD_COMM i #;
Equation CONT_EV_tech_amsirs
(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    CNTtech_amsirs(i,r,s) = [0.01 * EVSCALFACT(r)] * VIMS(i,r,s) * ams(i,r,s);

Variable (linear,change) (all,r,REG)
    CNTkbr(r)
    # cont. to EV of changes to beg. period capital stock and depreciation #;
Equation CONT_EV_kbr
(all,r,REG)
    CNTkbr(r) = - [0.01 * EVSCALFACT(r)] * VDEP(r) * [kb(r) - pop(r)];


!<
    -------------------------------
    D. Terms of Trade Decomposition
    -------------------------------

    Computations for decomposition the terms-of-trade effect
    Reference: Robert McDougall, SALTER No. 12 Working Paper
>!

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    SX_IR(i,r) # share of good i in total exports from r #;
Formula (all,m,MARG_COMM)(all,r,REG)
    SX_IR(m,r)
        = [sum(s,REG, VXWD(m,r,s)) + VST(m,r)]
        / [sum(k,TRAD_COMM, sum(s,REG, VXWD(k,r,s)))
            + sum(l,MARG_COMM, VST(l,r))];
Formula (all,i,NMRG_COMM)(all,r,REG)
    SX_IR(i,r)
        = sum(s,REG, VXWD(i,r,s))
        / [sum(k,TRAD_COMM, sum(s,REG, VXWD(k,r,s)))
            + sum(l,MARG_COMM, VST(l,r))];

Coefficient (all,m,MARG_COMM)(all,s,REG)
    VTICOMM(m,s) # margin usage of m in getting imports to region s #;
Formula (all,m,MARG_COMM)(all,s,REG)
    VTICOMM(m,s) = sum(i,TRAD_COMM, sum(r,REG, VTMFSD(m,i,r,s)));

Coefficient (all,i,TRAD_COMM)(all,s,REG)
    VIWDIRALL(i,s) # imports of i into s, with direct allocation of margins #;
Formula (all,m,MARG_COMM)(all,s,REG)
    VIWDIRALL(m,s) = sum(r,REG, VXWD(m,r,s)) + VTICOMM(m,s);
Formula (all,i,NMRG_COMM)(all,s,REG)
    VIWDIRALL(i,s) = sum(r,REG, VXWD(i,r,s));

Coefficient (all,s,REG)
    VIWDATOT(s) # total imports into s, calculated on direct allocation basis #;
Formula (all,s,REG)
    VIWDATOT(s) = sum(i,TRAD_COMM, VIWDIRALL(i,s));

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    SM_IR(i,r)  # share of good i in total imports into r #;
Formula (all,i,TRAD_COMM)(all,s,REG)
    SM_IR(i,s) = VIWDIRALL(i,s) / VIWDATOT(s);

Zerodivide (zero_by_zero) default 0;
Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    SX_IRS(i,r,s) # share of exports of good i from region r to s #;
Formula (all,m,MARG_COMM)(all,r,REG)(all,s,REG)
    SX_IRS(m,r,s) = VXWD(m,r,s) / [sum(k,REG, VXWD(m,r,k)) + VST(m,r)];
Formula (all,i,NMRG_COMM)(all,r,REG)(all,s,REG)
    SX_IRS(i,r,s) = VXWD(i,r,s) / sum(k,REG, VXWD(i,r,k));
Zerodivide (zero_by_zero) off;

Coefficient (all,m,MARG_COMM)(all,r,REG)
    SXT_IR(m,r) # share of margins in exports of good i from region r #;
Formula (all,m,MARG_COMM)(all,r,REG)
    SXT_IR(m,r) = VST(m,r) / [sum(k,REG, VXWD(m,r,k)) + VST(m,r)];

Variable (all,i,TRAD_COMM)(all,r,REG)
    px_ir(i,r) # export price index for good i and region r #;
Equation EXPPRICE_MARG
# price index for total exports of m from r #
(all,m,MARG_COMM)(all,r,REG)
    px_ir(m,r)
        = sum(s,REG, SX_IRS(m,r,s) * pfob(m,r,s)) + SXT_IR(m,r) * pm(m,r);
Equation EXPPRICE_NMRG
# price index for total exports of i from r #
(all,i,NMRG_COMM)(all,r,REG)
    px_ir(i,r) = sum(s,REG, SX_IRS(i,r,s) * pfob(i,r,s));

Coefficient (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    SM_IRS(i,r,s) # share of imports of good i into s from r at FOB prices #;
Formula (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    SM_IRS(i,r,s) = VXWD(i,r,s) / VIWDIRALL(i,s);

Coefficient (all,m,MARG_COMM)(all,r,REG)
    SMT_IR(m,r) # share of transport cost in imports of margin commodity #;
Formula (all,m,MARG_COMM)(all,s,REG)
    SMT_IR(m,s) = VTICOMM(m,s) / VIWDIRALL(m,s);

Variable (all,i,TRAD_COMM)(all,r,REG)
    pm_ir(i,r) # imports price index for good i and region r #;
Equation IMPPRICE1_MARG
# price index for total imports of m in s -- margins commodities #
(all,m,MARG_COMM)(all,s,REG)
    pm_ir(m,s)
        = sum(r,REG, SM_IRS(m,r,s) * pfob(m,r,s)) + SMT_IR(m,s) * pt(m);
Equation IMPPRICE1_NMRG
# price index for total imports of i in s -- non-margins commodities #
(all,i,NMRG_COMM)(all,s,REG)
    pm_ir(i,s) = sum(r,REG, SM_IRS(i,r,s) * pfob(i,r,s));

Coefficient (all,i,TRAD_COMM)(all,r,REG)
    SW_IR(i,r) # share of region r exports in world total for good i #;
Formula (all,m,MARG_COMM)(all,r,REG)
    SW_IR(m,r)
        = [sum(s,REG, VXWD(m,r,s)) + VST(m,r)]
        / sum(k,REG, sum(s,REG, VXWD(m,k,s)) + VST(m,k));
Formula (all,i,NMRG_COMM)(all,r,REG)
    SW_IR(i,r)
        = sum(s,REG, VXWD(i,r,s)) / sum(k,REG, sum(s,REG, VXWD(i,k,s)));

Variable (all,i,TRAD_COMM)
    px_i(i) # world export price index for commodity i #;
Equation WRLDPRICEi
# world export price index for good i #
(all,i,TRAD_COMM)
    px_i(i) = sum(r,REG, SW_IR(i,r) * px_ir(i,r));

Coefficient (all,i,TRAD_COMM)
    SW_I(i) # share of exports of i in world total #;
Formula (all,m,MARG_COMM)
    SW_I(m)
        = sum(r,REG, sum(s,REG, VXWD(m,r,s)) + VST(m,r))
        / [sum(k,TRAD_COMM, sum(r,REG, sum(s,REG, VXWD(k,r,s))))
            + sum(l,MARG_COMM, sum(r,REG, VST(l,r)))];
Formula (all,i,NMRG_COMM)
    SW_I(i)
        = sum(r,REG, sum(s,REG, VXWD(i,r,s)))
        / [sum(k,TRAD_COMM, sum(r,REG, sum(s,REG, VXWD(k,r,s))))
            + sum(l,MARG_COMM, sum(r,REG, VST(l,r)))];

Variable
    px_ # world export price index for all commodities #;
Equation WRLDPRICE
# world export price index for all goods #
    px_ = sum(i,TRAD_COMM, SW_I(i) * px_i(i));

Variable (all,i,TRAD_COMM)(all,r,REG)
    c1_ir(i,r) # contribution of world price, px_i, to ToT #;
Equation c1_irEQ
# contribution of world export price index of good i to ToT for region r #
(all,i,TRAD_COMM)(all,r,REG)
    c1_ir(i,r) = [SX_IR(i,r) - SM_IR(i,r)] * [px_i(i) - px_];

Variable (all,i,TRAD_COMM)(all,r,REG)
    c2_ir(i,r) # contribution of regional export price, px_ir, to ToT #;
Equation c2_irEQ
# contribution of regional export price of good i for region r #
(all,i,TRAD_COMM)(all,r,REG)
    c2_ir(i,r) = SX_IR(i,r) * [px_ir(i,r) - px_i(i)];

Variable (all,i,TRAD_COMM)(all,r,REG)
    c3_ir(i,r) # contribution of regional import price, pm_ir, to ToT #;
Equation c3_irEQ
# contribution of imports price index of good i for region r #
(all,i,TRAD_COMM)(all,r,REG)
    c3_ir(i,r) = SM_IR(i,r) * [pm_ir(i,r) - px_i(i)];

Variable (all,r,REG)
    c1_r(r) # contribution of world prices for all goods to ToT #;
Equation c1_rEQ
# contribution of world price indexes of all goods to ToT for r #
(all,r,REG)
    c1_r(r) = sum(i,TRAD_COMM, c1_ir(i,r));

Variable (all,r,REG)
    c2_r(r) # contribution of regional export prices to ToT #;
Equation c2_rEQ
# contribution of regional exports prices to ToT for r #
(all,r,REG)
    c2_r(r) = sum(i,TRAD_COMM, c2_ir(i,r));

Variable (all,r,REG)
    c3_r(r) # contribution of regional import prices to ToT #;
Equation c3_rEQ
# contribution of regional import prices to ToT for r #
(all,r,REG)
    c3_r(r) = sum(i,TRAD_COMM, c3_ir(i,r));

Variable (all,r,REG)
    tot2(r) # trade terms for region r, computed from components #;
Equation TOT2eq
# trade terms for region r, computed from components #
(all,r,REG)
    tot2(r) = c1_r(r) + c2_r(r) - c3_r(r);

!<
    ------------------
    E. Land Use Module
    ------------------
    Computes changes in land cover and harvested area in hectares. 
    Land cover refers to forestry, pasture and cropland.
    Land use in crops is based on harvested area.

In order to reconcile market clearing in hectares with that on a 
value basis, as dictated by the CET function, we require an adjustment factor 
in each of the AEZs. This adjusts the percentage change in hectares to 
reflect the average change in productivity of those hectares in their new 
uses.   

>!

Set LAND_COVER # set of land cover categories # 
  read elements from file GTAPSETS header "COVS";
Set
    UNMNGLAND #unmanaged land # (UNMNGLAND);
Subset
    UNMNGLAND is subset of LAND_COVER ;
Subset UNMNGLAND is subset of LAND_COVER ;
Set PROD_COVER # set of land cover categories to be used in the model # 
        = LAND_COVER - UNMNGLAND;

Set PROD2_COVER # first two land cover categories # = 
    FOREST_COMM UNION GRAZE_COMM ; 

Subset PROD2_COVER is subset of PROD_COVER ;
Subset PROD2_COVER is subset of LAND_COMM ;

Set ACROP_COMM # aggregated cropsland cover # = PROD_COVER - PROD2_COVER ;

Variable (all,i,AEZ_COMM)(all,r,REG)
    lcoveraez(i,r)
    # change in land in aez -- generally a fixed endowment #;

! Make LANDCOVER a levels variable -- and exogenize landcover change in 
unmanaged land. !

Variable (LEVELS) (all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG)
    LANDCOVER_L(i,j,r)
    # hectares of land by AEZ, sector and region #;
Read
    LANDCOVER_L from file GTAPDATA header "LCOV";

Coefficient (all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG)
    LANDCOVER(i,j,r)
    # hectares of land by AEZ, land cover type and region #;
Formula (all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG)
    LANDCOVER(i,j,r) = LANDCOVER_L(i,j,r);

Variable (LEVELS) (all,j,PROD_COVER)(all,r,REG)
    LANDCOVER_R(j,r) # hectares of land by sector and region #;
Formula & Equation E_LANDCOVER_R (all,j,PROD_COVER)(all,r,REG)
LANDCOVER_R(j,r) = sum(i,AEZ_COMM, LANDCOVER_L(i,j,r)) + 0.0000001;
 
Variable (LEVELS)(all,j,PROD_COVER)
    LANDCOVER_W(j) # hectares of land by sector #;
Formula & Equation E_LANDCOVER_W (all,j,PROD_COVER)
LANDCOVER_W(j) = sum(r,REG, LANDCOVER_R(j,r));

Zerodivide (zero_by_zero) default 1;
Zerodivide (nonzero_by_zero) default 1;

Coefficient (all,i,AEZ_COMM)(all,j,LAND_COMM)(all,r,REG)
    NVFM(i,j,r)
    # land rental payments in $ #;
Formula  
(all,i,AEZ_COMM)(all,j,LAND_COMM)(all,r,REG)
    NVFM(i,j,r) = VFM(i,j,r) * 10^6 ;

Variable (LEVELS) (all,i,AEZ_COMM)(all,j,PROD_COVER)(all,r,REG)
    RENTCOVER(i,j,r)
    # level of land rents by AEZ, sector and region, in $/ha. #;
Formula (initial) 
(all,i,AEZ_COMM)(all,j,PROD2_COVER)(all,r,REG)
    RENTCOVER(i,j,r) = [NVFM(i,j,r)/LANDCOVER_L(i,j,r)] ;
Formula (initial) 
(all,i,AEZ_COMM)(all,j,ACROP_COMM)(all,r,REG)
    RENTCOVER(i,j,r) = [sum(k,CROP_COMM, NVFM(i,k,r))/LANDCOVER_L(i,j,r)] ;

! The change in land rents per physical hectare is computed by noting that
total land rental payments, by cover type and AEZ, must be the same, regardless
of units of measurement for land. !

Equation E_RENTCOVER1
(all,i,AEZ_COMM)(all,j,PROD2_COVER)(all,r,REG)
    p_RENTCOVER(i,j,r) + p_LANDCOVER_L(i,j,r) = pmes(i,j,r) + qoes(i,j,r) ;
Equation E_RENTCOVER2
(all,i,AEZ_COMM)(all,j,ACROP_COMM)(all,r,REG)
    p_RENTCOVER(i,j,r) + p_LANDCOVER_L(i,j,r) = 
                        pmcropland(i,r) + qocropland (i,r);

Variable (LEVELS) (all,i,AEZ_COMM)(all,r,REG)
    RENTCOVERAV(i,r)
    # level of average land rents by AEZ and region #;
Formula (initial) 
(all,i,AEZ_COMM)(all,r,REG)
    RENTCOVERAV(i,r) = 
  [sum(j,LAND_COMM, NVFM(i,j,r))/sum(j,PROD_COVER, LANDCOVER_L(i,j,r))] ;
Equation E_RENTCOVERAV
(all,i,AEZ_COMM)(all,r,REG)
    p_RENTCOVERAV(i,r) + lcoveraez(i,r) = pm(i,r) + qo(i,r) ;

Variable (LEVELS) (all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    HARVSTAREA_L(i,j,r)
    # cropland harvested area #;
Read
    HARVSTAREA_L from file GTAPDATA header "AREA";

Coefficient (all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    HARVSTAREA(i,j,r)
    # hectares of land by AEZ, land cover type and region #;
Formula (all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    HARVSTAREA(i,j,r) = HARVSTAREA_L(i,j,r);

! New coefficient for deflating the growth in land cover certain regions. The 
  problem is that there is a great deal of crop land cover that is not 
  cropped for one reason or another (e.g., CRP land in USA). Therefore, we 
  do not expect crop land cover to expand in the same proportion as 
  harvested area. Instead, we keep the absolute change in these two levels
  variables the same. So if the model calls for 10 million hectares more
  of harvested area, land cover must also rise by this amount. !

Coefficient (all,i,AEZ_COMM)(all,r,REG)
    HA_LCSHR(i,r)
    # share of harvested area in total land cover #;
Formula  
(all,i,AEZ_COMM)(all,r,REG)
    HA_LCSHR(i,r) = 
sum(k,CROP_COMM, HARVSTAREA_L(i,k,r))/sum(k,ACROP_COMM, LANDCOVER_L(i,k,r)) ;


!<
    ------------------------------------------------------
    Translation of effective hectares to physical hectares
    ------------------------------------------------------

- This version specifies an elasticity of effective crop land wrt land crop land
expansion: ETA. ETA is the percentage change in effective cropland for a given
percentage change in crop land area. So, if ETA is 0.5, then to achieve a
one percent expansion in effective cropland, we need a two percent rise in
harvested cropland area. However, since a one percent rise in harvested cropland
does not require a one percent rise in total land cover, we also pre-multiply
by the ratio of harvested area to land cover. Thus, if ETA = 0.5 and HA is
just 2/3's of land cover, then crop land cover will rise by 4/3's of the rise in
harvested crop land requirements. Mathematically, we have:

ETA = %chng effective cropland / %change harvetsed cropland area
qocropland = our measure of % change in effective cropland
cropland = % change in cropland area, which is NOT the same as cropland cover.
 
Thus:
ETA = qocropland/cropland

The problem arises in determining the percentage change: cropland.

First of all, cropland is based on harvested area (HA), not physically
cultivated area (PA). When there are extensive crop failures, HA < PA. However,
when multiple cropping is present, we have HA > PA. We do not have global crop
data on PA.

Secondly, the data bases on crop land cover (LC) and PA are quite different. 
In the US, cropland cover includes crop land used for pasture, idle land and 
CRP land.The difference is huge. 

Combining both of these problems, we are left with a large, inexplicable, gap
between LC and HA. Since LC >> HA in most cases, assuming that both move in 
equal proportions greatly inflates the total land cover requirements for a given
percentage change in HA. For this reason, we have reverted to the assumption 
that del_LC = del_HA instead. 

In percentage changes, we have:

total land cover = (HA/LC)*harvested area + (Other/LC)*other crop land cover

If other crop land cover (e.g., idle land) does not change, then harvested 
area is simply scaled by HA/LC to give the change in total land cover.
    
>!

Coefficient (parameter) (all,i,AEZ_COMM)(all,r,REG)
    ETA(i,r)
    # elasticity of effective hectares w.r.t. acreage expansion #;
Read
    ETA from file GTAPPARM header "ETA";

! Now provide a revised estimate of the crop land area required, based
on ETA, the elasticity of effective crop land wrt harvested area. !

! Now switch to the harvested area portion of the code. !

Variable (all,i,AEZ_COMM)(all,r,REG)
    harvstaez(i,r)
    # change in crop harvested area within aez #;
Variable (all,i,AEZ_COMM)(all,r,REG)
    harvstslack(i,r)
    # slack variable in land allocation condition #;
Variable (all,j,CROP_COMM)(all,r,REG)
    harvstcom(j,r)
    # aggregates harvested area across AEZs for a given commodity, by region #;
    
Equation E_HARVSTAEZ
(all,i,AEZ_COMM)(all,r,REG)
    harvstaez(i,r) = [1/ETA(i,r)] * qocropland (i,r);

! Non-crop cover adjusts to accommodate this difference, so areaadjust
must change to satisfy the constant hectares condition for each AEZ. !
 
Variable (all,i,AEZ_COMM)(all,r,REG)
    areaadjust(i,r)
    # adjusts noncrop areas to satisfy constant hectares per AEZ #;

Equation E_NONCROPCOV
(all,i,AEZ_COMM)(all,j,PROD2_COVER)(all,r,REG)
    p_LANDCOVER_L(i,j,r) = qoes(i,j,r) + areaadjust(i,r) ;

Coefficient (all,i,AEZ_COMM)(all,j,PROD_COVER)(all,r,REG)
    LCOVERSHR(i,j,r)
    # share in land cover #;
Formula  
(all,i,AEZ_COMM)(all,j,PROD_COVER)(all,r,REG)
    LCOVERSHR(i,j,r) = 
        LANDCOVER_L(i,j,r)/sum(k,PROD_COVER, LANDCOVER_L(i,k,r)) ;

Equation FIXCOVER
# eq'n scales qoes to ensure constant hectares #
(all,i,AEZ_COMM)(all,r,REG)
    lcoveraez(i,r) = 
        sum(j,PROD_COVER, LCOVERSHR(i,j,r) * p_LANDCOVER_L(i,j,r)); 

Equation E_AREAADJUST
# links total land cover in the AEZ to the endowment #
(all,i,AEZ_COMM)(all,r,REG)
    lcoveraez(i,r) = qo(i,r); 
 
! Compute the change in national land cover, by type !

Variable (all,j,PROD_COVER)(all,r,REG)
    lcovercom(j,r)
    # change in regional land cover #;

Coefficient (all,i,AEZ_COMM)(all,j,PROD_COVER)(all,r,REG)
    COMLCOVERSHR(i,j,r)
    # share in land cover #;
Formula  
(all,i,AEZ_COMM)(all,j,PROD_COVER)(all,r,REG)
    COMLCOVERSHR(i,j,r) = 
LANDCOVER_L(i,j,r)/sum(m,AEZ_COMM, LANDCOVER_L(m,j,r)) ;

Equation COMCOVER
# eq'n computes regional land cover change #
(all,j,PROD_COVER)(all,r,REG)
    lcovercom(j,r) = 
 sum(m,AEZ_COMM, COMLCOVERSHR(m,j,r) * p_LANDCOVER_L(m,j,r)); 

Variable (LEVELS) (all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    RENTCROP(i,j,r)
    # level of land rents by AEZ, sector and region, in $/ha. #;
Formula (initial) 
(all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    RENTCROP(i,j,r) = [NVFM(i,j,r)/HARVSTAREA_L(i,j,r)]  ;
Equation E_RENTCROP1
(all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    p_RENTCROP(i,j,r) + p_HARVSTAREA_L(i,j,r) = pmes(i,j,r) + qoes(i,j,r) ;

Variable (LEVELS) (all,i,AEZ_COMM)(all,r,REG)
    RENTCROPAV(i,r)
    # level of average crop land rents by AEZ and region #;
Formula (initial) 
(all,i,AEZ_COMM)(all,r,REG)
    RENTCROPAV(i,r) = 
  [sum(j,CROP_COMM, NVFM(i,j,r))/sum(j,CROP_COMM, HARVSTAREA_L(i,j,r))] ;
Equation E_RENTCROPAV
(all,i,AEZ_COMM)(all,r,REG)
    p_RENTCROPAV(i,r) + harvstaez(i,r) = pmcropland(i,r) + qocropland(i,r) ;

Equation E_HARVSTAREA
# eq'n scales qoes to get revised p_HA and ensure constant physical hectares #
(all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    p_HARVSTAREA_L(i,j,r) = 
        harvstslack(i,r) + qoes(i,j,r) ;

Coefficient (all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    HAREASHR(i,j,r)
    # share of crop j in total AEZ i land area #;
Formula 
(all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    HAREASHR(i,j,r) = 
        HARVSTAREA_L(i,j,r)/sum(k,CROP_COMM, HARVSTAREA_L(i,k,r)) ;

Equation FIXAREA
# eq'n scales qoes to ensure constant hectares #
(all,i,AEZ_COMM)(all,r,REG)
    harvstaez(i,r) = 
        sum(j,CROP_COMM, HAREASHR(i,j,r) * p_HARVSTAREA_L(i,j,r)); 

Equation CROPCOVER
# total harvested area is tied to the endowment of cropland cover  #
(all,i,AEZ_COMM)(all,j,ACROP_COMM)(all,r,REG)
    p_LANDCOVER_L(i,j,r) = HA_LCSHR(i,r) * harvstaez(i,r);

Coefficient (all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    AEZHAREASHR(i,j,r)
    # share of AEZ i in total crop j land area #;
Formula 
(all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    AEZHAREASHR(i,j,r) = 
        HARVSTAREA_L(i,j,r)/sum(k,AEZ_COMM, HARVSTAREA_L(k,j,r)) ;

Equation COMHARVEST
# eq'n scales qoes to ensure constant hectares #
(all,j,CROP_COMM)(all,r,REG)
    harvstcom(j,r) = 
        sum(i,AEZ_COMM, AEZHAREASHR(i,j,r) * p_HARVSTAREA_L(i,j,r)); 

! FYI: also compute the change national average crop yield due to changes 
in crop land area !

Variable (all,r,REG)
    hareaslackreg(r)
    # national yield adjustment #;

Coefficient (all,i,AEZ_COMM)(all,r,REG)
    AEZHREGSHR(i,r)
    # AEZ share in regional land cover #;
Formula  
(all,i,AEZ_COMM)(all,r,REG)
    AEZHREGSHR(i,r) = 
sum(j,CROP_COMM, HARVSTAREA_L(i,j,r))/
        sum(m,AEZ_COMM, sum(j,CROP_COMM, HARVSTAREA_L(m,j,r))) ;

Equation HARVCOVERSLACK
# eq'n computes regional land cover change #
(all,r,REG)
    hareaslackreg(r) =  
 sum(i,AEZ_COMM, AEZHREGSHR(i,r) * harvstslack(i,r)); 

! Finally, compute the productivity-weighted change in land cover. This 
should be zero. !

Variable (all,i,AEZ_COMM)(all,r,REG)
    xlcoveraez(i,r)
# change in productivity-weighted land cover, by aez # ;
Equation E_XLCOVERAEZ
# change in productivity-weighted land cover, by aez #
(all,i,AEZ_COMM)(all,r,REG)
xlcoveraez(i,r) =
        sum(j,PROD2_COVER, REVSHR1(i,j,r) * qoes(i,j,r)) +
        [sum(j,CROP_COMM, REVSHR1(i,j,r))] * qocropland(i,r); 

!Compute land use change by land cover type, AEZ and region!
Variable (change) (all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG)
    LUC(i,j,r) # land cover change #;
Equation E_LUC
# land cover change#
(all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG) 
LUC(i,j,r) = 1/100 * LANDCOVER(i,j,r)* p_LANDCOVER_L(i,j,r);

!Compute crop harvested area change, by crop, AEZ and region!
Variable (change) (all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    cHARVSTAREA(i,j,r) # land cover change #;
Equation E_cHARVSTAREA
# land cover change#
(all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG) 
cHARVSTAREA(i,j,r) = 1/100 * HARVSTAREA(i,j,r)* p_HARVSTAREA_L(i,j,r);

![[! !]]!  
! Land Supply Elasticity Module !
! This module borrows from MAGNET model. Land supply at the levels
  is defined using the following function:
   
  L = A - B/(P^C)
  
  wherein L is the land supply, P is the real rental rate and B and C 
  are positive parameters. C = 1 in the general version of the model. The 
  linearized version of the model is defined by the following equation:

  p_L = (A/L)*p_A + E * p_P

  where the rental rate elasticity is given by the following equation 

  E = C (A / L -1)  
  
  References:
  Woltjer, G.B. and M.H. Kuiper, 2014. The MAGNET Model: Module description.
  Wageningen, LEI Wageningen UR (University & Research centre), 
  LEI Report 14-057. 146 pp.

Warning on asyptote shifter. This is only plausible for small changes

See Dixon et al (2012) RED Versus REDD:
The Battle Between Extending Agricultural Land Use and Protecting Forest

! 
Coefficient (parameter) (all,i,AEZ_COMM)(all,r,REG)
    PARM_C(i,r) # land supply curve shape parameter #;
    Read PARM_C from file GTAPPARM header "PRMC";

Variable (levels) (all,i,AEZ_COMM)(all,r,REG)
    ECONLAND(i,r) # area of land in economic use (in ha - for., crop., past.) #;
Formula (initial) (all,i,AEZ_COMM)(all,r,REG)
    ECONLAND(i,r) = sum(j, PROD_COVER, LANDCOVER_L(i,j,r)) + 1;

Variable (levels) (all,r,REG)
    ECONLANDW(r) # area of land in economic use (in ha - for., crop., past.) #;
Formula&Equation E_ECONLANDW (all,r,REG)
    ECONLANDW(r) = sum(i, AEZ_COMM, ECONLAND(i,r));

Variable (all,i,AEZ_COMM)(all,r,REG)
    p_slackland(i,r) # land supply slack #;

Variable (all,r,REG)
    p_slacklandr(r) # land supply slack #;


Variable (levels,change) (all,i,AEZ_COMM)(all,r,REG)
    MAX_LAND(i,r) # maximum available land (in ha) #;
    Read MAX_LAND from file GTAPDATA header "MAXL";


Coefficient (parameter) (all,i,AEZ_COMM)(all,r,REG)
    MAX_LAND_INT(i,r) # pre-simulation maximum available land (in ha) #;
    Formula (initial) (all,i,AEZ_COMM)(all,r,REG)
    MAX_LAND_INT(i,r) = IF(MAX_LAND(i,r) = 0, 1) + MAX_LAND(i,r);
 
Coefficient (all,i,AEZ_COMM)(all,r,REG)
    ELAND(i,r) # land supply elasticity #;
    Formula (all,i,AEZ_COMM)(all,r,REG)
    ELAND(i,r) = PARM_C(i,r) * (MAX_LAND(i,r)/ECONLAND(i,r) - 1);

Zerodivide default 0;

Coefficient (all,i,AEZ_COMM)(all,r,REG)
    MAXRATIO(i,r) # ratio of maximum land supply to econland #;
    Formula (all,i,AEZ_COMM)(all,r,REG)
    MAXRATIO(i,r) = (MAX_LAND(i,r)/ECONLAND(i,r));

Zerodivide off;
![[!
Equation E_LAND_SUPPLY
# eq'n defines the supply of economically active land in region r #
(all,i,AEZ_COMM)(all,r,REG)
   p_ECONLAND(i,r) = 
                     ELAND(i,r) * pfactreal(i,r) + p_slackland(i,r)
                                + p_slacklandr(r); 
!]]!

Equation E_LAND_SUPPLY
# eq'n defines the supply of economically active land in region r #
(all,i,AEZ_COMM)(all,r,REG)
   p_ECONLAND(i,r)  = p_slacklandr(r) + p_slackland(i,r)    
    - [MAXRATIO(i,r) * ((100/MAX_LAND_INT(i,r))*c_MAX_LAND(i,r))]
    +  ELAND(i,r) * pfactreal(i,r);

Coefficient (parameter)
    ID_PES # Set to zero to turn-off global PES via int'l transfer #;
    READ ID_PES from file GTAPSUPP header "IDPS";

Equation E_PES_COST
# eq'n defines the cost of PES payments to regions with decrease in max land #
(all,r,REG)
pes_inflow(r) = ID_PES * (1/10 ^ 6) * sum(i,AEZ_COMM, PESha(i,r) * c_MAX_LAND(i,r)) ;

!
   p_ECONLAND(i,r) = ELAND(i,r) * pfactreal(i,r) + p_slackland(i,r)
                                + p_slacklandr(r);
!
Equation E_LAND_ENDW
# eq'n defines the supply of economically active land in region r #
(all,i,AEZ_COMM)(all,r,REG)
   qo(i,r) = p_ECONLAND(i,r);


![[! !]]!

!<
    ---------------------------
    F. YIELD TRACKING MODULE
    ---------------------------
>!

Variable (LEVELS) (all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    PRODUCTION(i,j,r)
    # millions of bushels or tonnes (use "PRDN" for tonnes only)#;
Read
    PRODUCTION from file GTAPDATA header "TONS";

Subset
    CROP_COMM is subset of NSAV_COMM;

Equation E_PRODUCTION
# eq'n computes change in crop production by AEZ #
(all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    p_PRODUCTION(i,j,r) = p_HARVSTAREA_L(i,j,r)
            - harvstslack(i,r) + [qo(j,r) - qoes(i,j,r)] ;

Zerodivide (zero_by_zero) default 0;
Zerodivide (nonzero_by_zero) default 0;

! Compute yield from production and harvested area to ensure consistency !

Coefficient (all,j,CROP_COMM)(all,r,REG)
    YIELD(j,r)
    # av. national yield in millions of bushels or tonnes #;
Variable (all,j,CROP_COMM)(all,r,REG)
    p_YIELD(j,r)
  # av. national yield in millions of bushels or tonnes #;
Formula (initial)
(all,j,CROP_COMM)(all,r,REG)
    YIELD(j,r) = 
    sum(i,AEZ_COMM, PRODUCTION(i,j,r))/sum(i,AEZ_COMM, HARVSTAREA_L(i,j,r));
Update (all,j,CROP_COMM)(all,r,REG)
    YIELD(j,r) = p_YIELD(j,r);

Equation E_YIELD
# eq'n computes av. national yield in millions of bushels or tonnes #
(all,j,CROP_COMM)(all,r,REG)
    p_YIELD(j,r) = qo(j,r) - harvstcom(j,r);
    
Zerodivide (zero_by_zero) off;
Zerodivide (nonzero_by_zero) off;

! TH: Decomposing global yield changes. !

Variable (LEVELS)(all,j,CROP_COMM)
    YIELDGL(j)
    # av. global yield in millions of bushels or tonnes #;
Formula & Equation (LEVELS) E_YIELDGL (all,j,CROP_COMM)
    YIELDGL(j) = sum(r, REG, sum(i,AEZ_COMM, PRODUCTION(i,j,r)))
               / sum(r,REG,sum(i,AEZ_COMM,HARVSTAREA_L(i,j,r)));

Coefficient (all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    PRODUCTSHR(i,j,r)
    # share of production in AEZ i, region r of sector j's global output #;
Formula
(all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    PRODUCTSHR(i,j,r) = 
            PRODUCTION(i,j,r)/sum(k,REG, sum(m,AEZ_COMM, PRODUCTION(m,j,k)));

Variable (all,j,CROP_COMM)
    globalouput(j)
    # sector j's global output chng #;
Equation E_GLOBALOUTPUT
# sector j's global output chng #
(all,j,CROP_COMM)
    globalouput(j) = sum(r,REG, sum(i,AEZ_COMM, PRODUCTSHR(i,j,r) * qo(j,r)));

Variable (all,j,CROP_COMM)
    cnt_yield(j)
    # contribution of yield changes sector j's global output chng #;
Equation E_CNT_YIELD
# contribution of yield changes sector j's global output chng #
(all,j,CROP_COMM)
    cnt_yield(j) = sum(r,REG, sum(i,AEZ_COMM, 
        PRODUCTSHR(i,j,r) * [qo(j,r) - qoes(i,j,r) - harvstslack(i,r)])); 

Variable (all,j,CROP_COMM)
    cnt_yieldint(j)
    # contribution of intensive yield changes sector j's global output chng #;
Equation E_CNT_YIELDI
# contribution of intensive yield changes sector j's global output chng #
(all,j,CROP_COMM)
    cnt_yieldint(j) = 
    sum(r,REG, sum(i,AEZ_COMM, PRODUCTSHR(i,j,r) * [qo(j,r) - qoes(i,j,r)]));

Variable (all,j,CROP_COMM)
    cnt_yieldext(j)
    # contribution of extensive yield changes sector j's global output chng #;
Equation E_CNT_YIELDE
# contribution of extensive yield changes sector j's global output chng #
(all,j,CROP_COMM)
    cnt_yieldext(j) = 
    sum(r,REG, sum(i,AEZ_COMM, PRODUCTSHR(i,j,r) * [-harvstslack(i,r)])); 

Variable (all,j,CROP_COMM)
    cnt_area(j)
    # contribution of area changes sector j's global output chng #;
Equation E_CNT_AREA
# contribution of area changes sector j's global output chng #
(all,j,CROP_COMM)
    cnt_area(j) = 
    sum(r,REG, sum(i,AEZ_COMM, PRODUCTSHR(i,j,r) * p_HARVSTAREA_L(i,j,r))); 


! TH: Decomposing regional yield changes. !

Zerodivide (zero_by_zero) default 0;
Zerodivide (nonzero_by_zero) default 0;

Coefficient (all,j,CROP_COMM)(all,r,REG)
    YIELDREG(j,r)
    # av. regional yield in millions of bushels or tonnes #;
Formula (all,j,CROP_COMM)(all,r,REG)
    YIELDREG(j,r) = sum(i,AEZ_COMM, PRODUCTION(i,j,r))
               / sum(i,AEZ_COMM,HARVSTAREA_L(i,j,r));

Coefficient (all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    PRODUCTSHREG(i,j,r)
    # share of production in AEZ i, region r of sector j's regional output #;
Formula
(all,i,AEZ_COMM)(all,j,CROP_COMM)(all,r,REG)
    PRODUCTSHREG(i,j,r) = 
            PRODUCTION(i,j,r)/sum(m,AEZ_COMM, PRODUCTION(m,j,r));

Zerodivide (zero_by_zero) off;
Zerodivide (nonzero_by_zero) off;

Variable (all,j,CROP_COMM)(all,r,REG)
    regouput(j,r)
    # sector j's regional output chng #;
Equation E_REGOUTPUT
# contribution of yield changes sector j's regional  output chng #
(all,j,CROP_COMM)(all,r,REG)
    regouput(j,r) = sum(i,AEZ_COMM, PRODUCTSHREG(i,j,r) * qo(j,r));

Variable (all,j,CROP_COMM)(all,r,REG)
    cnt_yieldreg(j,r)
    # contribution of yield changes sector j's regional  output chng #;
Equation E_CNT_YIELDREG
# contribution of yield changes sector j's regional output chng #
(all,j,CROP_COMM)(all,r,REG)
    cnt_yieldreg(j,r) = sum(i,AEZ_COMM, 
        PRODUCTSHREG(i,j,r) * [qo(j,r) - qoes(i,j,r) - harvstslack(i,r)]); 

Variable (all,j,CROP_COMM)(all,r,REG)
    cnt_yieldintreg(j,r)
    # contribution of intensive yield changes sector j's regional output chng #;
Equation E_CNT_YIELDIREG
# contribution of intensive yield changes sector j's regional output chng #
(all,j,CROP_COMM)(all,r,REG)
    cnt_yieldintreg(j,r) = 
    sum(i,AEZ_COMM, PRODUCTSHREG(i,j,r) * [qo(j,r) - qoes(i,j,r)]);

Variable (all,j,CROP_COMM)(all,r,REG)
    cnt_yieldextreg(j,r)
  # contribution of extensive yield changes sector j's regional  output chng #;
Equation E_CNT_YIELDEREG
# contribution of extensive yield changes sector j's regional output chng #
(all,j,CROP_COMM)(all,r,REG)
    cnt_yieldextreg(j,r) = 
    sum(i,AEZ_COMM, PRODUCTSHREG(i,j,r) * [-harvstslack(i,r)]); 

Variable (all,j,CROP_COMM)(all,r,REG)
    cnt_areareg(j,r)
    # contribution of area changes sector j's regional output chng #;
Equation E_CNT_AREAREG
# contribution of area changes sector j's regional output chng #
(all,j,CROP_COMM)(all,r,REG)
    cnt_areareg(j,r) = 
    sum(i,AEZ_COMM, PRODUCTSHREG(i,j,r) * p_HARVSTAREA_L(i,j,r)); 

![[! 
! Land Use Tracking Module !

UB: Right now the shares do not evolve and depends on initial shares need to figure out how to update this share)

!]]!
![[!
Coefficient (parameter)
(all,j,CROP_COMM)(all,r,REG) SHRCROPDOMR(j,r)
# share of crop production sold domestically #; 
Formula (initial)  (all,j,CROP_COMM)(all,r,REG)
    SHRCROPDOMR(j,r) = VDM(j,r) / VOM(j,r); 

Coefficient (parameter)
(all,j,CROP_COMM)(all,r,REG) SHRCROPEXPR(j,r)
# share of crop production sold as exports #; 
Formula (initial) (all,j,CROP_COMM)(all,r,REG)
    SHRCROPEXPR(j,r) = sum(s,REG, VXMD(j,r,s))/VOM(j,r); 

Coefficient (parameter)
(all,j,CROP_COMM)(all,r,REG)(all,s,REG) SHRCROPEXP(j,r,s)
# share of crop production sold as exports from region r to s#; 
Formula (initial)  (all,j,CROP_COMM)(all,r,REG)(all,s,REG)
    SHRCROPEXP(j,r,s) = VXMD(j,r,s)/sum(q,REG, VXMD(j,r,q)); 

Variable (levels) (all,j,CROP_COMM)(all,r,REG)            CROPDOM(j,r)
 #  crop production sold in domestic market (in M MTs) #;
Variable (levels) (all,j,CROP_COMM)(all,r,REG)            CROPEXPR(j,r)
 #  crop production sold in export market (in M MTs) #;
Variable (levels) (all,j,CROP_COMM)(all,r,REG)(all,s,REG) CROPEXP(j,r,s)
 #  crop production sold as exports from region r to s (in M MTs) #;

Formula&Equation E_CROPDOM (all,j,CROP_COMM)(all,r,REG) 
 CROPDOM(j,r) = SHRCROPDOMR(j,r) * sum(i, AEZ_COMM, PRODUCTION(i,j,r));
Formula&Equation E_CROPEXPR (all,j,CROP_COMM)(all,r,REG) 
 CROPEXPR(j,r) =  SHRCROPEXPR(j,r) * sum(i, AEZ_COMM, PRODUCTION(i,j,r));
Formula&Equation E_CROPEXP (all,j,CROP_COMM)(all,r,REG)(all,s,REG)
 CROPEXP(j,r,s) = SHRCROPEXP(j,r,s) * sum(i, AEZ_COMM, PRODUCTION(i,j,r));


Coefficient (parameter)
(all,j,CROP_COMM)(all,r,REG) LANDHA(j,r)
# ratio of land cover per harvested area of crops #; 

ZERODIVIDE DEFAULT 0 ;
Formula (initial) (all,j,CROP_COMM)(all,r,REG) 
LANDHA     (j,r) = sum(i, AEZ_COMM, LANDCOVER_L(i,"CROPLAND",r))
                  /sum(i, AEZ_COMM, HARVSTAREA_L(i,j,r));
ZERODIVIDE OFF;


YIELDREG(j,r) * HARVRATIO (j,r)
!]]!
![[!
!<
    ------------------------------------------------------
    G. EMISSIONS DUE TO LAND COVER CHANGES TRACKING MODULE
    ------------------------------------------------------
>!
Coefficient (all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG)
    cLANDCOVER(i,j,r)
    # change in land cover by AEZ and region #;
Formula (initial) (all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG)
    cLANDCOVER(i,j,r)=0;
Update (change) (all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG)
    cLANDCOVER(i,j,r) = 1/100 * LANDCOVER(i,j,r) * p_LANDCOVER_L(i,j,r);

Coefficient (parameter) (all,i,AEZ_COMM)(all,r,REG)
    EFFORLOST(i,r)  # emission factor for forestry lost #;
Read
    EFFORLOST from file GTAPPARM header "EFL";

Coefficient (parameter) (all,i,AEZ_COMM)(all,r,REG)
    EFFORGAIN(i,r)  # emission factor for forestry gain #;
Read
    EFFORGAIN from file GTAPPARM header "EFG";

Coefficient (parameter) (all,i,AEZ_COMM)(all,r,REG)
    EFCROP(i,r)  # emission factor for forestry lost #;
Read
    EFCROP from file GTAPPARM header "ECR";

Coefficient (parameter) (all,i,AEZ_COMM)(all,r,REG)
    EFPAST(i,r)  # emission factor for forestry lost #;
Read
    EFPAST from file GTAPPARM header "EPS";

    
Coefficient (all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG)
    EMSSFCTR(i,j,r);
Formula (all,i,AEZ_COMM)(all,j, FOREST_COMM)(all,r,REG)
    EMSSFCTR(i,j,r) = 
                  if(cLANDCOVER(i,j,r) le 0, EFFORLOST(i,r))
                + if(cLANDCOVER(i,j,r) gt 0, EFFORGAIN(i,r));
Formula (all,i,AEZ_COMM)(all,j,ACROP_COMM)(all,r,REG)
    EMSSFCTR(i,j,r) = EFCROP(i,r);
Formula (all,i,AEZ_COMM)(all,j,GRAZE_COMM)(all,r,REG)
    EMSSFCTR(i,j,r) = EFPAST(i,r);

Variable (change) (all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG)
    EMSSLC(i,j,r) #CO2 emissions from land cover change, Mg #;
Coefficient (all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG)
    EMSSLCLEV(i,j,r) #CO2 emissions from land cover change, Mg #;
Formula (initial)(all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG)
    EMSSLCLEV(i,j,r) = 0;
Update (change) (all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG)
    EMSSLCLEV(i,j,r) = EMSSLC(i,j,r);


Equation E_EMSSLC
# new emissions from land cover change #
    (all,i,AEZ_COMM)(all,j,LAND_COVER)(all,r,REG) 
EMSSLC(i,j,r) = - 1/100 * EMSSFCTR(i,j,r) * 
           LANDCOVER(i,j,r)* p_LANDCOVER_L(i,j,r);

Variable (change)(all,j,LAND_COVER)(all,r,REG) 
    EMSSLCALCR(j,r)
    # emissions from land cover change, aggregated over AEZs #;
Equation E_EMSSLCALCR 
# new emissions from land cover change, aggregated over AEZs # 
 (all,j,LAND_COVER)(all,r,REG)  
    EMSSLCALCR(j,r) = sum(i,AEZ_COMM, EMSSLC(i,j,r));

Variable (change)(all,j,LAND_COVER) 
    EMSSLCALC(j)
    # new emissions from land cover change, by land cover type#;
Equation E_EMSSLCALC 
# new emissions from land cover change, by land cover type # 
 (all,j,LAND_COVER) 
    EMSSLCALC(j) = sum(i,AEZ_COMM, sum(r, REG, EMSSLC(i,j,r)));

Variable (change)(all,r,REG) 
    EMSSLCAR(r)
    # new emissions from land cover change, by region #;
Equation E_EMSSLCAR 
# new emissions from land cover change, by region # 
 (all,r, REG) 
    EMSSLCAR(r) = sum(i,AEZ_COMM, sum(j,LAND_COVER, EMSSLC(i,j,r)));

Variable (change) 
    EMSSLCAGL
    # new emissions from land cover change, global total#;
Equation E_EMSSLCAGL 
# new emissions from land cover change, global total # 
    EMSSLCAGL = 
        sum(i,AEZ_COMM, sum(j,LAND_COVER, sum(r,REG,EMSSLC(i,j,r))));
!]]!




!<
    ------------------------------------------------------------
    13. Laspeyres, Paasche and Fisher price and quantity indices
    ------------------------------------------------------------
>!

! GDP expenditure side summary !
Set
    GDPEX (Household,Investment,Government,Exports,IntnlMargins,Imports);


Coefficient (all,r,REG)
    VEXPORTS(r)
    # Value of merchandise and services sales to the world market from r #;
Formula (all,r,REG)
    VEXPORTS(r)
        = sum{i,NMRG_COMM, sum{s,REG, VXWD(i,r,s)}}
        + sum{m,MARG_COMM, sum{s,REG, VXWD(m,r,s)}}
;

Coefficient (all,s,REG)
    VIMPORTS(s)
    # Value of merchandise and services purchases from world market by s #;
Formula (all,s,REG)
    VIMPORTS(s) = sum{i,TRAD_COMM, sum{r,REG, VIWS(i,r,s)}};


Coefficient (all,r,REG)(all,g,GDPEX)
    GDPEXP(r,g) # GDP expenditure side summary #;
Formula
(all,r,REG) GDPEXP(r,"Household")    = PRIVEXP(r);
(all,r,REG) GDPEXP(r,"Investment")   = REGINV(r);
(all,r,REG) GDPEXP(r,"Government")   = GOVEXP(r);
(all,r,REG) GDPEXP(r,"Exports")      = VEXPORTS(r);
(all,r,REG) GDPEXP(r,"IntnlMargins") = sum{m,MARG_COMM, VST(m,r)};
(all,r,REG) GDPEXP(r,"Imports")      = - VIMPORTS(r);

!<
    13-0. GDP-expenditure price and quantity indices
    ------------------------------------------------
>!

Variable (orig_level=VP_ind)(all,r,REG)
    qp_ind(r) # aggregate private consumption quantity #;
Variable (orig_level=VG_ind) (all,r,REG)
    qg_ind(r) # aggregate government consumption quantity #;
Variable (all,r,REG)
    qfobxw_ind(r) # quantity of non margin exports by region r #;
Variable (all,r,REG)
    qstxw_ind(r) # quantity of margin exports by region r #;
Coefficient (all,r,REG)
    VP_ind(r) # value of aggregate private consumption #;
Coefficient (all,r,REG)
    VG_ind(r) # value of aggregate government consumption #;
Coefficient (all,r,REG)
    VFOBXW_ind(r) # value of non margin exports by region r #;
Coefficient (all,r,REG)
    VSTXW_ind(r) # value of margin exports by region r #;
Formula (all,r,REG)
    VP_ind(r) = sum{c,TRAD_COMM, VPA(c,r)};
Formula (all,r,REG)
    VG_ind(r) = sum{c,TRAD_COMM, VGA(c,r)};
Formula (all,r,REG)
    VFOBXW_ind(r) = sum{c,TRAD_COMM, VXW(c,r)};
Formula (all,r,REG)
    VSTXW_ind(r) = sum{m,MARG_COMM, VST(m,r)};

Equation E_qp
# computes % change in aggregate private consumption quantity #
(all,r,REG)
    VP_ind(r) * qp_ind(r) = sum{c,TRAD_COMM, VPA(c,r) * qp(c,r)};

Equation E_qg
# computes % change in aggregate government consumption quantity #
(all,r,REG)
    VG_ind(r) * qg_ind(r) = sum{c,TRAD_COMM, VGA(c,r) * qg(c,r)};

Equation E_qfobxw
# computes % change in quantity of non margin exports by region r #
(all,r,REG)
    VFOBXW_ind(r) * qfobxw_ind(r) = sum{c,TRAD_COMM, sum{d,REG, VXWD(c,r,d) * qxs(c,r,d)}};

Equation E_qstxw
# computes % change in quantity of margin exports by region r #
(all,r,REG)
    VSTXW_ind(r) * qstxw_ind(r) = sum{m,MARG_COMM, VST(m,r) * qst(m,r)};

Variable (all,r,REG)
    pfobxw_ind(r) # price index for non margin exports by region r #;
Variable (all,r,REG)
    pstxw_ind(r) # price index for margin exports by region r #;
Variable (orig_level=1.0)(all,r,REG)(all,g,GDPEX)
    pgdpexp_ind(r,g) # GDP-expenditure component price indices #;
Variable (orig_level=GDPEXP)(all,r,REG)(all,g,GDPEX)
    qgdpexp_ind(r,g) # GDP-expenditure side quantity indices #;
Variable (orig_level=GDPEXP)(all,r,REG)(all,g,GDPEX)
    vgdpexp_ind(r,g) # GDP-expenditure side value indices #;

Equation E_pfobxw
# computes % change in price index for non margin exports by region r #
(all,r,REG)
    VFOBXW_ind(r) * pfobxw_ind(r) = sum{c,TRAD_COMM, sum{d,REG, VXWD(c,r,d) * pfob(c,r,d)}};

Equation E_pstxw
# computes % change in price index for margin exports by region r #
(all,r,REG)
    VSTXW_ind(r) * pstxw_ind(r) = sum{m,MARG_COMM, VST(m,r) * ps(m,r)};

Equation E_pgdpexp_ind
# GDP-expenditure component price indices #
(all,r,REG)(all,g,GDPEX)
    pgdpexp_ind(r,g) = IF[g="household", ppriv(r)] + IF[g="Investment", pcgds(r)]
                 + IF[g="Government", pgov(r)] + IF[g="Exports", pfobxw_ind(r)]
                 + IF[g="IntnlMargins", pstxw_ind(r)] + IF[g="Imports", piwreg(r)];

Equation E_qgdpexp
# GDP-expenditure side quantity indices #
(all,r,REG)(all,g,GDPEX)
    qgdpexp_ind(r,g) = IF[g="household", qp_ind(r)]  + IF[g="Investment", qcgds(r)]
                 + IF[g="Government", qg_ind(r)] + IF[g="Exports", qfobxw_ind(r)]
                 + IF[g="IntnlMargins", qstxw_ind(r)] + IF[g="Imports", qiwreg(r)];

Equation E_vgdpexp
# value index for each GDP-expenditure component #
(all,r,REG)(all,g,GDPEX)
    vgdpexp_ind(r,g) = pgdpexp_ind(r,g) + qgdpexp_ind(r,g);

Variable (orig_level=1.0)(all,r,REG)
    pgdplaspeyres(r) # percent change in GDP Laspeyres price index in r #;
Variable (orig_level=GDP)(all,r,REG)
    qgdplaspeyres(r) # percent change in GDP Laspeyres quantity index in r #;
Variable (orig_level=1.0)(all,r,REG)
    pgdppaasche(r) # percent change in GDP Paasche price index in r #;
Variable (orig_level=GDP)(all,r,REG)
    qgdppaasche(r) # percent change in GDP Paasche quantity index in r #;
Variable (orig_level=1.0)(all,r,REG)
    pgdpfisher(r) # percent change in GDP Fisher price index in r #;
Variable (orig_level=GDP)(all,r,REG)
    qgdpfisher(r) # percent change in GDP Fisher quantity index in r #;
Variable (orig_level=GDP)(all,r,REG)
    vgdpfisher(r) # percent change in GDP Fisher value in r #;
Coefficient (all,r,REG)(all,g,GDPEX)
    GDPEXP_FPIQ(r,g) # GDP expenditure: final price and initial quantity #;
Coefficient (all,r,REG)(all,g,GDPEX)
    GDPEXP_IPFQ(r,g) # GDP expenditure: initial price and final quantity #;
Formula (initial)(all,r,REG)(all,g,GDPEX)
    GDPEXP_FPIQ(r,g) = GDPEXP(r,g);
Formula (initial)(all,r,REG)(all,g,GDPEX)
    GDPEXP_IPFQ(r,g) = GDPEXP(r,g);
Update (all,r,REG)(all,g,GDPEX)
   GDPEXP_FPIQ(r,g) = pgdpexp_ind(r,g);
Update (all,r,REG)(all,g,GDPEX)
   GDPEXP_IPFQ(r,g) = qgdpexp_ind(r,g);

Equation E_pgdplaspeyres
# computes % change in GDP Laspeyres price index in r #
(all,r,REG)
    0 = sum{g, GDPEX, GDPEXP_FPIQ(r,g) * [pgdpexp_ind(r,g) - pgdplaspeyres(r)]};

Equation E_qgdplaspeyres
# computes % change in GDP Laspeyres quantity index in r #
(all,r,REG)
    0 = sum{g, GDPEX, GDPEXP_IPFQ(r,g) * [qgdpexp_ind(r,g) - qgdplaspeyres(r)]};

Equation E_pgdppaasche
# computes % change in GDP Paasche price index in r #
(all,r,REG)
    pgdppaasche(r) = vgdp(r) - qgdplaspeyres(r);

Equation E_qgdppaasche
# computes % change in GDP Paasche quantity index in r #
(all,r,REG)
    qgdppaasche(r) = vgdp(r) - pgdplaspeyres(r);

Equation E_pgdpfisher
# computes % change in GDP Fisher price index in r #
(all,r,REG)
    pgdpfisher(r) = 0.5 * [pgdplaspeyres(r) + pgdppaasche(r)];
Equation E_qgdpfisher
# computes % change in GDP Fisher quantity index in r #
(all,r,REG)
    qgdpfisher(r) = 0.5 * [qgdplaspeyres(r) + qgdppaasche(r)];
Equation E_vgdpfisher
# computes % change in GDP Fisher value in r #
(all,r,REG)
    vgdpfisher(r) = pgdpfisher(r) + qgdpfisher(r);

Variable (orig_level=1.0)(all,r,REG)
    preg(r) # price of aggregate utility in r #;
Equation PHOUSEHOLD
# eq'n defines price of aggregate utility in r  #
(all,r,REG)
    preg(r) = (1 / INCOME(r)) * [sum(i, TRAD_COMM, VPA(i,r) * pp(i,r) 
              + VGA(i,r) * pg(i,r)) + SAVE(r) * psave(r)] ;

Variable (all,i,ENDW_COMM)(all,r,REG)
    pfacthouse(i,r) # price of aggregate utility in r #;

Equation HOUSERETURN 
# eq'n defines the real rate of return to primary factor i in region r # 
(all,i,ENDW_COMM)(all,r,REG)  
    pfacthouse(i,r) = pm(i,r) - preg(r);

Variable (all,r,REG) pfacthouse_l(r) 
# eq'n defines the real rate of return land i in region r #;
Equation E_HOUSERETURN_LAND (all,r,REG)
pfacthouse_l(r) = 
sum(i, AEZ_COMM, [VOA(i,r) / sum(s , AEZ_COMM, VOA(s,r))] * pfacthouse(i,r)); 

Variable (all,r,REG) pfactreal_l(r) 
# eq'n defines the real rate of return land i in region r #;
Equation E_REALRETURN_LAND (all,r,REG)
pfactreal_l(r) = 
sum(i, AEZ_COMM, [VOA(i,r) / sum(s , AEZ_COMM, VOA(s,r))] * pfactreal(i,r)); 


!<
    ====================
    END OF GTAP.TAB FILE
    ====================
>!

!<
    --------------------------------------
    Additional codes for reporting results
    --------------------------------------
>!

Set 
    REGINC # Regional income classification #
    read elements from file GTAPSUPP header "RINC";
Mapping 
    MREGINC from REG to REGINC;
    read (by_elements) MREGINC from file GTAPSUPP header "MRIN";

Coefficient (all,r,REG)
    POPR(r) # population #;
Read
    POPR from file GTAPDATA header "POP";
Update (all,r,REG)
    POPR(r) = pop(r);

Variable (change)(all,r,REG)
    delQGDP(r) # ordinary change in real GDP #;
Equation E_delQGDP
 # ordinary change in real GDP #
(all,r,REG)
    100 * delQGDP(r) = [GDP(r) * qgdpFisher(r)];

Variable (all,r,REGINC)
    qgdpreginc(r) # percent change in GDP - income classification #;
Equation E_qgdpreginc
 # ordinary change in real GDP #
(all,r,REGINC)
    0 = sum{rr, REG:MREGINC(rr)=r, GDP(rr) * [qgdpFisher(rr) - qgdpreginc(r)]};  

Coefficient (parameter)(all,r,REG)
    INITGDP(r) # Initial GDP value #;
Formula (initial)(all,r,REG)
    INITGDP(r) = GDP(r); 
postsim (begin);
Coefficient (all,r,REG)
    REALGDP(r) # Real GDP #;
Formula (all,r,REG)
    REALGDP(r) = INITGDP(r) * [1 + [qgdpFisher(r)*0.01]];
Coefficient (all,r,REG)
    VALRGDP(r) # change in value of Real GDP #;
Formula (all,r,REG)
    VALRGDP(r) = INITGDP(r) * [qgdpFisher(r)*0.01];
Write 
 REALGDP to file GTAPSUM header "RGDP";
 VALRGDP to file GTAPSUM header "VRGD";
postsim (end);

Write
 GDP to file GTAPSUM header "BGDP" longname "Base Nominal GDP";
 (postsim)GDP to file GTAPSUM header "UGDP" longname "Updated Nominal GDP";

!<  
    GDP decomposition
    -----------------
>!

! GDP (Expenditure-side) decomposition  !

Variable (change) (all,r,REG)(all,e,GDPEX)
    contGDPEXP(r,e) # contributions to % change in qgdp #;
Coefficient (all,r,REG)
    BGDP(r) # Initial real GDP at current prices #;
Formula (initial)(all,r,REG)
    BGDP(r) = GDP(r);
Update (all,r,REG)
    BGDP(r) = pgdp(r);
Equation E_contGDPEXP
# (expenditure-side) contributions to % change in real gdp #
(all,r,REG)(all,e,GDPEX)
    BGDP(r) * contGDPEXP(r,e) 
        = IF[e="Household",    sum{c,TRAD_COMM, VPA(c,r) * qp(c,r)} ]
        + IF[e="Investment",   REGINV(r) * qcgds(r)]
        + IF[e="Government",   sum{c,TRAD_COMM, VGA(c,r) * qg(c,r)} ]
        + IF[e="Exports",      sum{c,TRAD_COMM, sum{d,REG, VXWD(c,r,d) * qxs(c,r,d)}}]
        + IF[e="IntnlMargins", sum{m,MARG_comm, VST(m,r) * qst(m,r)} ]
        - IF[e="Imports",      sum{c,TRAD_COMM, sum{s,REG, VIWS(c,s,r) * qxs(c,s,r)}}];

Variable (change)(all,r,REG)
    qgdpexp(r) # GDP (income side) quantity index #;
Equation E_qgdpexp2
# GDP (income side) quantity index #
(all,r,REG)
    qgdpexp(r) = sum{g,GDPEX, contGDPEXP(r,g)}; 

! GDP (Income-side) decomposition  !
Set 
    GDPIN # income-side GDP # = ENDW_COMM + "IndTaxes";
Set 
    CONTGDPIN # Components of income-side GDP changes # = GDPIN + "TechChange";
Set 
    CONTGDPIN2 # income-side GDP # 
        = "Land" + NAEZ_COMM + "IndTaxes" + "TechChange";

Variable (change) (all,r,REG)(all,e,CONTGDPIN)
    contGDPINC(r,e) # contributions to % change in qgdp #;
Equation E_contGDPINC
# (income-side) contributions to % change in real gdp #
(all,r,REG)(all,g,CONTGDPIN)
    BGDP(r) * contGDPINC(r,g) 
        = IF[g in ENDW_COMM, sum{a,PROD_COMM, EVFA(g,a,r) *qfe(g,a,r)}] 
        + IF[g="IndTaxes", 
                sum{a,PROD_COMM, PTAX(a,r) * qo(a,r)}
              + sum{c,TRAD_COMM, sum{a,PROD_COMM, DFTAX(c,a,r) * qfd(c,a,r) 
                                                + IFTAX(c,a,r) * qfm(c,a,r)}}
              + sum{c,TRAD_COMM, DPTAX(c,r) * qpd(c,r) + IPTAX(c,r) * qpm(c,r)}
              + sum{c,TRAD_COMM, DGTAX(c,r) * qgd(c,r) + IGTAX(c,r) * qgm(c,r)}
              + sum{c,TRAD_COMM, sum{d,REG, XTAXD(c,r,d) * qxs(c,r,d)}} 
              + sum{c,TRAD_COMM, sum{s,REG, MTAX(c,s,r)  * qxs(c,s,r)}}
            ] 
        + IF[g="TechChange", 
                sum{a,PROD_COMM, VOA(a,r) * ao(a,r)}
              + sum{e,ENDW_COMM, sum{a,PROD_COMM, VFA(e,a,r) * afe(e,a,r)}}
              + sum{a,PROD_COMM, VVA(a,r) * ava(a,r)}
              + sum{c,TRAD_COMM, sum{a,PROD_COMM, 
                        [VIFA(c,a,r) + VDFA(c,a,r)] * af(c,a,r)}}
              + sum{m,MARG_COMM, sum{c,TRAD_COMM, sum{s,REG, 
                        VTMFSD(m,c,s,r) * atmfsd(m,c,s,r)}}}              
              + sum{c,TRAD_COMM, sum{s,REG, VIMS(c,s,r) * ams(c,s,r)}}
            ];

Variable (change)(all,r,REG)(all,e,CONTGDPIN2)
    contGDPINC2(r,e) # contributions to income side real GDP (1 land) #;
Equation E_contGDPINC2
# (income-side) contributions to % change in real gdp #
(all,r,REG)(all,g,CONTGDPIN2)
   BGDP(r) * contGDPINC2(r,g) 
        = IF[g="land",          sum{e,AEZ_COMM, sum{a,PROD_COMM, EVFA(e,a,r) *qfe(e,a,r)}}] 
        + IF[g in NAEZ_COMM,    sum{e,NAEZ_COMM, sum{a,PROD_COMM, EVFA(g,a,r) *qfe(g,a,r)}}] 
        + IF[g="IndTaxes", 
                sum{a,NSAV_COMM, PTAX(a,r) * qo(a,r)}
              + sum{c,TRAD_COMM, sum{a,PROD_COMM, DFTAX(c,a,r) * qfd(c,a,r) 
                                                + IFTAX(c,a,r) * qfm(c,a,r)}}
              + sum{c,TRAD_COMM, DPTAX(c,r) * qpd(c,r) + IPTAX(c,r) * qpm(c,r)}
              + sum{c,TRAD_COMM, DGTAX(c,r) * qgd(c,r) + IGTAX(c,r) * qgm(c,r)}
              + sum{c,TRAD_COMM, sum{d,REG, XTAXD(c,r,d) * qxs(c,r,d)}} 
              + sum{c,TRAD_COMM, sum{s,REG, MTAX(c,s,r)  * qxs(c,s,r)}}
            ] 
        + IF[g="TechChange", 
                sum{a,PROD_COMM, VOA(a,r) * ao(a,r)}
              + sum{e,ENDW_COMM, sum{a,PROD_COMM, VFA(e,a,r) * afe(e,a,r)}}
              + sum{a,PROD_COMM, VVA(a,r) * ava(a,r)}
              + sum{c,TRAD_COMM, sum{a,PROD_COMM, 
                        [VIFA(c,a,r) + VDFA(c,a,r)] * af(c,a,r)}}
              + sum{m,MARG_COMM, sum{c,TRAD_COMM, sum{s,REG, 
                        VTMFSD(m,c,s,r) * atmfsd(m,c,s,r)}}}              
              + sum{c,TRAD_COMM, sum{s,REG, VIMS(c,s,r) * ams(c,s,r)}}
            ];

Variable (change)(all,r,REG)
    qgdpinc(r) # GDP (income side) quantity index #;
Equation E_qgdpinc
# GDP (income side) quantity index #
(all,r,REG)
    qgdpinc(r) = sum{g,CONTGDPIN, contGDPINC(r,g)}; 

Variable (all,r,REG)
    qgdpdiff(r) # check that decomposed qgdp adds up #;
Equation E_qgdpdiff
# check that decomposed qgdp adds up: must by tiny #
(all,r,REG)
    qgdpdiff(r) = sum{e,GDPIN, contGDPINC(r,e)}  - sum{e,GDPEX, contGDPEXP(r,e)} ;

! GDP (GVA-industry) decomposition  !
Set
    ACTS # Activities # = PROD_COMM - CGDS_COMM;
Set 
    GDPVA # income-side GDP # = ACTS + "IndTaxes";
Set 
    CONTGVA # Components of GVA changes # = GDPVA + "TechChange";

Variable (change) (all,r,REG)(all,e,CONTGVA)
    contGDPGVA(r,e) # contributions to % change in industry GDP #;
Equation E_contGDPGVA
# (income-side) contributions to % change in real gdp #
(all,r,REG)(all,g,CONTGVA)
    BGDP(r) * contGDPGVA(r,g) 
        = IF[g in ACTS, sum{e,ENDW_COMM, EVFA(e,g,r) * qfe(e,g,r)}] 
        + IF[g="IndTaxes", 
                sum{a,PROD_COMM, PTAX(a,r) * qo(a,r)}
              + sum{c,TRAD_COMM, sum{a,PROD_COMM, DFTAX(c,a,r) * qfd(c,a,r) 
                                                + IFTAX(c,a,r) * qfm(c,a,r)}}
              + sum{c,TRAD_COMM, DPTAX(c,r) * qpd(c,r) + IPTAX(c,r) * qpm(c,r)}
              + sum{c,TRAD_COMM, DGTAX(c,r) * qgd(c,r) + IGTAX(c,r) * qgm(c,r)}
              + sum{c,TRAD_COMM, sum{d,REG, XTAXD(c,r,d) * qxs(c,r,d)}} 
              + sum{c,TRAD_COMM, sum{s,REG, MTAX(c,s,r)  * qxs(c,s,r)}}
            ] 
        + IF[g="TechChange", 
                sum{a,PROD_COMM, VOA(a,r) * ao(a,r)}
              + sum{e,ENDW_COMM, sum{a,PROD_COMM, VFA(e,a,r) * afe(e,a,r)}}
              + sum{a,PROD_COMM, VVA(a,r) * ava(a,r)}
              + sum{c,TRAD_COMM, sum{a,PROD_COMM, 
                        [VIFA(c,a,r) + VDFA(c,a,r)] * af(c,a,r)}}
              + sum{m,MARG_COMM, sum{c,TRAD_COMM, sum{s,REG, 
                        VTMFSD(m,c,s,r) * atmfsd(m,c,s,r)}}}              
              + sum{c,TRAD_COMM, sum{s,REG, VIMS(c,s,r) * ams(c,s,r)}}
            ];

Variable (change)(all,r,REG)
    qgdpind(r) # GDP quantity index by industry #;
Equation E_qgdpind
# GDP (income side) quantity index #
(all,r,REG)
    qgdpind(r) = sum{g,CONTGVA, contGDPGVA(r,g)}; 

Set GDPREP (income, expend, industry);
Variable (change)(all,r,REG)(all,g,GDPREP)
    qgdprep(r,g) # GDP quantity index #;
Equation E_qgdprep 
# GDP quantity index #
(all,r,REG)(all,g,GDPREP)
    qgdprep(r,g) = IF[g="expend",   qgdpexp(r)]
                 + IF[g="income",   qgdpinc(r)]
                 + IF[g="industry", qgdpind(r)];

Set 
    BACT # Broad activities #
    read elements from file GTAPSUPP header "BACT";
Mapping 
    MACT from ACTS to BACT;
    read (by_elements) MACT from file GTAPSUPP header "MACT";
Mapping 
    MCOM from TRAD_COMM to BACT;
    read (by_elements) MCOM from file GTAPSUPP header "MACT";
Set 
    CONTGVABACT # Components of GVA changes # = BACT + "IndTaxes" + "TechChange";

Variable (change) (all,r,REG)(all,a,CONTGVABACT)
    contGDPBACT(r,a) # contributions to % change in GDP by broard activities #;
Equation E_contGDPBACT
# contributions to % change in industry GDP #
(all,r,REG)(all,g,CONTGVABACT)
    contGDPBACT(r,g) = IF[g in BACT, sum{a,ACTS:MACT(a)=g, contGDPGVA(r,a)}]
                     + IF[g="IndTaxes",   contGDPGVA(r,"IndTaxes")]
                     + IF[g="TechChange", contGDPGVA(r,"TechChange")]; 

Variable (all,a,BACT)(all,r,REG)
    qobactreg(a,r) # quantity output index by broad activities in region r #;
Equation E_qoreg
# quantity index for supply of good i in region r #
(all,a,BACT)(all,r,REG)
    0 = sum{i,TRAD_COMM:MCOM(i)=a, VOM(i,r) * [qobactreg(a,r) - qo(i,r)]};  

Variable (all,a,BACT)
    qobactwld(a) # global quantity output index by broad activities #;
Equation E_qobactwld
# quantity index for supply of good i in region r #
(all,a,BACT)
    0 = sum{i,TRAD_COMM:MCOM(i)=a, sum{r,REG, VOM(i,r) * [qobactwld(a) - qo(i,r)]}};  

 
!<
    ---------------------------------------
    Additional codes for revenue neutrality
    ---------------------------------------
>!

Set
    AEZ_REVN # Land categories under revenue neutral scenario #
    read elements from file GTAPSUPP header "AEZR";
Subset
    AEZ_REVN is subset of AEZ_COMM;
Set
    PROD_REVN # Activities under revenue neutral scenario #
    read elements from file GTAPSUPP header "PRDR";  
Subset     
    PROD_REVN is subset of PROD_COMM;
Set
    ENDW_X_REVN # Endowments not subject to revenue neutral scenario #
    = ENDW_COMM - AEZ_REVN;
Set
    PROD_X_REVN # Endowments not subject to revenue neutral scenario #
    = PROD_COMM - PROD_REVN;

Variable (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    tfijr(i,j,r) # tax shifter on primary factor i used by j in region r #;
Variable (all,r,REG)
    tfreg(r) # Revenue-neutral shifter on primary factor use in region r #;
Equation E_tf
# tax shifter on primary factor i used by j in region r #
(all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    tf(i,j,r) = IF[i in AEZ_REVN,    IF[j in PROD_REVN,  tfijr(i,j,r) + tfreg(r)]]
              + IF[i in ENDW_X_REVN, IF[j in PROD_X_REVN, tfijr(i,j,r)]];

zerodivide default 0.0;
Coefficient (all,i,NSAV_COMM)(all,r,REG)
    rTO(i,r) # output tax rate #;
Coefficient (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    rTFD(i,j,r) # tax on domestic i purchased by j in r #; 
Coefficient (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    rTFM(i,j,r) # tax on imported i purchased by j in r #;
Coefficient (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    rTF(i,j,r) # tax rate on factor use by firms#;

Coefficient (all,i,NSAV_COMM)(all,r,REG)
    dTO(i,r) # output tax collection #; 
Coefficient  (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    dTFD(i,j,r) # tax on domestic i purchased by j in r #; 
Coefficient (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    dTFM(i,j,r) # tax on imported i purchased by j in r #; 
Coefficient (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    dTF(i,j,r) # tax collection on factor use by firms#;

Formula (all,i,NSAV_COMM)(all,r,REG)
    rTO(i,r) = IF[VOA(i,r)>0 and VOM(i,r)>0, 100*[VOA(i,r)/VOM(i,r)-1]];
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    rTFD(i,j,r) = IF[VDFA(i,j,r)>0 and VDFM(i,j,r)>0, 100*[VDFA(i,j,r)/VDFM(i,j,r) - 1]];
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    rTFM(i,j,r) = IF[VIFA(i,j,r)>0 and VIFM(i,j,r)>0, 100*[VIFA(i,j,r)/VIFM(i,j,r) - 1]];
Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    rTF(i,j,r)  = 100*{[VFA(i,j,r)/VFM(i,j,r)]-1};

Formula (all,i,NSAV_COMM)(all,r,REG)
    dTO(i,r) =VOA(i,r) - VOM(i,r);
Formula (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
    dTF(i,j,r) = VFA(i,j,r) - VFM(i,j,r);
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    dTFD(i,j,r) = VDFA(i,j,r) - VDFM(i,j,r);
Formula (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    dTFM(i,j,r) = VIFA(i,j,r) - VIFM(i,j,r);


Write 
 VDFA to file GTAPSUM header "rVDA";
 (postsim)VDFA to file GTAPSUM header "sVDA";
 VDFM to file GTAPSUM header "rVDM";
 (postsim)VDFM to file GTAPSUM header "sVDM";
 VIFA to file GTAPSUM header "rVIA";
 (postsim)VIFA to file GTAPSUM header "sVIA";
 VIFM to file GTAPSUM header "rVIM";
 (postsim)VIFM to file GTAPSUM header "sVIM";
 VOA to file GTAPSUM header "rVOA";
 (postsim)VOA to file GTAPSUM header "sVOA";
 VOM to file GTAPSUM header "rVOM";
 (postsim)VOM to file GTAPSUM header "sVOM";
 rTO to file GTAPSUM header "rTO";
 (postsim)rTO to file GTAPSUM header "sTO";
 rTF to file GTAPSUM header "rTF";
 (postsim)rTF to file GTAPSUM header "sTF";
 rTFD to file GTAPSUM header "rTFD";
 (postsim)rTFD to file GTAPSUM header "sTFD";
 rTFM to file GTAPSUM header "rTFM";
 (postsim)rTFM to file GTAPSUM header "sTFM";
 dTO to file GTAPSUM header "dTO";
 (postsim)dTO to file GTAPSUM header "eTO";
 dTF to file GTAPSUM header "dTF";
 (postsim)dTF to file GTAPSUM header "eTF";
 dTFD to file GTAPSUM header "dTFD";
 (postsim)dTFD to file GTAPSUM header "eTFD";
 dTFM to file GTAPSUM header "dTFM";
 (postsim)dTFM to file GTAPSUM header "eTFM";
 INCOME to file GTAPSUM header "INC";
 (postsim) INCOME to file GTAPSUM header "UINC";
 (postsim)to to file GTAPSUM header "to";
 (postsim)tfd to file GTAPSUM header "tfd";
 (postsim)tfm to file GTAPSUM header "tfm";
 (postsim)tf to file GTAPSUM header "tf";


!<
    ---------------------------------------------
    END: Add'l codes for revenue neutral scenario
    ---------------------------------------------
>!


!<
    ----------------------------------------
    Additional codes for reporting variables
    ----------------------------------------
>!
!**** industry output at world prices ****!
Variable (levels) (all,i,TRAD_COMM)(all,r,REG)
    qow_r(i,r) # industry output of commodity i in region r valued at world prices #;
Formula (initial) (all,i,TRAD_COMM)(all,r,REG)
    qow_r(i,r) = VOW(i,r);
Equation E_qow_r (all,i,TRAD_COMM)(all,r,REG)
    p_qow_r(i,r) = qo(i,r);

Variable (levels) (all,r,REG)
    qow_cropout(r) # industry real crop output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_crops (all,r,REG)
    qow_cropout(r) =  qow_r("paddyrice",r) + qow_r("wheat",r) + qow_r("crsgrns",r) 
                + qow_r("fruitveg",r) + qow_r("oilsds",r) + qow_r("sugarcrps",r) 
                + qow_r("cotton",r) + qow_r("othercrps",r);

Variable (levels) (all,r,REG)
    qow_pdyrice(r) # industry real paddyrice output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_paddyrice (all,r,REG)
    qow_pdyrice(r) =  qow_r("paddyrice",r);

Variable (levels) (all,r,REG)
    qow_wheat(r) # industry real wheat output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_wheat (all,r,REG)
    qow_wheat(r) =  qow_r("wheat",r);

Variable (levels) (all,r,REG)
    qow_crsgrns(r) # industry real crsgrns output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_crsgrns (all,r,REG)
    qow_crsgrns(r) =  qow_r("crsgrns",r);

Variable (levels) (all,r,REG)
    qow_fruitveg(r) # industry real fruitveg output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_fruitveg (all,r,REG)
    qow_fruitveg(r) =  qow_r("fruitveg",r);

Variable (levels) (all,r,REG)
    qow_oilsds(r) # industry real oilsds output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_oilsds (all,r,REG)
    qow_oilsds(r) =  qow_r("oilsds",r);

Variable (levels) (all,r,REG)
    qow_sugarcrp(r) # industry real sugarcrps output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_sugarcrps (all,r,REG)
    qow_sugarcrp(r) =  qow_r("sugarcrps",r);

Variable (levels) (all,r,REG)
    qow_cotton(r) # industry real cotton output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_cotton (all,r,REG)
    qow_cotton(r) =  qow_r("cotton",r);

Variable (levels) (all,r,REG)
    qow_liveout(r) # industry real livestock output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_liveout (all,r,REG)
    qow_liveout(r) =  qow_r("ruminant",r) + qow_r("nonruminant",r);

Variable (levels) (all,r,REG)
    qow_fishout(r) # industry real fishery output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_fishout (all,r,REG)
    qow_fishout(r) =  qow_r("fishery",r) ;

Variable (levels) (all,r,REG)
    qow_forsout(r) # industry real forest output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_forsout (all,r,REG)
    qow_forsout(r) =  qow_r("forestsec",r);

Variable (levels) (all,r,REG)
    qow_agforout(r) # industry real agri + forestry output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_agforout (all,r,REG)
    qow_agforout(r) =  qow_cropout(r) + qow_liveout(r) + qow_fishout(r);

Variable (levels) (all,r,REG)
    qow_mnfc(r) # industry real nres+mnfcs output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_mnfc (all,r,REG)
    qow_mnfc(r) =  qow_r("NRes",r) + qow_r("Mnfcing",r) + qow_r("PrFood",r) + qow_r("PrLstk",r); 

Variable (levels) (all,r,REG)
    qow_serv(r) # industry real services output in region r (in M USD, at world prices)#;
    Formula&Equation E_qow_serv (all,r,REG)
    qow_serv(r) =  qow_r("Services",r);

!**** aggregate exports at FOB prices ****!

Variable (levels) (all,i,TRAD_COMM)(all,r,REG)
    qxw_r(i,r) # aggregate export of commodity i in region r valued at world prices #;
Formula (initial) (all,i,TRAD_COMM)(all,r,REG)
    qxw_r(i,r) = VXW(i,r);
Equation E_qxw_r (all,i,TRAD_COMM)(all,r,REG)
    p_qxw_r(i,r) = qxw(i,r);

Variable (levels) (all,r,REG)
    qxw_cropout(r) # aggregate export crop in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_crops (all,r,REG)
    qxw_cropout(r) =  qxw_r("paddyrice",r) + qxw_r("wheat",r) + qxw_r("crsgrns",r) 
                + qxw_r("fruitveg",r) + qxw_r("oilsds",r) + qxw_r("sugarcrps",r) 
                + qxw_r("cotton",r) + qxw_r("othercrps",r);

Variable (levels) (all,r,REG)
    qxw_pdyrice(r) # aggregate export paddyrice in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_paddyrice (all,r,REG)
    qxw_pdyrice(r) =  qxw_r("paddyrice",r);

Variable (levels) (all,r,REG)
    qxw_wheat(r) # aggregate export wheat in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_wheat (all,r,REG)
    qxw_wheat(r) =  qxw_r("wheat",r);

Variable (levels) (all,r,REG)
    qxw_crsgrns(r) # aggregate export crsgrns in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_crsgrns (all,r,REG)
    qxw_crsgrns(r) =  qxw_r("crsgrns",r);

Variable (levels) (all,r,REG)
    qxw_fruitveg(r) # aggregate export fruitveg in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_fruitveg (all,r,REG)
    qxw_fruitveg(r) =  qxw_r("fruitveg",r);

Variable (levels) (all,r,REG)
    qxw_oilsds(r) # aggregate export oilsds in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_oilsds (all,r,REG)
    qxw_oilsds(r) =  qxw_r("oilsds",r);

Variable (levels) (all,r,REG)
    qxw_sugarcrp(r) # aggregate export sugarcrps in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_sugarcrps (all,r,REG)
    qxw_sugarcrp(r) =  qxw_r("sugarcrps",r);

Variable (levels) (all,r,REG)
    qxw_cotton(r) # aggregate export cotton in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_cotton (all,r,REG)
    qxw_cotton(r) =  qxw_r("cotton",r);

Variable (levels) (all,r,REG)
    qxw_liveout(r) # aggregate export livestock in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_liveout (all,r,REG)
    qxw_liveout(r) =  qxw_r("ruminant",r) + qxw_r("nonruminant",r);

Variable (levels) (all,r,REG)
    qxw_fishout(r) # aggregate export fishery in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_fishout (all,r,REG)
    qxw_fishout(r) =  qxw_r("fishery",r) ;

Variable (levels) (all,r,REG)
    qxw_forsout(r) # aggregate export forest in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_forsout (all,r,REG)
    qxw_forsout(r) =  qxw_r("forestsec",r);

Variable (levels) (all,r,REG)
    qxw_agforout(r) # aggregate export agri + forestry in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_agforout (all,r,REG)
    qxw_agforout(r) =  qxw_cropout(r) + qxw_liveout(r) + qxw_fishout(r);

Variable (levels) (all,r,REG)
    qxw_mnfc(r) # aggregate export nres+mnfcs in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_mnfc (all,r,REG)
    qxw_mnfc(r) =  qxw_r("NRes",r) + qxw_r("Mnfcing",r) + qxw_r("PrFood",r) + qxw_r("PrLstk",r); 

Variable (levels) (all,r,REG)
    qxw_serv(r) # aggregate export services in region r (in M USD, at world prices)#;
    Formula&Equation E_qxw_serv (all,r,REG)
    qxw_serv(r) =  qxw_r("Services",r);

!**** crop output in metric tonne ****!

Variable (levels) (all,r,REG)
    qmt_pdyrice(r) # paddyrice output in region r (in MTs)#;
    Formula&Equation E_qmt_paddyrice (all,r,REG)
    qmt_pdyrice(r) =  sum(i, AEZ_COMM, PRODUCTION(i,"paddyrice",r)) + 0.0001;

Variable (levels) (all,r,REG)
    qmt_wheat(r) # wheat output in region r (in MTs)#;
    Formula&Equation E_qmt_wheat (all,r,REG)
    qmt_wheat(r) =  sum(i, AEZ_COMM, PRODUCTION(i,"wheat",r)) + 0.0001;

Variable (levels) (all,r,REG)
    qmt_crsgrns(r) # crsgrns output in region r (in MTs)#;
    Formula&Equation E_qmt_crsgrns (all,r,REG)
    qmt_crsgrns(r) =  sum(i, AEZ_COMM, PRODUCTION(i,"crsgrns",r)) + 0.0001;

Variable (levels) (all,r,REG)
    qmt_fruitveg(r) # fruitveg output in region r (in MTs)#;
    Formula&Equation E_qmt_fruitveg (all,r,REG)
    qmt_fruitveg(r) =  sum(i, AEZ_COMM, PRODUCTION(i,"fruitveg",r)) + 0.0001;

Variable (levels) (all,r,REG)
    qmt_oilsds(r) # oilsds output in region r (in MTs)#;
    Formula&Equation E_qmt_oilsds (all,r,REG)
    qmt_oilsds(r) =  sum(i, AEZ_COMM, PRODUCTION(i,"oilsds",r)) + 0.0001;

Variable (levels) (all,r,REG)
    qmt_sugarcrp(r) # sugarcrps output in region r (in MTs)#;
    Formula&Equation E_qmt_sugarcrps (all,r,REG)
    qmt_sugarcrp(r) =  sum(i, AEZ_COMM, PRODUCTION(i,"sugarcrps",r)) + 0.0001;

Variable (levels) (all,r,REG)
    qmt_cotton(r) # cotton output in region r (in MTs)#;
    Formula&Equation E_qmt_cotton (all,r,REG)
    qmt_cotton(r) =  sum(i, AEZ_COMM, PRODUCTION(i,"cotton",r)) + 0.0001;

!**** other reporting variables ****!

Variable (all,r,REG)(orig_level=1) pfactrl_land(r) 
# real rate of return land i in region r #;
Equation E_REALRETURN_LND (all,r,REG)
pfactrl_land(r) = 
sum(i, AEZ_COMM, [VOA(i,r) / sum(s , AEZ_COMM, VOA(s,r))] * pfactreal(i,r));

Variable (all,r,REG)(orig_level=1) pfactrl_sklab(r) 
# real rate of skilled labor in region r #;
Equation E_REALRETURN_SKLAB (all,r,REG)
pfactrl_sklab(r) = pfactreal("SkLab",r);

Variable (all,r,REG)(orig_level=1) pfactrl_usklab(r) 
# real rate of unskilled labor in region r #;
Equation E_REALRETURN_USKLAB (all,r,REG)
pfactrl_usklab(r) = pfactreal("UnSkLab",r);

Variable (all,r,REG)(orig_level=1) pfactrl_cap(r) 
# real rate of capital in region r #;
Equation E_REALRETURN_CAP (all,r,REG)
pfactrl_cap(r) = pfactreal("Capital",r); 

Variable (levels) (all,r,REG) lc_cropland(r) 
# cropland cover in region r (in hectares) #;
Formula&Equation E_LC_CROPLAND (all,r,REG)
lc_cropland(r) = LANDCOVER_R("CROPLAND",r);

Variable (levels) (all,r,REG) lc_mngforest(r) 
# managed forest cover in region r (in hectares) #;
Formula&Equation  E_LC_MNGD_FOREST (all,r,REG)
lc_mngforest(r) = LANDCOVER_R("forestsec",r);

Variable (levels) (all,r,REG) lc_pasture(r)
# pasture cover in region r (in hectares) #;
Formula&Equation  E_LC_PASTURE (all,r,REG)
lc_pasture(r) = LANDCOVER_R("ruminant",r);

Variable (change) (all,r,REG) lc_ntl_land(r) 
# change in natural land cover in region r (in hectares) #;
Equation E_LC_NTL_LAND (all,r,REG)
lc_ntl_land(r) = p_lc_cropland(r) + p_lc_mngforest(r) + p_lc_pasture(r);

Variable (all,r,REG) 
    pmallland(r) # Aggregate land returns #;
Coefficient (all,r,REG) 
    VFM_AEZ(r) # Total land #;
Formula (all,r,REG)
    VFM_AEZ(r) = sum{i,AEZ_COMM, sum{j,PROD_COMM, VFM(i,j,r)}};

Equation E_pmallland
# Aggregate land returns #
(all,r,REG) 
   pmallland(r) = sum{i,AEZ_COMM, sum{j,PROD_COMM, VFM(i,j,r)/ID01[VFM_AEZ(r)] * pmes(i,j,r)}};

!<
    ---------------------------------------------
    END: Add'l codes for revenue neutral scenario
    ---------------------------------------------
>!


!<
    ---------------------
    Add'l codes for taxes
    ---------------------
>!

! Additional variables for tracking difference for VOM and VOA !
! **** VOA **** ! 
Variable (levels) (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG) VDFA_alt(i,j,r)
    # VDFA alternative #;
    Formula (initial) (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG) VDFA_alt(i,j,r) = VDFA(i,j,r);
    Equation E_VDFA_alt (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG) p_VDFA_alt(i,j,r) = pfd(i,j,r) + qfd(i,j,r);
 
Variable (levels) (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG) VIFA_alt(i,j,r)
    # VIFA alternative #;
    Formula (initial) (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG) VIFA_alt(i,j,r) = VIFA(i,j,r);
    Equation E_VIFA_alt (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG) p_VIFA_alt(i,j,r) = pfm(i,j,r) + qfm(i,j,r);

Variable (levels) (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG) EVFA_alt(i,j,r)
    # EVFA alternative #;
    Formula (initial) (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG) EVFA_alt(i,j,r) = EVFA(i,j,r);
    Equation E_EVFA_alt (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)  
    p_EVFA_alt(i,j,r) = pfe(i,j,r) + qfe(i,j,r);

Variable (levels) (all,i,DEMD_COMM)(all,j,PROD_COMM)(all,r,REG) VFA_alt(i,j,r)
    # VFA alternative #;
   Formula&Equation  E_VFA_alt_endw (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG) VFA_alt(i,j,r) = EVFA_alt(i,j,r); ! need to update evfa !
   Formula&Equation  E_VFA_alt_trad (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG) VFA_alt(i,j,r) = VDFA_alt(i,j,r) + VIFA_alt(i,j,r);

Variable (levels) (all,i,NSAV_COMM)(all,r,REG) VOA_alt(i,r)
    # EVOA alternative #  ;
    Formula (initial) (all,i,ENDW_COMM)(all,r,REG)  VOA_alt(i,r) = EVOA(i,r);
    Equation E_VOA_alt_endw (all,i,ENDW_COMM)(all,r,REG) 
       p_VOA_alt(i,r) = ps(i,r) + qo(i,r);
    Formula&Equation E_VOA_alt_prod (all,i,PROD_COMM)(all,r,REG) 
         VOA_alt(i,r) = sum(j,DEMD_COMM, VFA_alt(j,i,r));

! **** VOM **** !
    Variable (levels) (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
       VFM_alt(i,j,r) # producer expenditure on i by j in r valued at mkt prices #;
    Formula (initial) (all,i,ENDW_COMM)(all,j,PROD_COMM)(all,r,REG)
       VFM_alt(i,j,r) = VFM(i,j,r);
    Equation E_VFM_alt_endwm (all,i,ENDWM_COMM)(all,j,PROD_COMM)(all,r,REG)
       p_VFM_alt(i,j,r) = pm(i,r) + qfe(i,j,r);
    Equation E_VFM_alt_endws (all,i,ENDWS_COMM)(all,j,PROD_COMM)(all,r,REG)
       p_VFM_alt(i,j,r) = pmes(i,j,r) + qfe(i,j,r);

         Variable (levels)(all,i,TRAD_COMM)(all,r,REG)
                VDPM_alt(i,r) # private consumption expenditure on domestic i in r #;
            Formula (initial) (all,i,TRAD_COMM)(all,r,REG)
                VDPM_alt(i,r) = VDPM(i,r);
            Equation E_VDPM_alt (all,i,TRAD_COMM)(all,r,REG)
                p_VDPM_alt(i,r) = pm(i,r) + qpd(i,r);
            
            Variable (levels)(all,i,TRAD_COMM)(all,r,REG)
                VDGM_alt(i,r) # government consumption expenditure on domestic i in r #;
            Formula (initial) (all,i,TRAD_COMM)(all,r,REG)
                VDGM_alt(i,r) = VDGM(i,r);
            Equation E_VDGM_alt (all,i,TRAD_COMM)(all,r,REG)
                p_VDGM_alt(i,r) = pm(i,r) + qgd(i,r);

            Variable (levels)(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
                VDFM_alt(i,j,r) # purchases of domestic i for use by j in region r #;
            Formula (initial) (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
                VDFM_alt(i,j,r) = VDFM(i,j,r);
            Equation E_VDFM_alt (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
                p_VDFM_alt(i,j,r) = pm(i,r) + qfd(i,j,r);

            Variable (levels)(all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
                VIFM_alt(i,j,r) # purchases of imports i for use by j in region r #;
            Formula (initial) (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
                VIFM_alt(i,j,r) = VIFM(i,j,r);
            Equation E_VIFM_alt (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
                p_VIFM_alt(i,j,r) = pim(i,r) + qfm(i,j,r);


    Variable (levels) (all,i,TRAD_COMM)(all,r,REG)
    VDM_alt(i,r) # VDM Alternative #;
    Formula&Equation E_VDM_alt (all,i,TRAD_COMM)(all,r,REG)
    VDM_alt(i,r) = VDPM_alt(i,r) + VDGM_alt(i,r) + sum(j,PROD_COMM, VDFM_alt(i,j,r));

Variable (levels)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VXMD_alt(i,r,s)
    # exports of i from r to s valued at mkt prices (tradeables only) #;
Formula (initial)(all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    VXMD_alt(i,r,s) =  VXMD(i,r,s);
Equation E_VXMD_alt (all,i,TRAD_COMM)(all,r,REG)(all,s,REG)
    p_VXMD_alt(i,r,s) = pm(i,r) + qxs(i,r,s);

Variable (levels)(all,m,MARG_COMM)(all,r,REG)
    VST_alt(m,r) # exprts of m from r for int'l trnsport valued at mkt p (tradeables only) #;
Equation E_VST_alt (all,m,MARG_COMM)(all,r,REG)
    p_VST_alt(m,r) = pm(m,r) + qst(m,r);
Formula (initial)(all,m,MARG_COMM)(all,r,REG)
    VST_alt(m,r) = VST(m,r);

Variable (levels) (all,i,NSAV_COMM)(all,r,REG)
    VOM_alt(i,r) # VOM Alternative #;

Formula&Equation E_VOM_alt_endw (all,i,ENDW_COMM)(all,r,REG)
    VOM_alt(i,r) = sum(j,PROD_COMM, VFM_alt(i,j,r)) + 0.001; ! Add tiny value !

Formula&Equation E_VOM_alt_marg (all,m,MARG_COMM)(all,r,REG)
    VOM_alt(m,r) = VDM_alt(m,r) + sum(s,REG, VXMD_alt(m,r,s)) + VST_alt(m,r);

Formula&Equation E_VOM_alt_nmrg (all,i,NMRG_COMM)(all,r,REG)
    VOM_alt(i,r) = VDM_alt(i,r) + sum(s,REG, VXMD_alt(i,r,s));

Formula&Equation E_VOM_alt_cgds (all,h,CGDS_COMM)(all,r,REG)
    VOM_alt(h,r) = VOA_alt(h,r);

Variable (levels,change) (all,i,NSAV_COMM)(all,r,REG)
    dTO_alt(i,r) # Difference in VOA and VOM #;
Formula&Equation E_dTO_alt (all,i,NSAV_COMM)(all,r,REG)
    dTO_alt(i,r) = {VOA_alt(i,r) - VOM_alt(i,r)}; ! Add tiny value !

Coefficient (parameter) (all,i,TRAD_COMM)(all,r,REG)
    TO_dTO_ID(i,r) # ID for TRAD_COMM with Output Subsidy Reduction #;
        Read  TO_dTO_ID from file GTAPSUPP header "IDTO";   
    
Coefficient (Parameter) (all,i,AEZ_COMM)(all,r,REG)
    TO_AEZ_ID(i,r) # value per ha for AEZ_COMM with Subsidy Repurpose: for weights calc #;
        Read  TO_AEZ_ID from file GTAPSUPP header "AZTO";   

Coefficient (all,i,AEZ_COMM)(all,r,REG)
    WGHT_AEZ_ID(i,r) # weights for AEZ_COMM with Subsidy Repurpose: by reg , aez #;
        Formula (all,i,AEZ_COMM)(all,r,REG) 
        WGHT_AEZ_ID(i,r) = TO_AEZ_ID(i,r)/sum(j, AEZ_COMM, TO_AEZ_ID(j,r));

Variable (levels,change) (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    dTFD_alt(i,j,r) # Difference in VDFA and VDFM #;
Formula&Equation E_dTFD_alt (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    dTFD_alt(i,j,r) = {VDFA_alt(i,j,r) - VDFM_alt(i,j,r)}; ! Add tiny value !

Coefficient (parameter) (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    TO_dTFD_ID(i,j,r) # ID for TRAD_COMM with Domestic Subsidy Reduction #;
        Read  TO_dTFD_ID from file GTAPSUPP header "IDTD";   

Variable (levels,change) (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    dTFM_alt(i,j,r) # Difference in VIFA and VIFM #;
Formula&Equation E_dTFM_alt (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    dTFM_alt(i,j,r) = {VIFA_alt(i,j,r) - VIFM_alt(i,j,r)}; ! Add tiny value !

Coefficient (parameter) (all,i,TRAD_COMM)(all,j,PROD_COMM)(all,r,REG)
    TO_dTFM_ID(i,j,r) # ID for TRAD_COMM with Imports Subsidy Reduction #;
        Read  TO_dTFM_ID from file GTAPSUPP header "IDTM";   

Variable (change) (all,r,REG)
    c_dTO_slack(r) # slack variable for TO #;

Variable (change) (all,i,AEZ_COMM)(all,r,REG)
    c_dTO_AEZ(i,r) # Change in subsidy payments TO by region by AEZ#;
Equation E_c_dTO_AEZ (all,i,AEZ_COMM)(all,r,REG)   
       c_dTO_AEZ(i,r) = TO_AEZ_ID(i,r) * c_dTO_alt(i,r);

Variable (change) (all,r,REG)
    c_dTO_AEZR(r) # Change in subsidy payments TO by region #;
Equation E_c_dTO_AEZR (all,r,REG)   
       c_dTO_AEZR(r) = sum(j, AEZ_COMM, TO_AEZ_ID(j,r) * c_dTO_alt(j,r));
   
Equation E_dTO_revnue (all,r,REG)
- sum(j, TRAD_COMM, TO_dTO_ID(j,r)*c_dTO_alt(j,r)) +
  sum(j, TRAD_COMM, sum(k, PROD_COMM, TO_dTFD_ID(j,k,r)*c_dTFD_alt(j,k,r))) +
  sum(j, TRAD_COMM, sum(k, PROD_COMM, TO_dTFM_ID(j,k,r)*c_dTFM_alt(j,k,r)))
 = sum(j, AEZ_COMM, WGHT_AEZ_ID(j,r) * [c_dTO_alt(j,r)]) + c_dTO_slack(r) ;  

! EC (Apr 2022): Add global PES welfare contribution !
Variable (linear,change) (all,r,REG)
    CNTpesgrin(r) # contribution to regional EV of changes in PES inflow #;
Equation E_CNTpesgin
(all,r,REG)
    CNTpesgrin(r)
        = [0.01 * EVSCALFACT(r)] * 100 * pes_inflow(r);

Variable (linear,change) (all,r,REG)
    CNTpesgrout(r) # contribution to regional EV of changes in PES outflow  #;
Equation E_CNTpesgrgout
(all,r,REG)
    CNTpesgrout(r)
        = [0.01 * EVSCALFACT(r)] * 100 * pes_outflow(r);

Variable (linear,change) (all,r,REG)
    CNTpesgr(r) # contribution to regional EV of net changes in PES #;
Equation E_CNTpesgr
(all,r,REG)
    CNTpesgr(r)
        = CNTpesgrin(r) - CNTpesgrout(r);
! End: Add global PES !

! EC (Aug 2022): Add % change in utility by aggregate region !
Coefficient (all,r,REGINC)
    AGGINCOMEEV(r) # aggregate regional household per capita expenditure #;
Formula (all,r,REGINC)
    AGGINCOMEEV(r) = sum{rr, REG:MREGINC(rr)=r, INCOMEEV(rr)};

Variable (orig_level=AGGINCOMEEV)(all,r,REGINC)
    ureginc(r) # percent change in per capita utility - by aggregate regions #;
Equation E_ureginc
 # ordinary change in real GDP #
(all,r,REGINC)
    0 = sum{rr, REG:MREGINC(rr)=r, INCOMEEV(rr) * [u(rr) - ureginc(r)]};  
! EC (Aug 2022): End of changes !

Write
 INCOMEEV to file GTAPSUM header "YEV0";
(postsim) INCOMEEV to file GTAPSUM header "YEVU";

! EC (Aug 2022): Add GDP decomposition by aggregate region !
Variable (all,r,REGINC)(all,g,GDPEX)
    pgdpexreginc(r,g) # percent change in GDP expenditure components, by aggregate region #;
Equation E_pgdpexreginc
# GDP-expenditure component price indices #
(all,r,REGINC)(all,e,GDPEX)
    0 = sum{rr,REG:MREGINC(rr)=r, GDPEXP(rr,e) * [pgdpexp_ind(rr,e) - pgdpexreginc(r,e)]};

Variable (all,r,REGINC)(all,g,GDPEX)
    qgdpexreginc(r,g) # percent change in real GDP expenditure components, by aggregate region #;
Equation E_qgdpexreginc
# GDP-expenditure component quantity indices #
(all,r,REGINC)(all,e,GDPEX)
    0 = sum{rr,REG:MREGINC(rr)=r, GDPEXP(rr,e) * [qgdpexp_ind(rr,e) - qgdpexreginc(r,e)]};

Coefficient (all,r,REGINC)(all,e,GDPEX)
    GDPEXP_a(r,e) # GDP expenditure components, by aggregate region #;
Formula (all,r,REGINC)(all,e,GDPEX)
    GDPEXP_a(r,e) = sum{rr,REG:MREGINC(rr)=r, GDPEXP(rr,e)};
Coefficient (all,r,REGINC)(all,e,GDPEX)
    BGDPEXP_a(r,e) # Initial real GDP at current prices, by aggregate region #;
Formula (initial)(all,r,REGINC)(all,e,GDPEX)
    BGDPEXP_a(r,e) = GDPEXP_a(r,e);
Update (all,r,REGINC)(all,e,GDPEX)
    BGDPEXP_a(r,e) = pgdpexreginc(r,e);
Variable (all,r,REGINC)
    pgdpreginc(r) # percent change in GDP price index - income classification #;
Equation E_pgdpreginc
 # ordinary change in real GDP #
(all,r,REGINC)
    0 = sum{rr, REG:MREGINC(rr)=r, GDP(rr) * [pgdpFisher(rr) - pgdpreginc(r)]};

Coefficient (all,r,REGINC)
    BGDP_a(r) # Initial real GDP at current prices, by aggregate region #;
Formula (initial) (all,r,REGINC)
    BGDP_a(r) = sum{rr,REG:MREGINC(rr)=r, BGDP(rr)};
Update (all,r,REGINC)
    BGDP_a(r) = pgdpreginc(r);
Variable (change) (all,r,REGINC)(all,e,GDPEX)
    contGDPEXP_a(r,e) # contributions to % change in qgdp by aggregate region #;
Equation E_contGDPEXP_a
# (expenditure-side) contributions to % change in real gdp #
(all,r,REGINC)(all,e,GDPEX)
    BGDP_a(r) * contGDPEXP_a(r,e) 
        = GDPEXP_a(r,e) * qgdpexreginc(r,e);

Variable (change)(all,r,REGINC)(all,e,CONTGDPIN2)
    contGDPINC_a(r,e) # contributions to income side real GDP (1 land) #;
Equation E_contGDPINC_a
# (income-side) contributions to % change in real gdp #
(all,rr,REGINC)(all,g,CONTGDPIN2)
   BGDP_a(rr) * contGDPINC_a(rr,g) 
        = sum{r,REG:MREGINC(r)=rr,
          IF[g="land",          sum{e,AEZ_COMM, sum{a,PROD_COMM, EVFA(e,a,r) *qfe(e,a,r)}}] 
        + IF[g in NAEZ_COMM,    sum{e,NAEZ_COMM, sum{a,PROD_COMM, EVFA(g,a,r) *qfe(g,a,r)}}] 
        + IF[g="IndTaxes", 
                sum{a,NSAV_COMM, PTAX(a,r) * qo(a,r)}
              + sum{c,TRAD_COMM, sum{a,PROD_COMM, DFTAX(c,a,r) * qfd(c,a,r) 
                                                + IFTAX(c,a,r) * qfm(c,a,r)}}
              + sum{c,TRAD_COMM, DPTAX(c,r) * qpd(c,r) + IPTAX(c,r) * qpm(c,r)}
              + sum{c,TRAD_COMM, DGTAX(c,r) * qgd(c,r) + IGTAX(c,r) * qgm(c,r)}
              + sum{c,TRAD_COMM, sum{d,REG, XTAXD(c,r,d) * qxs(c,r,d)}} 
              + sum{c,TRAD_COMM, sum{s,REG, MTAX(c,s,r)  * qxs(c,s,r)}}
            ] 
        + IF[g="TechChange", 
                sum{a,PROD_COMM, VOA(a,r) * ao(a,r)}
              + sum{e,ENDW_COMM, sum{a,PROD_COMM, VFA(e,a,r) * afe(e,a,r)}}
              + sum{a,PROD_COMM, VVA(a,r) * ava(a,r)}
              + sum{c,TRAD_COMM, sum{a,PROD_COMM, 
                        [VIFA(c,a,r) + VDFA(c,a,r)] * af(c,a,r)}}
              + sum{m,MARG_COMM, sum{c,TRAD_COMM, sum{s,REG, 
                        VTMFSD(m,c,s,r) * atmfsd(m,c,s,r)}}}              
              + sum{c,TRAD_COMM, sum{s,REG, VIMS(c,s,r) * ams(c,s,r)}}
            ]
            };

Variable 
    pgdpwld # percent change in GDP price index - income classification #;
Equation E_pgdpwld
 # ordinary change in real GDP #
    0 = sum{r, REG, GDP(r) * [pgdpFisher(r) - pgdpwld]};
Variable 
    qgdpwld # percent change in GDP price index - income classification #;
Equation E_qgdpwld
 # ordinary change in real GDP #
    0 = sum{r, REG, GDP(r) * [qgdpFisher(r) - qgdpwld]};

Variable (all,e,GDPEX)
    qgdpewld(e) # percent change in global real GDP expenditure components #;
Equation E_qgdpewld
# GDP-expenditure component quantity indices #
(all,e,GDPEX)
    0 = sum{r,REG, GDPEXP(r,e) * [qgdpexp_ind(r,e) - qgdpewld(e)]};
Variable (all,e,GDPEX)
    pgdpewld(e) # percent change in global real GDP expenditure components #;
Equation E_pgdpewld
# GDP-expenditure component quantity indices #
(all,e,GDPEX)
    0 = sum{r,REG, GDPEXP(r,e) * [pgdpexp_ind(r,e) - pgdpewld(e)]};

Coefficient (all,e,GDPEX)
    GDPEXPWLD(e) # GDP expenditure components, by aggregate region #;
Formula (all,e,GDPEX)
    GDPEXPWLD(e) = sum{r,REG, GDPEXP(r,e)};
Coefficient (all,e,GDPEX)
    BGDPEXPWLD(e) # Initial real GDP at current prices, global #;
Formula (initial)(all,e,GDPEX)
    BGDPEXPWLD(e) = GDPEXPWLD(e);
Update (all,e,GDPEX)
    BGDPEXPWLD(e) = pgdpewld(e);

Coefficient 
    BGDPWLD # Initial real GDP at current prices, world #;
Formula (initial) 
    BGDPWLD = sum{r,REG, BGDP(r)};
Update 
    BGDPWLD = pgdpwld;
Variable (change) (all,e,GDPEX)
    contGDPEXP_w(e) # contributions to % change in qgdp world #;
Equation E_contGDPEXP_w
# (expenditure-side) contributions to % change in real gdp #
(all,e,GDPEX)
    BGDPWLD * contGDPEXP_w(e) = BGDPEXPWLD(e) * qgdpewld(e);

Variable (change)(all,g,CONTGDPIN2)
    contGDPINC_w(g) # contributions to income side real GDP, world #;
Equation E_contGDPINC_w
# (income-side) contributions to % change in real gdp #
(all,g,CONTGDPIN2)
   BGDPWLD * contGDPINC_w(g) 
        = sum{r,REG,
          IF[g="land",          sum{e,AEZ_COMM, sum{a,PROD_COMM, EVFA(e,a,r) *qfe(e,a,r)}}] 
        + IF[g in NAEZ_COMM,    sum{e,NAEZ_COMM, sum{a,PROD_COMM, EVFA(g,a,r) *qfe(g,a,r)}}] 
        + IF[g="IndTaxes", 
                sum{a,NSAV_COMM, PTAX(a,r) * qo(a,r)}
              + sum{c,TRAD_COMM, sum{a,PROD_COMM, DFTAX(c,a,r) * qfd(c,a,r) 
                                                + IFTAX(c,a,r) * qfm(c,a,r)}}
              + sum{c,TRAD_COMM, DPTAX(c,r) * qpd(c,r) + IPTAX(c,r) * qpm(c,r)}
              + sum{c,TRAD_COMM, DGTAX(c,r) * qgd(c,r) + IGTAX(c,r) * qgm(c,r)}
              + sum{c,TRAD_COMM, sum{d,REG, XTAXD(c,r,d) * qxs(c,r,d)}} 
              + sum{c,TRAD_COMM, sum{s,REG, MTAX(c,s,r)  * qxs(c,s,r)}}
            ] 
        + IF[g="TechChange", 
                sum{a,PROD_COMM, VOA(a,r) * ao(a,r)}
              + sum{e,ENDW_COMM, sum{a,PROD_COMM, VFA(e,a,r) * afe(e,a,r)}}
              + sum{a,PROD_COMM, VVA(a,r) * ava(a,r)}
              + sum{c,TRAD_COMM, sum{a,PROD_COMM, 
                        [VIFA(c,a,r) + VDFA(c,a,r)] * af(c,a,r)}}
              + sum{m,MARG_COMM, sum{c,TRAD_COMM, sum{s,REG, 
                        VTMFSD(m,c,s,r) * atmfsd(m,c,s,r)}}}              
              + sum{c,TRAD_COMM, sum{s,REG, VIMS(c,s,r) * ams(c,s,r)}}
            ]
            };

! results by aggregate sector and aggregate regions !
Variable (all,a,BACT)(all,r,REGINC)
    qoar(a,r) # quantity output index by broad activities in region r #;
Equation E_qoar
# quantity index for supply of good i in region r #
(all,a,BACT)(all,r,REGINC)
    0 = sum{i,TRAD_COMM:MCOM(i)=a, sum{rr,REG:MREGINC(rr)=r,
                 VOM(i,rr) * [qoar(a,r) - qo(i,rr)]}};  

Variable (all,a,BACT)(all,r,REGINC)
    poar(a,r) # quantity output index by broad activities in region r #;
Equation E_poreg
# quantity index for supply of good i in region r #
(all,a,BACT)(all,r,REGINC)
    0 = sum{i,TRAD_COMM:MCOM(i)=a, sum{rr,REG:MREGINC(rr)=r,
                 VOM(i,rr) * [poar(a,r) - pm(i,rr)]}};  

Variable (all,a,BACT)
    pobactwld(a) # global quantity output index by broad activities #;
Equation E_pobactwld
# quantity index for supply of good i in region r #
(all,a,BACT)
    0 = sum{i,TRAD_COMM:MCOM(i)=a, sum{r,REG, VOM(i,r) * [pobactwld(a) - qo(i,r)]}};  

Variable (all,e,ENDW_COMM)(all,r,REGINC)
    qear(e,r) # endowment quantity index in aggregate region r #;
Equation E_qear
# quantity index for supply of good i in region r #
(all,e,ENDW_COMM)(all,r,REGINC)
    0 = sum{rr,REG:MREGINC(rr)=r, VOM(e,rr) * [qear(e,r) - qo(e,rr)]};

Variable (all,e,ENDW_COMM)(all,r,REGINC)
    pear(e,r) # endowment quantity index in aggregate region r #;
Equation E_pear
# price index for supply of good i in region r #
(all,e,ENDW_COMM)(all,r,REGINC)
    0 = sum{rr,REG:MREGINC(rr)=r, VOM(e,rr) * [pear(e,r) - pm(e,rr)]};  

Variable (all,e,ENDW_COMM)
    qewld(e) # endowment quantity index world #;
Equation E_qewld
# quantity index for supply of good i in region r #
(all,e,ENDW_COMM)
    0 = sum{r,REG, VOM(e,r) * [qo(e,r) - qewld(e)]};
  
Variable (all,e,ENDW_COMM)
    pewld(e) # endowment price index world #;
Equation E_pewld
# quantity index for supply of good i in region r #
(all,e,ENDW_COMM)
    0 = sum{r,REG, VOM(e,r) * [pm(e,r) - pewld(e)]};
  
Variable (all,i,ENDW_COMM)(all,j,BACT)(all,r,REGINC)
    qfear(i,j,r) # factor demand by broad activities and aggregate region #;
Equation E_qfear 
# factor demand by broad activities and aggregate region #
(all,i,ENDW_COMM)(all,j,BACT)(all,r,REGINC)
    0 = sum{a,ACTS:MACT(a)=j, sum{rr,REG:MREGINC(rr)=r, 
            ID01[EVFA(i,a,rr)] * [qfear(i,j,r) - qfe(i,a,rr)]}};

Variable (all,i,ENDW_COMM)(all,j,BACT)(all,r,REGINC)
    pfear(i,j,r) # factor price by broad activities and aggregate region #;
Equation E_pfear 
# factor demand by broad activities and aggregate region #
(all,i,ENDW_COMM)(all,j,BACT)(all,r,REGINC)
    0 = sum{a,ACTS:MACT(a)=j, sum{rr,REG:MREGINC(rr)=r, 
            ID01[EVFA(i,a,rr)] * [pfear(i,j,r) - pfe(i,a,rr)]}};

Variable (all,i,ENDW_COMM)(all,j,BACT)
    qfeawld(i,j) # global factor demand by broad activities #;
Equation E_qfeawld 
# global factor demand by broad activities #
(all,i,ENDW_COMM)(all,j,BACT)
    0 = sum{a,ACTS:MACT(a)=j, sum{r,REG, 
            ID01[EVFA(i,a,r)] * [qfeawld(i,j) - qfe(i,a,r)]}};

Variable (all,i,ENDW_COMM)(all,j,BACT)
    pfeawld(i,j) # global factor price by broad activities #;
Equation E_pfeawld 
# global factor price by broad activities #
(all,i,ENDW_COMM)(all,j,BACT)
    0 = sum{a,ACTS:MACT(a)=j, sum{r,REG, 
            ID01[EVFA(i,a,r)] * [pfeawld(i,j) - pfe(i,a,r)]}};

Variable (all,i,BACT)(all,r,REGINC)
    qxwacomreg(i,r) # exports by aggregate commodity i and aggregate region  #;
Equation E_qxwacomreg
# exports by aggregate commodity i and aggregate region  #
(all,i,BACT)(all,r,REGINC)
    0 = sum{c,TRAD_COMM:MCOM(c)=i, sum{rr,REG:MREGINC(rr)=r,   
            VXW(c,rr) * [qxwacomreg(i,r) - qxw(c,rr)]}};

Variable (all,i,BACT)(all,r,REGINC)(all,s,REGINC)
    qxsareg(i,r,s) # bilateral export of commodity i by aggregate regions #;
Equation E_qxsareg
# bilateral export of commodity i by aggregate regions #
(all,i,BACT)(all,r,REGINC)(all,s,REGINC)
    0 = sum{c,TRAD_COMM:MCOM(c)=i, sum{rr,REG:MREGINC(rr)=r, sum{ss,REG:MREGINC(ss)=s, 
            VXWD(c,rr,ss) * [qxsareg(i,r,s) - qxs(c,rr,ss)]}}};

Variable (all,i,BACT)(all,r,REGINC)(all,s,REGINC)
    pfobareg(i,r,s) # bilateral export price of commodity i by aggregate regions #;
Equation E_pfobareg
# bilateral export of commodity i by aggregate regions #
(all,i,BACT)(all,r,REGINC)(all,s,REGINC)
    0 = sum{c,TRAD_COMM:MCOM(c)=i, sum{rr,REG:MREGINC(rr)=r, sum{ss,REG:MREGINC(ss)=s, 
            VXWD(c,rr,ss) * [pfobareg(i,r,s) - pfob(c,rr,ss)]}}};

